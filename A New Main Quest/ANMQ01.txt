Quest: ANMQ01
DisplayName: ANMQ01
-- Message panels
QRC:

QuestComplete:  [1004]
<ce>      You kneel before King Eadwyre and beseech him to open the
<ce>      contents of the bag. At first amused, he smirks and asks a
<ce>     servant to do it for him. Faces turn pale in the throne room
<ce>        as they realize its contents and the evidence of orcs.
<ce>
<ce>       Eadwyre, consumed by rage, nearly orders your execution
<ce>       before Barenziah demands moderation. You repeat the tale
<ce>       told to you by the Redguard. The King calls his closest
<ce>             advisors to him and begins shouting at them.
<ce>
<ce>          "By the blood of my ancestors, the orcs will pay!
<ce>       They will bleed! They will suffer! I will not rest until
<ce>     Orsinium itself is reduced to ASHES!" He then points at you.
<ce>    "And you, foul messenger, LEAVE AT ONCE!" His court nervously
<ce>      hurries you along. It appears Wayrest Castle will not look
<ce>                forward to any future visits of yours.

RumorsPostFailure:  [1006]
All the royalty's talking about a coming war or calamity or something.
<--->
They say a big crisis is coming. You think it's true?

RumorsPostSuccess:  [1007]
All the royalty's talking about a coming war or calamity or something.
<--->
They say a big crisis is coming. You think it's true?

Message:  1010
<ce>          "Forgive me, my good %ra, let me just finish this
<ce>           business and I will be happy to attend to you."
<ce>
<ce>     This man is in the process of tearing up a parchment. In the
<ce>       dim light you make out the imprint of the Emperor's seal
<ce>        before his fingers pull it apart. This was the letter
<ce>                        you were sent to find!
<ce>
<ce>          "Now then, I have committed to memory the contents
<ce>       of the letter, and would be happy to repeat them to you
<ce>       for a small price. I'm sorry that I have to do this, but
<ce>         I simply must demand your help in a trifling matter.
<ce>
<ce>        "I represent the one true heir of the Emperor's power,
<ce>        and will speedily aid you in resolving Uriel Septim's
<ce>  errand if you will side with me. I swear it on my Redguard honor.
<ce>       All I need is a simple war between Orsinium and Wayrest,
<ce>   instigated by your hand. I have here in this bag the remains of
<ce>  Eadwyre's niece, along with clear evidence of Orcish involvement.
<ce>    Deliver it to him and say Gortwog left it outside the gates of
<ce>      Wayrest. All else should roll along smoothly from there."

Message:  1011
<ce>                     (ORCISH SKILL CHECK FAILED)
<ce>
<ce>      As the stranger ceases speaking, you hear Orcish grunting
<ce>         behind you. It appears you've been followed. A small
<ce>    detachment of two orcs and a shaman maintain a short distance
<ce>       from you. They shout at you, urging some kind of action,
<ce>    but they are clearly not hostile. You simply don't understand
<ce>                     what they want to tell you.
<ce>
<ce>        The Redguard scowls at the sight of the orcs. His hand
<ce>           lights up with a fire spell, ready to scorch the
<ce>                  remaining fragments of the letter.
<ce>
<ce>       "Restore a burnt parchment? Impossible! Don't be a fool,
<ce>        agent! Help me slay them and then deliver this bag to
<ce>        Eadwyre! It's your only hope of serving the Emperor!"
<ce>
<ce>              You must make a decision. Do you side with
<ce>                 the man (yes) or with the orcs (no)?
<ce>
<ce>        (NOTE: This version of ANMQ only allows the Orc path.)
<ce>                (The man's path will be added later.)
<ce>              (Thank you for playing the beta version.)

Message:  1012
<ce>                     (ORCISH SKILL CHECK PASSED)
<ce>
<ce>      As the stranger ceases speaking, you hear Orcish grunting
<ce>         behind you. It appears you've been followed. A small
<ce>    detachment of two orcs and a shaman maintain a short distance
<ce>                              from you.
<ce>
<ce>       "Do not trust this man, %ra. He will not keep his word!
<ce>           He is with the Underking, enemy of all mankind!
<ce>          Help us slay him and we will restore the letter he
<ce>            holds in his hands. We will read it together!"
<ce>
<ce>        The Redguard scowls at the sight of the orcs. His hand
<ce>           lights up with a fire spell, ready to scorch the
<ce>                  remaining fragments of the letter.
<ce>
<ce>       "Restore a burnt parchment? Impossible! Don't be a fool,
<ce>        agent! Help me slay them and then deliver this bag to
<ce>        Eadwyre! It's your only hope of serving the Emperor!"
<ce>
<ce>              You must make a decision. Do you side with
<ce>                 the man (yes) or with the orcs (no)?
<ce>
<ce>        (NOTE: This version of ANMQ only allows the Orc path.)
<ce>                (The man's path will be added later.)
<ce>              (Thank you for playing the beta version.)

Message:  1013
<ce>           The man throws the bag at you before anyone can
<ce>          react. He casts a spell to teleport away, but not
<ce>           before invoking a pair of atronachs to help you
<ce>                            in the fight.

Message:  1014
<ce>        With a moment to yourself, you notice a note is pinned
<ce>             to the outside of the bag, which you detach.
<ce>
<ce>        "Once I hear of a new war, I will send word for you."
<ce>
<ce>              This appears to be your only way forward.

-Message:  1015
-<ce>     As you leave the castle, you hear troops being ordered into
-<ce>       regiments. Eadwyre suffers no hesitation in ordering the
-<ce>        war against Orsinium. For better or for worse, things
-<ce>                   are now irretrievably in motion.

Message:  1016
<ce>         The man's face contorts in a rage, but before he can
<ce>           react, the shaman responds with a silence spell.
<ce>           The Redguard's face turns pale. He can no longer
<ce>           destroy the parchment with fire or use whatever
<ce>        escape method he had planned. The orcs descend on him
<ce>               before he can harm the letter any more.

Message:  1017
<ce>        You quickly grab the letter scraps from the dead man's
<ce>        hands. However, the orcs surround you and request that
<ce>        you give them over. The shaman then places them on the
<ce>          floor and attempts a few spells. To your surprise,
<ce>              the pieces begin to rearrange themselves!
<ce>          Not all the harm can be reversed, but you're able
<ce>                    to read most of the contents.
<ce>
<ce>                   "Deares... a calamity i...on us.
<ce>        The Numi...is outside Direnni Tower. Secure the Tot...
<ce>             Guard it with you...ife. If ...ll possible,
<ce>         capture Diren...er itself or e... the whole Isle of
<ce>            Balfiera. Even Oblivion cannot do us such harm
<ce>                      as could this monstro..."
<ce>
<ce>       One of the orcs, even without knowing the common tongue,
<ce>     memorizes the written words. The shaman invokes invisibility
<ce>       on them all, and the troupe then makes haste to deliver
<ce>          the sayings to their leader, who will know how to
<ce>             understand them. They don't need the letter.
<ce>      At the very least, you can deliver what remains to Aubk-i.
<ce>        If the orcs know its contents, destroying it now would
<ce>                   only put you at a disadvantage.

Message:  1018
<ce>           As the Emperor originally intended, you show the
<ce>          letter to the Queen of Daggerfall. The rest of the
<ce>           court crowds around, squinting to see it as best
<ce>          they can. They begin to murmur about its meaning.
<ce>
<ce>              Aubk-i, clearly out of her depth, tries to
<ce>            speak with some authority. "I insist you show
<ce>            this parchment to the court sorcerer, Popudax.
<ce>            He left to visit the Mages Guild this morning.
<ce>                    If anyone will know concerning
<ce>                    this calamity, he it shall be.
<ce>
<ce>          "The rest of you, maintain silence. We trust that
<ce>             a single word shall not leave this castle."
<ce>                  The court patrons bow before her.

Message:  1019
<ce>                   "Deares... a calamity i...on us.
<ce>        The Numi...is outside Direnni Tower. Secure the Tot...
<ce>             Guard it with you...ife. If ...ll possible,
<ce>         capture Diren...er itself or e... the whole Isle of
<ce>            Balfiera. Even Oblivion cannot do us such harm
<ce>                      as could this monstro..."

Message:  1020
%qdt:
Perhaps I'm a fool, but I traded away my imperial seal of authority
to find the location of the Emperor's letter. It is somewhere
in ___tristlab_, on the Isle of Betony to the south.

Message:  1021
%qdt:
 I found the Emperor's letter, damaged though
 it may be. The orcs who followed me in repaired
 the parchment and read its contents.
 The best I can do now is deliver it to Queen Aubk-i
 in ___dagg_.

Message:  1022
%qdt:
 Queen Aubk-i asked me to deliver the Emperor's
 damaged letter to Popudax, who should be in
 the Mages Guild of the city of ___dagg_.

QBN:
Person _ukagent_ group Resident1 male
Person _ead_ named King_Eadwyre atHome
Person _aubki_ named Queen_Aubk-i atHome
Person _pop_ named Popudax atHome

Foe _orc1_ is Orc_shaman
Foe _orc2_ is Orc
Foe _orc3_ is Orc
Foe _atr1_ is Fire_atronach
Foe _atr2_ is Fire_atronach
Foe _uk_ is Nightblade

Clock _delay_ 0:00

Place _tristlab_ permanent Tristore_Laboratory2
Place _dagg_ permanent DaggerfallCastle

Item _bag_ small_sack
Item _empletter_ letter used 1019

--	Quest start-up:
    place npc _ukagent_ at _tristlab_
    pc at _tristlab_ do _inside_
    pc at _dagg_ do _dig_
    hide npc _pop_
    log 1020 step 0

variable _inside_
_talk_ task:
    clicked npc _ukagent_ say 1010
    start timer _delay_

variable _delay_
_skilled_ task:
    when skill Orcish is at least 30

_noorc_ task:
    when _delay_ and not _skilled_
    create foe _orc1_ every 0 minutes 1 times with 100% success
    create foe _orc2_ every 0 minutes 1 times with 100% success
    create foe _orc3_ every 0 minutes 1 times with 100% success
    prompt 1011 yes _orcpath_ no _orcpath_

_goorc_ task:
    when _delay_ and _skilled_
    create foe _orc1_ every 0 minutes 1 times with 100% success
    create foe _orc2_ every 0 minutes 1 times with 100% success
    create foe _orc3_ every 0 minutes 1 times with 100% success
    prompt 1012 yes _orcpath_ no _orcpath_

_ukpath_ task:
    say 1013
    place foe _atr1_ at _tristlab_
    place foe _atr2_ at _tristlab_
    change foe _atr1_ infighting true
    change foe _atr2_ infighting true
    change foe _atr1_ team 1
    change foe _atr2_ team 1
    change foe _orc1_ infighting true
    change foe _orc2_ infighting true
    change foe _orc3_ infighting true
    get item _bag_

_pinnote_ task:
    when _ukpath_ and not _inside_
    say 1014

_eadwyrerage_ task:
    toting _bag_ and _ead_ clicked
    give pc nothing
    end quest

_orcpath_ task:
    say 1016
    hide npc _ukagent_
    place foe _uk_ at _tristlab_
    change foe _orc1_ infighting true
    change foe _orc2_ infighting true
    change foe _orc3_ infighting true
    change foe _orc1_ team 1
    change foe _orc2_ team 1
    change foe _orc3_ team 1
    change foe _uk_ infighting true
    cast Silence spell on _uk_

_killuk_ task:
    killed _uk_
    say 1017
    remove foe _orc1_
    remove foe _orc2_
    remove foe _orc3_
    get item _empletter_
    log 1021 step 1

_deliver_ task:
    toting _empletter_ and _aubki_ clicked
    say 1018
    log 1022 step 2

variable _dig_
_endit_ task:
    when _deliver_ and not _dig_
    restore npc _pop_
    start quest ANMQ02
    end quest