-- Cartographers Quest
Quest: JHCG021
DisplayName: Tomb of the Unknown...
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHCG021

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHCG021

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHCG021

QuestFail:  [1003]
<ce>   Your assignment to scout the
<ce>   unmarked tomb has expired.

QuestComplete:  [1004]
<ce>        Not what you were expecting, right Pathfinder?
<ce>        We Cartographers make different maps for
<ce>        different purposes. The place we just sent you
<ce>        to won't be on ordinary maps, I can say that
<ce>        much. Take your pay, and see if anyone will
<ce>        believe what you've witnessed.

Message:  1010
<ce>        You realize a message has found its way
<ce>        into your belongings without your noticing.
<--->
<ce>        There's a letter in your pack which carries
<ce>        the compass seal of the Cartographers.

Message:  1011
  Pathfinder,
 
  We have a mission which will require you to scout
  a location I cannot name at this time. Please see
  me for directions on how to reach it. All I can say
  is that it is a tomb that carries great meaning for
  the people of High Rock. This assignment will
  last for =timer_ days.
 
                         _qg_
                         Master Assistant
                         The Cartographers
                         ___place_, ____place_

Message:  1012
<ce>        Pathfinder, well met. We are sending you
<ce>        to Menevia. Begin at the village of Westcastle.
<ce>        From the House of Dibella, walk straight south.
<ce>        Your destination will be visible after the second
<ce>        hill you cross. It's a small building made of brick
<ce>        with an odd-looking roof that stretches over it,
<ce>        like an awning. Venture inside if you wish for
<ce>        curiosity's sake, but I urge caution. We have
<ce>        lost enough people inside that cavern. I will
<ce>        hear from you before =timer_ days are up.

Message:  1013
<ce>        This must be the mysterious tomb.
<--->
<ce>        You see the tomb before you. Now to
<ce>        return to _qg_.

Message:  1030
%qdt:
 The Cartographers have sent me
 to scout a nameless tomb. I
 should speak to _qg_ at
 ___place_ in ____place_
 for directions on where it is.
 I have =timer_ days to see it
 and return.

Message:  1031
%qdt:
 The Cartographers instructed me
 on how to find the tomb. I should
 start in the village of Westcastle in
 Menevia. Beginning at the House of
 Dibella, I should walk south across two
 hills. The tomb should be in front of me
 then, a small brick building.

Message:  1099
<ce>  The place should be pretty easy to find.
<ce>  I don't know that it's more than a few hundred
<ce>  steps out. Follow the directions and come
<ce>  back here when you've found it.

QBN:
Item _reward_ gold range 700 to 799
Item _letter_ letter used 1011

Place _dung_ permanent LysandusTomb1
Place _place_ remote house2

Clock _timer_ 70.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
          start timer _timer_
          give pc _letter_ notify 1010
          place npc _qg_ at _place_
          pc at _dung_ set _inside_
          log 1030 step 0

_qgclicked_ task:
	clicked npc _qg_ 

_begin_ task:
     when _qgclicked_ and not _talked_ and not _found_
     say 1012
     reveal _dung_
     clear _qgclicked_
     log 1031 step 1
     start task _talked_

variable _talked_
_inside_ task:
     start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
         say 1003
         start quest JHCGFAL3
         end quest