-- Perpetual Quest
Quest: JHPQB01
DisplayName: Race to the Daedra
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHPQB01

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHPQB01

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHPQB01

QuestFail:  [1003]
<ce>          ERROR 1003 JHPQB01

QuestComplete:  [1004]
<ce> The Daedra slain, the Oghma Infinium clatters onto the ground.
<ce> Its powers are now yours to unleash.

Message:  1010
<ce> A page quietly offers you a letter and departs.

Message:  1011
%pcn, %ra in the service of Uriel Septim VII

  Please forgive my indiscretion. Lady Brisienna
spoke to me concerning your assignment to Daggerfall
and I have some information to offer as aid. If what
is herein contained is of no use to you, please
disregard it and forget that I have bothered you.

  In the region of ____dung_ some ruins by the name
of ___dung_ lie in the wilderness. The place has
been adopted as a lair by a specimen of Oblivion, a
Daedra Lord named =enemy_. This on its own does not
merit so much attention were it not that another agent
in the region reported to us that the foe possesses an
artifact of great power called the Oghma Infinium.
The short conversation the two eked out suggested that
the foe will spend six more months in the cave, and once
it leaves, it intends to take the Oghma Infinium with it.

  My involvement in this matter is to inform you concerning
the object's existence and entreat you to employ all possible
means to recover it. Surely it will aid you in your mission.

  This letter will cease to be in one week's time from delivery,
and I request that you jot down all information of use. A map
to ___dung_ is included on the opposite side of this page.

                The Emperor's warmest greetings,
                The Great Knight

Message:  1012
<ce> According to the Great Knight, in this cave
<ce> resides =enemy_. The Oghma Infinium awaits.

Message:  1013
<ce> =enemy_ merely laughs at your intrusion.
<ce> There will be only one way to retrieve the Oghma Infinium.

QBN:
Item _reward_ artifact Oghma_Infinium
Item _letter_ letter used 1011

Place _dung_ remote dungeon

Clock _timer_ 7.00:00
Clock _fail_ 180.00:00
Clock _offer_ 9.00:00 22.00:00

Foe _enemy_ is Daedra_lord

--	Quest start-up:
    start timer _offer_

_offer_ task:
    give pc _letter_ notify 1010
    have _letter_ set _read_
    pc at _dung_ set _inside_

_read_ task:
    start timer _timer_
    start timer _fail_
    place foe _enemy_ at _dung_
    reveal _dung_

_timer_ task:
    take _letter_ from pc

_fail_ task:
    end quest

_inside_ task:
    say 1012

_inj_ task:
    injured _enemy_
    say 1013

_kill_ task:
    killed _enemy_
    give pc _reward_
    make _reward_ permanent
    end quest