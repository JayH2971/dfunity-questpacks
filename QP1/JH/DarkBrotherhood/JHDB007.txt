Quest: JHDB007
DisplayName: General Protection Fault
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>            We rarely ask questions concerning why certain
<ce>           tasks are brought to us, and further still when
<ce>          they pay well. I will tell you nothing about this
<ce>             mark except that a bodyguard had to be hired
<ce>               for peace of mind. Show the poor fool in
<ce>                 ___res_ what we think of bodyguards.
                                     <--->
<ce>            There are times we find satisfaction in simply
<ce>            proving our superiority over every other force
<ce>                  moving across ____res_. What name
<ce>            inspires so much as half the fear as the Dark
<ce>            Brotherhood among the citizenry? Yet to ensure
<ce>               we remain more than a mere name, we must
<ce>                overpower those who wish to thwart us.
<ce>                  Go to ___res_ and slay a fool who
<ce>                        has hired a bodyguard.

RefuseQuest:  [1001]
<ce>            Perhaps you should work for the Fighters Guild
<ce>                 if you dislike dirtying your hands.

AcceptQuest:  [1002]
<ce>              The bodyguard is of little concern to us.
<ce>               Perhaps shattering its ego would provide
<ce>              you more satisfaction than killing it. The
<ce>                accounting is for the mark only. This
<ce>                    tragic duo will stay at _res_
<ce>                      in ___res_. Return here in
<ce>                 =timer_ days. Go with Mephala, %pct.

QuestFail:  [1003]
<ce>                  The Dark Brotherhood expected you
<ce>                      in ___questgiver_ by now.

QuestComplete:  [1004]
<ce>                  Well done, %pct. And what did you
<ce>              do to the incompetent guardian? A fitting
<ce>            resolution. The Night Mother will be pleased.

Message:  1019
<ce>                         You step into _res_,
<ce>                watching for this so-called bodyguard.

Message:  1020
<ce>                   The acrobat collapses in defeat.
<ce>                      The contract is fulfilled.

Message:  1025
<ce>                     You step into _res_ and are
<ce>              accosted by a voice coming from thin air.
<ce>
<ce>                 "%ra, cease. I can see your doings.
<ce>               The one you seek is under my protection.
<ce>                Continuing your fruitless mission will
<ce>             engender my wrath. Do not understimate me."
                                     <--->
<ce>                     You step into _res_ and are
<ce>              accosted by a voice coming from thin air.
<ce>
<ce>                "Stop. The one you seek is my ward and
<ce>                 I will avenge thirtyfold any injury
<ce>                   you produce. Turn and leave now,
<ce>                       lest you face my wrath."

Message:  1026
<ce>               The voice you previously heard returns.
<ce>
<ce>              "You cannot comprehend what you have done.
<ce>             Your life is forfeit. Suffer for your deed."
                                     <--->
<ce>               The voice you previously heard returns.
<ce>
<ce>               "Your end is come. Nothing can repay the
<ce>            crime you have committed. You will die, worm."

Message:  1030
%qdt:
 The Dark Brotherhood has sent
 me from ___questgiver_ to
 ___res_ to look for an
 fool and a bodyguard, who hide
 in _res_. I must at
 least kill the acrobat and return within
 =timer_ days for recognition.

QBN:

Person _questgiver_ group Questor male

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:01 00:02

Foe _acro_ is Acrobat
Foe _bguard_ is Barbarian

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _acro_ at _res_
	pc at _res_ set _inside_
	pick one of _e1_ _e2_

variable _e1_
variable _e2_
variable _inside_
_spawn_ task:
	when _inside_ and _e1_
	say 1019
	create foe _bguard_ every 0 minutes 1 times with 100% success

_extend_ task:
	when _inside_ and _e2_
	say 1025

_slain_ task:
	killed 1 _acro_ saying 1020

_extend2_ task:
	when _e2_ and _slain_
	start timer _delay_

_delay_ task:
	say 1026
-	start quest JHDBA00

_qgclicked_ task:
	clicked npc _questgiver_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	end quest

_timer_ task:
	say 1003
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_