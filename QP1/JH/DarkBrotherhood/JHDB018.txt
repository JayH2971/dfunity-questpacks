Quest: JHDB018
DisplayName: The Debt Collector
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  A mediocre creature living in %crn
<ce>  has acquired a debt exceeding six figures,
<ce>  obligating the creditors to turn to us.
<ce>  Death is now the fool's only future.
<ce>  You may carry it out at your leisure.

RefuseQuest:  [1001]
<ce>  Looking for courier work instead?

AcceptQuest:  [1002]
<ce>  Look for the owner of _res_, found in
<ce>  ___res_. Whoever it is will probably be armed
<ce>  since this isn't the first time someone's
<ce>  tried to settle this. Tell me the tale of
<ce>  this mark's dismemberment within =timer_ days.

QuestFail:  [1003]
<ce>  The Dark Brotherhood required your report on
<ce>  the delinquent homeowner in ___res_ by now.

QuestComplete:  [1004]
<ce>  Well done. As demonstrated, we fill an important role.
<ce>  Let no one swindle with impunity while we
<ce>  have agents to do our work.

Message:  1010
<ce>  This must be the well-armed debtor.

Message:  1011
<ce>  "No! Wait! Please, don't! I'm begging you! I'll give you
<ce>  anything! Here! In this box is the last bit of money
<ce>  I have, =reward_ gold! Take it and please just let me live!"

Message:  1012
<ce>  "Thank you! Oh, please, just give me a second. Let me put in
<ce>  the combination. It'll only take so long. You can watch me,
<ce>  see? I'm not going anywhere."
<ce>  
<ce>  The debtor turns several dials on a complex box nearby.
<ce>  Soon a satisfying click bounces against the walls.
<ce>  
<ce>  "Okay! It's done. It'll open in sixty seconds, okay?
<ce>  I'm out of here! Thank you! Goodbye!"
<ce>  
<ce>  You are left alone. One whole minute passes and the
<ce>  box's door springs open, revealing your bribe money.

Message:  1013
<ce>  Hopefully %crn's delinquents will learn
<ce>  to do better than this fool did. The accounting is complete.

Message:  1030
%qdt:
 The Dark Brotherhood of ___qgiver_
 has sent me to ___res_ to settle
 a delinquent debt in _res_.
 I need to report my success in =timer_ days.

QBN:
Person _qgiver_ group Questor male
Person _debtor_ faction The_Merchants

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _m1_ is Nightblade
Foe _m2_ is Vampire
Foe _m3_ is Werewolf

Item _reward_ gold range 2501 to 2799

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0
    pick one of _e1_ _e2_ _e3_
    pick one of _f1_ _f2_

_e1_ task:
    place npc _debtor_ at _res_

_e2_ task:
    place foe _m2_ at _res_

_e3_ task:
    place foe _m3_ at _res_

variable _f1_
variable _f2_
_ipr_ task:
  injured _m1_
  injured _m2_
  injured _m3_
  say 1010

_click_ task:
    clicked npc _debtor_

_norm_ task:
  when _click_ and _f1_
  say 1010
  hide npc _debtor_
  place foe _m1_ at _res_

_choice_ task:
    when _click_ and _f2_
    prompt 1011 yes _bribe_ no _dud_

_bribe_ task:
    say 1012
    hide npc _debtor_
    get item _reward_
    end quest

variable _dud_
_slain_ task:
	killed _m1_
    killed _m2_
    killed _m3_
    say 1013

_timer_ task:
	end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_ 

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	end quest 