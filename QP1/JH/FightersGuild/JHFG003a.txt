Quest: JHFG003a
DisplayName: You're Hired

QRC:

QuestComplete:  [1004]
<ce>             Not bad. You've got some potential, I'd say.
<ce>               Maybe someday we'll have a use for you.
<ce>                   Here's your gold. We never met.

Message:  1010
<ce>               You find a letter among your belongings.

Message:  1011
%ra
 
  It's kinda funny. We were watching a
supposedly "very skilled assassin" down in
some hole who we were hoping
to hire for a job. Then you come along and
kill the wretch. We accept your qualifications.
Meet me in =firsttimer_ days if you want to make
some money.
 
                    _res_
                    ____res_

Message:  1012
<ce>             Good. I was worried you'd gotten cold feet.
<ce>             Or maybe you were just one of those lunatics
<ce>         that kills random people without thought for profit.
<ce>       We've got someone off in a remote piece of property who
<ce>         needs to die. Do it quick or someone's gonna notice
<ce>        and raise the alarm on you. You in for =reward_ gold?

Message:  1013
<ce>               Ah, you must merely be stupid. Goodbye.

Message:  1014
<ce>                Get to ___farm_. There's a wizard who
<ce>             maintains the property. Got on my nerves too
<ce>            many times now. Be back here with the killing
<ce>         done in =timer_ days. And if you're on the property
<ce>           too long you're gonna be caught, like an idiot,
<ce>                     and you won't find me here.

Message:  1015
%qdt:
 I've taken up a job to slay a wizard
 residing at ___farm_.
 If I do so without getting caught, I can
 return to ___res_ for my =reward_ gold
 within =timer_ days.

Message:  1016
<ce>                   If this is ___farm_, then you're
<ce>               sure to find the wizard here somewhere.

Message:  1017
<ce>          Your mark is slain. You should hurry out of here.

Message:  1018
<ce>       Screams rise from the small home, and a sense of magical
<ce>          communication rushes through the air. Your deed is
<ce>                   now known throughout ____farm_.

QBN:

Clock _delay_ 2.00:00 3.00:00
Clock _firsttimer_ 12.00:00 14.00:00
Clock _timer_ 30.00:00
Clock _alert_ 00:05

Place _farm_ remote poorhome
-Place _dung_ local dungeon
Place _res_ remote house

Person _tg_ faction The_Thieves_Guild

Foe _wiz_ is Mage

Item _letter_ letter used 1011
Item _reward_ gold range 2001 to 2999

---- Quest startup
    start timer _delay_

_delay_ task:
    give pc _letter_ notify 1010
    have _letter_ set _receive_
    place npc _tg_ at _res_

_receive_ task:
    start timer _firsttimer_

_talk_ task:
    clicked npc _tg_

_talk2_ task:
    when _talk_ and not _accept_
    stop timer _firsttimer_
    prompt 1012 yes _accept_ no _quit_

_quit_ task:
    say 1013
    destroy _tg_
    end quest

_accept_ task:
    say 1014
    log 1015 step 0
    start timer _timer_
    pc at _farm_ set _arrive_

_firsttimer_ task:
    end quest

_begin_ task:
    start timer _timer_
    pc at _farm_ set _arrive_

_arrive_ task:
    say 1016
    start timer _alert_
    start task _spawn_

_noalert_ task:
    when not _arrive_
    stop timer _alert_

_alert_ task:
    say 1018
    end quest

variable _spawn_
_spawnthere_ task:
    when _spawn_ and _arrive_
    create foe _wiz_ every 1 minutes 1 times with 100% success

_slain_ task:
    killed _wiz_
    say 1017

_talk3_ task:
    when _talk_ and _accept_ and not _slain_
    clear _talk_ _talk3_

_victory_ task:
    when _talk_ and _slain_
    give pc _reward_
    end quest

_timer_ task:
    end quest