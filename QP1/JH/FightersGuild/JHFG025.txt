Quest: JHFG025
DisplayName: Rescue Mission
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  The Mages Guild is looking for someone
<ce>  to go rescue one of their own that got
<ce>  lost out in some cave. It'll pay like
<ce>  any other job and you'll get in good with
<ce>  them and us. Wanna take it?

RefuseQuest:  [1001]
<ce>  Yeah, they're a pretty pompous bunch.

AcceptQuest:  [1002]
<ce>  Head on down to ___dung_.
<ce>  I'll mark it on your map. If you can bring
<ce>  _scholar_ here in =timer_ days
<ce>  I'll have =reward_ gold for you. Don't
<ce>  worry about whatever the mage
<ce>  was doing. Your one job is to get
<ce>  %g2 and yourself back here alive.

QuestFail:  [1003]
<ce>  I see you, but where's _scholar_?

QuestComplete:  [1004]
<ce>  Hey, _scholar_, glad to see
<ce>  you above ground! I'm _qgiver_.
<ce>  %pcf and I are from the Fighters Guild. 
<ce>  You head on back home and remember who
<ce>  kept you safe, you hear now?
<ce>  
<ce>  Great job getting the job done.
<ce>  These people have families, you know.
<ce>  I wish I could pay you more.

Message:  1010
<ce>  Who's there?! %oth. Well met,
<ce>  %pct. I have spent weeks searching
<ce>  for a missing atronach fruitlessly.
<ce>  The hideous creature has probably
<ce>  long departed this foul hole.
<ce>  Yes, it's high time we return
<ce>  to the civilized world. Lead the way.
<--->
<ce>  Who's there?! %oth. Well met, %ra.
<ce>  I have been here far too long.
<ce>  I've convinced myself that the
<ce>  _dummy_ I was sent to find
<ce>  does not exist at all. Please,
<ce>  lead the way to the civilized world.
<--->
<ce>  Aha! Oh. Well met, %ra.
<ce>  Did the Mages Guild send you to
<ce>  search for a lost scroll too?
<ce>  Oh, a recall order. Well, I can
<ce>  do that too. Show me the way to
<ce>  the exit, good friend.
<--->
<ce>  Ho! Good day, friend. I've been
<ce>  searching for weeks for a mummy
<ce>  possessing superlatively fresh
<ce>  wrappings, and simply cannot find it.
<ce>  The Mages Guild likely has no further
<ce>  use for them at this point, to
<ce>  be honest. I agree, we should
<ce>  find a way out of this place.

Message:  1030
%qdt:
 __qgiver_ of ___qgiver_
 will pay me =reward_ gold if I can
 find _scholar_, a member of
 the Mages Guild who's lost in
 ___dung_. I should be
 back in =timer_ days.

QBN:
Item _reward_ gold
Item _dummy_ organs

Person _qgiver_ group Questor male
Person _scholar_ faction The_Mages_Guild

Place _dung_ remote dungeon12

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
	start timer _timer_
	log 1030 step 0 
    place npc _scholar_ at _dung_
	reveal _dung_

_success_ task:
    clicked npc _scholar_
    say 1010
    hide npc _scholar_
    add _scholar_ face
    change repute with _scholar_ by +2

_pcgetsgold_ task:
    when _qgclicked_ and _success_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _success_
    say 1003
	clear _qgclicked_ _clearclick_ 

_timer_ task:
	end quest