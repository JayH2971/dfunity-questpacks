Quest: JHFG029
DisplayName: Mirage
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  There's a fellow in town who keeps comin'
<ce>  'round demanding a challenger for a fight.
<ce>  Offers gold and everything. I think it's
<ce>  gonna be harder than it looks, but it's
<ce>  there if you want it.

RefuseQuest:  [1001]
<ce>  I guess I'll just have to get used to this guy.
<ce>  No one else wants to try.

AcceptQuest:  [1002]
<ce>  Really? All right then. Go to
<ce>  _res_ here in town. Name's
<ce>  _pers_. All's I know is that
<ce>  you just wanna go in and start a
<ce>  fight. The twit's been aching for
<ce>  a good one for a long time, he says.
<ce>  I'll send him word that you'll be
<ce>  there within, say, two days. Let
<ce>  you rest up a little if you need it.

QuestFail:  [1003]
<ce>  _pers_ is in _res_.
<ce>  Head on over when you're ready.

QuestComplete:  [1004]
<ce>                       %oth, you can be lethal.
<ce>
<ce>              Please don't misunderstand. I can be a bit
<ce>              overly dramatic at times, I understand. My
<ce>               siblings tell me as much when they come
<ce>           around. Well, I suppose some explanation is due.
<ce>
<ce>             We belong to a clan who identify with a gift
<ce>         for illusion magic. It's not so much a clan in that
<ce>              we all trace to a common ancestor. We all
<ce>           adopt ourselves, you could say. In any case, we
<ce>             have an interest in expanding our sphere of
<ce>             influence. We are indeed shapeshifters, but
<ce>            we often find ourselves disadvantaged in other
<ce>           fields. Combat in general is something of a weak
<ce>            spot for us. We would enjoy counting you among
<ce>        our allies. If that sounds agreeable, perhaps someday we
<ce>           could send you occasional missives according to our
<ce>               needs. I assure you we will compensate you for
<ce>           the work we do together. Oh, the things we could
<ce>          accomplish with your skill! But for now, farewell.
<ce>           Thank you for listening to me with such respect.

Message:  1011
<ce>        "Thank you for coming. Please, tell me your full name.
<ce>
<ce>                 "Very well then, %pcn. I wish to test
<ce>         my skills and see if I emerge the victor. I ask that
<ce>        you hold nothing back. I have a small reward available
<ce>          for you if you succeed. And if you leave this building,
<ce>                           you forfeit our duel.
<ce>
<ce>                      "Let our duel begin!"
<ce>
<ce>                        _pers_ shouts a cantrip
<ce>                dramatically before disappearing. You
<ce>              brace yourself for the battle before you.

Message:  1014
<ce>                      The rat keels over. _pers_'s
<ce>              voice breaks your puzzled thought process.
<ce>
<ce>           "Well, you can kill a rat! So what? Take this!"

Message:  1015
<ce>              The bat drops to the ground pathetically.
<ce>                  From its mouth you hear _pers_ say,
<ce>
<ce>               "Warm-ups are over! Now we get serious!"

Message:  1016
<ce>               "No arachnophobe out of you! Get ready!"

Message:  1017
<ce>                From the giant fish's mouth you hear,
<ce>
<ce>                 "Come on, I want a real fight! Show
<ce>                   me the mettle of the Fighters Guild!"

Message:  1018
<ce>            The scorpion's body heaves and finally drops.
<ce>
<ce>             "Well, so be it! Let's see you handle this!"

Message:  1019
<ce>               "Not the first wereboar you've seen, eh?
<ce>                    But I'm just getting started!"

Message:  1020
<ce>                       "Hmph! Then watch this!"

Message:  1021
<ce>             "No more ghosts! See if you can beat this!"

Message:  1022
<ce>                "A strength that can even break iron!
<ce>                        But what about this?!"

Message:  1023
<ce>              The levitating dreugh drops to the ground.
<ce>
<ce>        "Pretty impressive, right? But playtime is now over!"

Message:  1024
<ce>                 "Breathing fire, even! But no more.
<ce>                       Here's something else!"

Message:  1025
<ce>             "Stone skin cracked. All my tricks are shot!
<ce>           You leave me with no choice. See my true form!"

Message:  1026
<ce>              The vampire tumbles to the ground. As with
<ce>          the other creatures, a voice comes from its mouth.
<ce>
<ce>              "It's been a long time since someone made
<ce>            it to the vampire. Your skill truly is memorable.
<ce>                       Come, see me once more."
<ce>
<ce>                       _pers_ appears once again.
<ce>             Now you can get to the bottom of the matter.

Message:  1030
%qdt:
 Someone named _pers_
 in _res_ of ___res_
 wants a challenge from a member
 of the Fighters Guild, and
 I have answered. I have two days
 to take on the fight for a gold reward.

Message:  1050
<ce>                   _pers_'s voice booms in your ears.
<ce>
<ce>                  "HA! I've proven my victory! Leave
<ce>                    %crn before my siblings and I
<ce>                leave your carcass nailed to a wall!"

QBN:
Item _reward_ gold range 1000 to 1000

Place _res_ local house

Clock _timer_ 2.00:00

Person _pers_ group Resident1 male

Foe _rat_ is Giant_rat
Foe _bat_ is Giant_bat
Foe _spid_ is Spider
Foe _fish_ is Slaughterfish
Foe _scorp_ is Giant_scorpion
Foe _were_ is Wereboar
Foe _gho_ is Ghost
Foe _wra_ is Wraith
Foe _atr_ is Iron_atronach
Foe _dreu_ is Dreugh
Foe _dra_ is Dragonling
Foe _garg_ is Gargoyle
Foe _vamp_ is Ancient_vampire

--	Quest start-up:
       start timer _timer_
       pc at _res_ set _inside_
       place npc _pers_ at _res_
       log 1030 step 0

variable _inside_
_clickp_ task:
       clicked npc _pers_

_begin_ task:
       when _clickp_ and not _f1_
       say 1011
       start task _f0_
       clear _clickp_

_f0_ task:
       hide npc _pers_
-       start task _f_
-
-_f_ task:
-       create foe _rat_ every 0 minutes 1 times with 100% success
        place foe _rat_ at _res_

_f1_ task:
       killed _rat_
       say 1014
       place foe _bat_ at _res_

_f2_ task:
       killed _bat_
       say 1015
       place foe _spid_ at _res_

_f3_ task:
       killed _spid_
       say 1016
       place foe _fish_ at _res_

_f4_ task:
       killed _fish_
       say 1017
       place foe _scorp_ at _res_

_f5_ task:
       killed _scorp_
       say 1018
       place foe _were_ at _res_

_f6_ task:
       killed _were_
       say 1019
       place foe _gho_ at _res_
       cast Silence spell on _gho_

_f7_ task:
       killed _gho_
       say 1020
       place foe _wra_ at _res_

_f8_ task:
       killed _wra_
       say 1021
       place foe _atr_ at _res_

_f9_ task:
       killed _atr_
       say 1022
       place foe _dreu_ at _res_

_f10_ task:
       killed _dreu_
       say 1023
       place foe _dra_ at _res_

_f11_ task:
       killed _dra_
       say 1024
       place foe _garg_ at _res_

_f12_ task:
       killed _garg_
       say 1025
       place foe _vamp_ at _res_
       cast Silence spell on _vamp_

_f13_ task:
       killed _vamp_
       say 1026
       restore npc _pers_

_victory_ task:
       when _clickp_ and _f13_
       give pc _reward_
       end quest

_fail_ task:
       when _f0_ and not _inside_ and not _f13_
       say 1050
       end quest

variable _timer_
_timeup_ task:
       when _timer_ and not _inside_
       end quest