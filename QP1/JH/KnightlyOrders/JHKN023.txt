Quest: JHKN023
DisplayName: Counting Crows
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  The formidable outlaw known as the Crow has surfaced
<ce>  in %crn. We need someone well-armed to apprehend
<ce>  him and bring him to trial here. Will you do so?

RefuseQuest:  [1001]
<ce>  Well, I suppose I can enlist someone more valiant.

AcceptQuest:  [1002]
<ce>  His hideout is _res_ of ___res_.
<ce>  The man is wanted in several provinces for indescribable
<ce>  crimes against civilization. Use any means available to
<ce>  subdue him. I wish you could avoid the lethal method,
<ce>  but ultimate resistance calls for the ultimate prybar.
<ce>  However you do it, return here within =timer_ days
<ce>  for his trial. %god watch over you.

QuestFail:  [1003]
<ce>  _qgiver_ needed your report on the Crow before the trial.

QuestComplete:  [1004]
<ce>  A body double? What a shame. This tends to be his
<ce>  manner of evasion, but we hoped this would be the
<ce>  time we succeeded. Your work will be counted as
<ce>  a victory, my good %pct, though he has escaped us.
<ce>  Thank you for doing a knight's work.

Message:  1010
<ce>  The Crow grins at you inhumanly, his face stretching
<ce>  beyond ordinary proportions. His head begins to pull out
<ce>  toward both sides, and his eyes turn bright red.
<ce>  Once his skin turns a different color it is well
<ce>  apparent that this is not the Crow, but a well-placed
<ce>  diversion. Your task will no doubt require slaying this
<ce>  foe before reporting the matter to _qgiver_.

Message:  1011
<ce>  The Crow has escaped but this grants him one fewer place
<ce>  to hide. _qgiver_ will want to know this.

Message:  1030
%qdt:
 _qgiver_ of ___qgiver_
 has given me orders to arrest the Crow,
 an infamous villain in ____qgiver_
 and other provinces who is hiding in
 ___res_ at _res_.
 I am permitted to use any means necessary and
 return before =timer_ days have passed.

QBN:
Foe _q1_ is Gargoyle
Foe _q2_ is Ancient_vampire

Person _qgiver_ group Questor male
Person _crow_ named The_Crow

Place _res_ remote house1

Clock _timer_ 00:00 0 flag 1 range 0 2

--	Quest start-up:
	start timer _timer_
	log 1030 step 0 
  place npc _crow_ at _res_

_deliver_ task:
  clicked npc _crow_
  say 1010
  hide npc _crow_
  pick one of _g1_ _g2_

_g1_ task:
    place foe _q1_ at _res_

_g2_ task:
    place foe _q2_ at _res_

_slain_ task:
    killed _q1_
    killed _q2_
    say 1011

_victory_ task:
  when _qgclicked_ and _slain_
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _deliver_
	clear _qgclicked_ _clearclick_ 

_timer_ task:
  say 1003
	end quest