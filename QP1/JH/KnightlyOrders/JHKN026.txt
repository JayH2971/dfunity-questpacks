Quest: JHKN026
DisplayName: Spoiling the Plot
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  The Dark Brotherhood has struck again,
<ce>  trapping a child in a remote cave in
<ce>  %crn. We need a hero to rescue
<ce>  the poor youth. Will you go?

RefuseQuest:  [1001]
<ce>  We have many fine members who will leap
<ce>  at this prospect, I assure you.

AcceptQuest:  [1002]
<ce>  The deadline for the ransom is in =2dung_ days.
<ce>  Please find _child_ by that
<ce>  time. %g1 will be in ___dung_.
<ce>  Courage and honor, %pct.

QuestFail:  [1003]
<ce>  The ransom deadline for _child_ has passed.

QuestComplete:  [1004]
<ce>  Hail, _child_! Have you been
<ce>  properly introduced to your rescuer? %pg3 name
<ce>  is %pcf, and %pg1 is a true hero.
<ce>  Your family will be here within the hour,
<ce>  and these knights here will keep you safe.
<ce>  
<ce>  %pct, you have honored the name of
<ce>  our order this day. Fine work, my friend.

Message:  1020
<ce>  "H-hello? Who are you?"
<ce>  
<ce>  You explain your mission and convince
<ce>  _child_ to leave with you.
<ce>  A magical field surrounding %g2 fades away
<ce>  once %g1 leaves its circle. Any pursuers
<ce>  from the Dark Brotherhood will now
<ce>  know the ransom is failing.
<--->
<ce>  "I'm scared. What do you want?"
<ce>  
<ce>  You explain your mission and convince
<ce>  _child_ to leave with you.
<ce>  A magical field surrounding %g2 fades away
<ce>  once %g1 leaves its circle. Any pursuers
<ce>  from the Dark Brotherhood will now
<ce>  know the ransom is failing.

Message:  1030
%qdt:
 _qgiver_ has sent me to
 ___dung_ to frustrate a ransom
 attempt by the Dark Brotherhood.
 I am to rescue _child_
 within =2dung_ days' time and return
 %g2 to the order hall.

QBN:

Person _qgiver_ group Questor male
Person _child_ faction Children
Person _dummydb_ faction The_Dark_Brotherhood

Place _dung_ remote dungeon

Clock _2dung_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 30.00:00

Foe _enemy1_ is Assassin
Foe _enemy2_ is Rogue

--	Quest start-up:
	start timer _2dung_ 
	log 1030 step 0 
    place npc _child_ at _dung_
  	reveal _dung_

_found_ task:
	clicked npc _child_
    say 1020
    hide npc _child_
    create foe _enemy1_ every 15 minutes indefinitely with 15% success
    create foe _enemy2_ every 10 minutes indefinitely with 35% success
    stop timer _2dung_
    start timer _failsafe_

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _found_ 
	clear _qgclicked_ _clearclick_ 

_pcgetsgold_ task:
	when _qgclicked_ and _found_ 
	give pc nothing
    change repute with _dummydb_ by -10
    legal repute +4
	end quest 

_2dung_ task:
    say 1003
	end quest

_failsafe_ task:
    change repute with _qgiver_ by +3
    end quest