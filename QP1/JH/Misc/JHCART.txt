-- Perpetual Quest
Quest: JHCART
DisplayName: The Cartographers
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          JHCART CARTOGRAPHERS OFFER

RefuseQuest:  [1001]
<ce>          JHCART CARTOGRAPHERS REFUSAL

AcceptQuest:  [1002]
<ce>           JHCART CARTOGRAPHERS ACCEPTANCE

QuestFail:  [1003]
<ce>   You were given =timer_ days to start your
<ce>   work with the Cartographers and return.
<ce>   That time has expired.

QuestComplete:  [1004]
<ce>        Good work! We occasionally send our
<ce>        members on such trips to calculate distances
<ce>        and so on. Cartography is a science that
<ce>        must undergo continual development.
<ce>
<ce>        As I said, your early fees will be low.
<ce>        This time we only have _reward_ gold pieces
<ce>        allotted for you. But please be patient with
<ce>        us and let more funding come in. We hope
<ce>        you'll keep helping us. Who knows what could
<ce>        turn up in one of Tamriel's forgotten corners?
<ce>        So, we'll send for you when we need you.

Message:  1010
<ce>        A piece of paper gently blows past you.
<ce>        You grab it and pick it up.

Message:  1011
  The Cartographers' Association of the Iliac Bay
 
  A group of expert explorers and designers wish to
  chart some of the locations lost to memory that might
  have historical or resourceful value to the public.
  We are currently accepting applicants for new explorers
  at ___place_ for the next =1st_ days. Present
  personally to _qg_ at _place_. In
  your service to Tamriel, you will travel leagues innumerable,
  see locations forgotten by time, and unearth secrets
  the Archaeologists have dreamt of for decades.
 
  The Cartographers' Association is supported by donations
  from generous citizens and by a commission of local wealth,
  as chartered by each regional monarch.

Message:  1012
<ce>        Welcome! My name is _qg_, and I am
<ce>        the regional registrar for the Cartographers'
<ce>        Association of the Iliac Bay. I assume that by
<ce>        coming here, you show some interest in
<ce>        uncovering the mysteries of Hammerfell and
<ce>        High Rock, are capable of long-distance travel
<ce>        on a coin's drop, and can handle yourself under
<ce>        adverse and sometimes dangerous conditions.
<ce>        Now that I have warned you concerning the
<ce>        hazards of the cartographer's path, do you
<ce>        desire to follow it and aid us in mapping
<ce>        out Tamriel's past, present, and future?

Message:  1013
<ce>       Then I formally welcome you, %pcf,
<ce>       to the ranks of the Cartographers. Please allow
<ce>       me some short explanations. First, your actions
<ce>       will be your own while on duty. If you sleep on
<ce>       some street you're not supposed to or take
<ce>       something that doesn't really belong to you,
<ce>       you will be prosecuted and not us. The courts
<ce>       know this well and will not humor any excuse
<ce>       that involves naming us. Second, compensation
<ce>       is middling for a new recruit such as yourself
<ce>       and depends on the generosity of our donors.
<ce>       We hope to improve on it, but at the start you
<ce>       may only be earning your own travel expenses.
<ce>
<ce>       With that out of the way, allow me to present you
<ce>       your first assignment. This is indeed menial, but
<ce>       we must ascertain the skill level of each member
<ce>       by the work they do. Your first assignment is
<ce>       to find  _target_ in ___target_,
<ce>       a simple test. I'll give you =timer_ days to do that
<ce>      and return here.

Message:  1015
<ce>     Well, I hope you change your mind. You appear
<ce>     to be cut out for it, in my honest opinion.

Message:  1020
<ce>  From all you can tell, this is the place
<ce>  _qg_ told you to find. Now
<ce>  to report its location back to %g2.

Message:  1030
%qdt:
 _qg_, registrar of the
 Cartographers, has given me an
 entry exam of sorts to look for
 _target_ in ___target_.
 I must find it, enter it, and return
 to ___place_ within =timer_ days.

Message:  1099
<ce>  You will find _target_ in
<ce>  ___target_. Enter it to make
<ce>  sure and return here. You have =timer_ days
<ce>  to complete your entry exam.

QBN:
Item _reward_ gold range 60 to 100
Item _letter_ letter used 1011

Place _target_ remote random
Place _place_ remote library

Clock _1st_ 20.00:00 45.00:00
Clock _timer_ 16.00:00 20.00:00

Person _qg_ group Librarian

--	Quest start-up:
	start timer _1st_
          give pc _letter_ notify 1010
          place npc _qg_ at _place_
          pc at _target_ set _inside_

_qgclicked_ task:
	clicked npc _qg_ 

_enroll_ task:
     when _qgclicked_ and not _begin_
     prompt 1012 yes _begin_ no _refuse_
     clear _qgclicked_

_refuse_ task:
     say 1015
     clear _qgclicked_ _refuse_ _enroll_

_begin_ task:
    say 1013
    log 1030 step 0
    stop timer _1st_
    start timer _timer_

_inside_ task:
     start task _found_

_found_ task:
    say 1020

_reminder_ task:
    when _qgclicked_ and _begin_ and not _found_
    say 1099
    clear _qgclicked_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT0
    give pc _reward_
    end quest

_1st_ task:
         end quest

_timer_ task:
         say 1003
         end quest