Quest: JHTG003
DisplayName: A Little Mugging
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> We've got a bit more hands-on action this time.
<ce> Some land deed of some kind to a house over in
<ce> ___dummy_. It's kept by someone staying at
<ce> _dest_ in ___dest_. Mere pick-
<ce>  pocketing isn't gonna cut it with this one. A good impact,
<ce> non-lethal, is more likely to get it free. It's paying
<ce> =reward_ gold if you want in.

RefuseQuest:  [1001]
<ce>  Well, I'm sure I have easier stuff to offer.
<--->  
<ce>  Why? The "no killing" part turn you off?

AcceptQuest:  [1002]
<ce> And we have our %ra. Get it back here in =queston_ days.
<ce> Make sure you go at night, like after ten. Less ruckus
<ce> that way. And I'm just gonna say this once: if you
<ce> kill the carrier, there will be no forgiveness for you.
<ce> At least not on my part. I'll have the money here after.
<ce> Get to it.

QuestComplete:  [1004]
<ce>  There it is. This kind of thing can make you
<ce>  a person of influence. Well done. Here's your
<ce>  gold. I'll try to arrange these kinds of things
<ce>  for you in the future.

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ told me to go
 to ___dest_ to steal a
 land deed from _dest_.
 I should go in after midnight.
 I can't kill the carrier, but some fighting
 might have to be done to get it free.
 I've got =queston_ days to get it done.


Message:  1020
<ce>  "%oth!" 
<ce>  
<ce>  A paper falls from your enemy's pack.
<ce>  You now have the deed.

Message:  1021
<ce> With the killing blow made, you wonder
<ce>  about your future with the Thieves Guild.

Message:  1030
<ce> You read a section of the parchment at random.
<ce> 
<ce> "...under retainer to lawfully present aforementioned
<ce> conditions to one _dummy_, being heretofore held
<ce> in honorable reputation before the people of ____qgiver_,
<ce> and there being no objection from aforementioned
<ce> region and its duly sworn representatives, wishes to
<ce> ensure that no manner of misrepesentation be heretofore..."
<--->
<ce> You read a section of the parchment at random.
<ce> 
<ce> "...hereby deeded to one _dummy_, having been
<ce> proven a worthy citizen of the region of ____qgiver_,
<ce> having committed neither murder nor embezzlement nor
<ce> theft nor distinctive deceit nor calumny nor manipulation
<ce> of the aged nor illegal spelunking nor defaming of the
<ce> royal ladies nor intoxication at improper hours nor
<ce> arson nor practice of necromancy nor opening of a..."
<--->
<ce> You read a section of the parchment at random.
<ce> 
<ce> "...with no exceptions as to material weight of value.
<ce> Nor has the Bank of ____qgiver_ placed a lien on
<ce> the property preventing this transfer. Nor has a duly
<ce> authorized judge found _dummy_ to be
<ce> unworthy of such transfer based on previous conduct, a
<ce> summary of which is presented in paragraph _falsetimer_.
<ce> Wherefore, as the marriage of residents of two provinces
<ce> of Tamriel is not forbidden, it is hereby enjoined that the..."

Message:  1040
<ce>  A middle-aged figure wearing a hood offers you
<ce>  a letter and departs without a word.

Message:  1041
%ra,
 
Would you like to make =bigmoney_ gold pieces? Bring
that deed to me. You know _qgiver_ has no
legitimate claim to it, nor %g3 client. My agent will
receive it at ___divers_. Look inside
_divers_.

Message:  1042
<ce> Very wise decision. The Thieves Guild is usually
<ce> reliable, and yet on occasion gets in over its head.
<ce> You have saved everyone a great headache.
<ce> My master thanks you, though %g1 could not
<ce> be here. Farewell.

Message:  1045
<ce> Everyone! The %ra's here! Get over here!
<--->
<ce> Ah, there you are. Thank you for coming. Reward
<ce> money? No one told me about that. I'll be taking that
<ce> deed from you in just a moment.

Message:  1050
<ce> No, I had already heard. Spymaster's good for that.
<ce> You're pathetic. I'll take the deed but you get nothing.
<ce> Get lost.

Message:  1055
<ce> This was a waste of time. Now to get to _qgiver_.

QBN:

Item _deed_ letter used 1030
Item _reward_ gold range 351 to 599
Item _falsetimer_ gold range 4 to 8
Item _lett_ letter used 1041
Item _bigmoney_ gold range 1401 to 2499

Person _qgiver_ group Questor male
Person _dummy_ group Local_3.0
Person _alt_ group Local_4.1

Place _dest_ remote tavern
Place _divers_ remote house1

Foe _mark_ is Burglar
Foe _ambush_ is 3 Barbarian

Clock _queston_ 00:00 0 flag 17 range 1 2
Clock _cd_ 00:00

--	Quest start-up:
	start timer _queston_ 
	log 1010 step 0 
  pick one of _E.01_ _E.02_
  pc at _dest_ set _inside_

variable _E.01_
variable _E.02_
variable _F.01_
variable _F.02_
variable _inside_

_day_ task:
 daily from 7:00 to 22:00

_spawn1_ task:
 when not _day_ and _inside_
 start task _spawn2_

_spawn2_ task:
 place foe _mark_ at _dest_

_steal_ task:
  injured _mark_
  say 1020
	get item _deed_

_startcd_ task:
 when _steal_ and _E.02_
 start timer _cd_

_cd_ task:
 give pc _lett_ notify 1040
 pick one of _F.01_ _F.02_
 place npc _alt_ at _divers_

_murder_ task:
  killed _mark_
  say 1021
  change repute with _qgiver_ by -20

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _steal_ 
	clear _qgclicked_ _clearclick_ 

_alttalk_ task:
  clicked npc _alt_

_alttalk1_ task:
  when _alttalk_ and _F.01_
  get item _bigmoney_
  say 1042
	end quest

_alttalk2_ task:
  when _alttalk_ and _F.02_	
  say 1045
  hide npc _alt_
  create foe _ambush_ every 1 minutes 1 times with 100% success

_finishalt_ task:
  killed 3 _ambush_
  say 1055

_S.13_ task:
  when _qgclicked_ and _steal_ and not _murder_
  take _deed_ from pc
	give pc _reward_ 
	end quest 

_S.14_ task:
  when _qgclicked_ and _murder_
  say 1050
	end quest

_queston_ task:
	end quest 