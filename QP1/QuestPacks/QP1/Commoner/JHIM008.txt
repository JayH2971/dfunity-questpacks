Quest: JHIM008
DisplayName: Only a Plank Between One and Perdition
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          %ra, I need help, and quickly. For the past several
<ce>          nights I have suffered dreams of a terrifying sort, all
<ce>          related to a coven of witches I believe I offended
<ce>          a fortnight ago. In these dreams I see myself being
<ce>          carried away into death by spirits of the world beyond.
<ce>          Please help me right my wrong, that perhaps I may
<ce>          be spared of this offense I have committed.

RefuseQuest:  [1001]
<ce>          Then I must soon write my will, if only to distract me.

AcceptQuest:  [1002]
<ce>          Then let us hurry. I desire to speak to one member of
<ce>          the coven, _wit1_, who at least tolerates my presence.
<ce>          Please take me to ___loc1_ to see her. If I have
<ce>          any hope of averting disaster, it will be through speaking
<ce>          to her. I will give you everything I have if we can hurry
<ce>          about this without delay. I am ready to go now.

QuestFail:  [1003]
<ce>          _questgiver_ gurgles and slumps down on the ground.
<ce>          The witches' death curse has come to pass.

QuestComplete:  [1004]
<ce>          The witch silently puts the _organs_ into a rough
<ce>          hide bag behind her. Then she begins muttering an incantation
<ce>          that begins slowly and increases in speed and fury. She stops
<ce>          after about fifteen seconds and looks away. The burden taken
<ce>          from _questgiver_ is palpable. He speaks to you.
<ce>          
<ce>          "%pcf, I owe my life to you. I will publish it all
<ce>          across ___questgiver_ for the next five years,
<ce>          you are truly an honorable %ra! Take all the gold
<ce>          I saved for this journey, and my blessing."

Message:  1010
<ce>       Ah, the fool _questgiver_. Tell me, what hast
<ce>       thou come to accuse us of this time? Or will it be
<ce>       mere thievery? Do not believe we take lightly the
<ce>       "borrowing" of a fresh Daedra heart or _organs_,
<ce>       and certainly not when prepared for ritual. If thou hast
<ce>       a finger's length of sincerity within that walking corpse
<ce>       of thine, flee thou to ___loc2_. Our dear sister
<ce>       _wit2_ has wept under the moonlight for the
<ce>       cruelty thou hast shown for far too long. The curse which
<ce>       thou carriest will be delayed for a span, that thou
<ce>       mayest prove thy sincerity. Now be gone, sick cur.

Message:  1011
<ce>       Oh, pathetic _questgiver_. We should have
<ce>       sooner made thee crawl like a dog after what thou
<ce>       didst to us. Thou shalt earn thy penitence, though
<ce>       it leave thee inches from death as it pleases us.
<ce>       Be thou moved to ___apoc_, that thou
<ce>       mayest restore unto us for the _organs_ thou
<ce>       didst steal. An adequate specimen lies within
<ce>       _apoc_. We will forgive thee the Daedra heart,
<ce>       and thou shalt tell all ____apoc_ of the kindness
<ce>       and mercy of these dear sisters. Now move thy
<ce>       feet and bring the _organs_ to me with haste,
<ce>       lest death find thee at its leisure.

Message:  1012
<ce>       _questgiver_ whispers to you with joy.
<ce>       
<ce>       "Here it is! Let us take this to ___loc2_! Hurry!"

Message:  1030
%qdt:
 _questgiver_ of ___questgiver_
 has asked me to help %g2 alleviate
 a witch's curse that has been placed on
 him. We must first speak to _wit1_
 in ___loc1_.

Message:  1031
%qdt:
 _wit1_ told us to speak with a
 coven sister, _wit2_, who resides
 at _loc2_ in ___loc2_,
 to prove our sincerity.

Message:  1032
%qdt:
 _wit2_ requires _questgiver_
 to bring some _organs_ to replace what
 %g1 previously stole. We must take it
 from _apoc_ in ___apoc_
 to her in ___loc2_.

QBN:
Item _reward_ gold range 601 to 799
Item _organs_ organs

Person _questgiver_ group Questor male
Person _wit1_ faction The_Glenmoril_Witches female
Person _wit2_ faction The_Glenmoril_Witches female

Place _loc1_ remote house1
Place _loc2_ remote house2
Place _apoc_ remote apothecary

Clock _2loc1_ 00:00 0 flag 1 range 0 2
Clock _2loc2_ 00:00 0 flag 1 range 0 2
Clock _2apoc_ 00:00 0 flag 1 range 0 2
Clock _extend_ 5.00:00

--	Quest start-up:
	start timer _2loc1_ 
	log 1030 step 0 
          hide npc _questgiver_
          add _questgiver_ face
          place npc _wit1_ at _loc1_

_talk1_ task:
         clicked npc _wit1_
         stop timer _2loc1_
         say 1010
         log 1031 step 1
         start timer _2loc2_
         place npc _wit2_ at _loc2_

_talk2_ task:
         clicked npc _wit2_
         stop timer _2loc2_
         say 1011
         log 1032 step 2
         start timer _2apoc_
         place item _organs_ at _apoc_

_get_ task:
         clicked item _organs_
         say 1012

_victory_ task:
         toting _organs_ and _wit2_ clicked
         give pc _reward_
         end quest

_2loc1_ task:
         say 1003
         destroy _questgiver_
         end quest

_2loc2_ task:
         say 1003
         destroy _questgiver_         
         end quest

_2apoc_ task:
         start timer _extend_

_extend_ task:
         say 1003
         destroy _questgiver_
         end quest