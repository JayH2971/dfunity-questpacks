Quest: JHIM105
DisplayName: Brawl Pals
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                  My name is _qgiver_. My fool of a
<ce>         half-sibling got into a bar fight last night and now
<ce>         stands to return and maintain our "family honor." I
<ce>          personally don't care at all about how many faces
<ce>           get beaten in, but I promised my late father on
<ce>         his deathbed that I'd take care of his family. There
<ce>          are =reward_ gold pieces in it for you if you head
<ce>              over to _tavern_ and help with the fight.

RefuseQuest:  [1001]
<ce>                    He might be able to handle it
<ce>                        on his own. I hope so.

AcceptQuest:  [1002]
<ce>                  Fine. Here, this is _family_. You
<ce>            two should get along just fine. I figure now's
<ce>                 as good a time as any to get over to
<ce>                     _tavern_. The lowlifes over
<ce>             there are probably drunk out of their minds
<ce>            day and night anyhow. Help us out, and you'll
<ce>             get your payment and I'll speak well of you
<ce>                    any time someone comes asking.

QuestFail:  [1003]
<ce>                   "Forget you. I'm headed back to
<ce>                      _tavern_ to do it myself."

QuestComplete:  [1004]
<ce>             You take the money that is rightfully yours.

QuestorPostSuccess:   [1008]
Half-siblings are always trouble, believe it.

Message:  1010
<ce>                "Hi %pcf, thanks for going with me. I
<ce>              don't need a lot of help or anything, but
<ce>               a little back up doesn't hurt, does it?"
<ce>
<ce>                        _family_ packs a blade
<ce>                for the fight and follows you.

Message:  1011
<ce>                 As soon as you enter the tavern, two
<ce>             figures stand up and start shouting at you.
<ce>                Among the cacophany, you pick out that
<ce>                they're angry at your partner. A fight
<ce>                seems to be inevitable at this point.

Message:  1020
<ce>                 "You're a pretty good fighter, %pcf.
<ce>               I'll give you that, but you were kind of
<ce>                  weighing me down during the fight,
<ce>               and I think I'd feel cheated if I had to
<ce>               pay you. It felt more like I was keeping
<ce>                you safe, don't you agree? Let's just
<ce>               go our separate ways and call it done."
<ce>
<ce>                        Will you let _family_
<ce>                     get away without paying you?

Message:  1021
<ce>                     "%oth! What are you doing?!"

Message:  1023
<ce>              "That's right! I knew you'd agree with me.
<ce>                Maybe you can talk the bartender into
<ce>                giving you some free beer for getting
<ce>                    rid of those bozos. Bye, %ra."
<ce>
<ce>                   _family_ waves and slips out the
<ce>             door. At least %g gets to live one more day.

Message:  1025
<ce>              "Wow, that was incredible! You fight like
<ce>                  a demon on sursum! Take the money.
<ce>                   Just watching you thrash 'em was
<ce>                      worth this whole debacle!"
<ce>
<ce>                      _family_ hands you a cloth
<ce>               sack containing the _reward_ gold coins
<ce>               before heading to the owner for a drink.

Message:  1030
%qdt:
 _qgiver_ has asked
 me to accompany a half-sibling,
 _family_, to _tavern_
 to settle an overdue bar brawl.
 I'll get the payment from %g2
 after it's over with.

Message:  1099
<ce>                If I'm lucky, I'll get a more sensible
<ce>                 sibling after this. Some people need
<ce>                 to have the sense knocked into them.

QBN:
Item _reward_ gold range 101 to 149

Person _qgiver_ group Questor male
Person _family_ group Chemist

Place _tavern_ local tavern

Clock _timer_ 1.00:00
Clock _delay_ 00:01

Foe _enemy1_ is Monk
Foe _enemy2_ is Monk
Foe _fam_ is Archer

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _tavern_ set _inside_
    say 1010
    add _family_ face
    pick one of _honor_ _betray_

_inside_ task:
    start task _begin_

_begin_ task:
    say 1011
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    create foe _enemy2_ every 0 minutes 1 times with 100% success
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true

_kill1_ task:
    killed _enemy1_

_kill2_ task:
    killed _enemy2_

_success_ task:
    when _kill1_ and _kill2_
    start timer _delay_

_delay_ task:
    drop _family_ face

variable _honor_
_happy_ task:
    when _delay_ and not _betray_
    say 1025
    give pc _reward_
    end quest

variable _betray_
_sad_ task:
    when _delay_ and _betray_
    prompt 1020 yes _escape_ no _murder_

_escape_ task:
    say 1023
    end quest

_murder_ task:
    say 1021
    create foe _fam_ every 0 minutes 1 times with 100% success
    change foe _fam_ infighting true
    change foe _fam_ team 0

_deadfam_ task:
    killed _fam_
    give pc _reward_
    change repute with _qgiver_ by -10
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_timer_ task:
    say 1003
    drop _family_ face
    end quest

_clearclick_ task:
    when _qgclicked_
    say 1099
    clear _qgclicked_ _clearclick_