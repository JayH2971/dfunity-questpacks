Quest: JHIM110
DisplayName: Adventurer's Bounty
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>             My name is _qg_. I have come into possession
<ce>        of a map purporting to lead to some forgotten treasure
<ce>        in %crn. I consider myself incapable of recovering it,
<ce>      but I would be willing to sell it for the mere pittance of
<ce>            200 gold pieces. Would this interest you, %ra?
                                     <--->
<ce>            Good day, %ra. I have here a map leading to a
<ce>             forgotten treasure in %crn, which I would be
<ce>              willing to part with for 200 gold pieces.
<ce>                 Would you consider that a good deal?

RefuseQuest:  [1001]
<ce>                  %oth. Tell your friends about me.
                                     <--->
<ce>                What a pity, for I will not go lower.
                                     <--->
<ce>                    I suppose I should look for an
<ce>                   adventurer. Forgive the mistake.

AcceptQuest:  [1002]
<ce>               Good, very good. Talk to me in a moment
<ce>                   and we'll make the transaction.

QuestFail:  [1003]
<ce>               As authentic as the treasure map may be,
<ce>          someone is likely to have found its bounty by now.

QuestComplete:  [1004]
<ce>               This appears to be the fabled treasure.

QuestorPostSuccess:   [1008]
I presume the treasure hunt was successful. We both won, %ra.

Message:  1020
<ce>             Very good, thank you for waiting. I'll take
<ce>                     your 200 gold. That's good.
<ce>
<ce>              No refunds, I should state, but that's an
<ce>                 obvious assumption by all parties in
<ce>               transactions such as these. There is no
<ce>               hope of restoring it beyond its current
<ce>               condition. Take it, see if it makes you
<ce>                 rich beyond your wildest dreams, and
<ce>                   come tell me about it some time.
<ce>              My curiosity will itch at me until I hear
<ce>                 regarding the matter. Farewell, %ra.

Message:  1021
<ce>                I see you don't have 200 gold pieces.
<ce>              What a pity. I certainly hope to be taken
<ce>               seriously by the next potential client.

Message:  1025
Diary of _dummy_
Page _num_
 
... last remaining ... hope of relie ...
... murderous thieves will stop at not ...
... if you can r ... no expectati ...
... last of my belo ... deep under the ...
... ___dung_ ...
... lest the assassins get ...

QBN:

Person _qg_ group Questor male
Person _dummy_ group Local_3.0

Item _letter_ letter used 1025
Item _num_ gold range 14 to 49
Item _sack_ item class 1 subclass 10
Item _reward1_ magic_item
Item _reward2_ mineral
Item _reward3_ diamond
Item _reward4_ gold range 521 to 679
Item _reward5_ gold range 1835 to 2999

Place _dung_ remote dungeon

Clock _timer_ 90.00:00

--   Quest start-up:
  start timer _timer_

_start_ task:
  clicked _qg_ and at least 200 gold otherwise do _fail_
  say 1020
  get item _letter_
  pick one of _item1_ _item2_ _item3_ _item4_ _item5_ _item5_
  reveal _dung_
  place item _sack_ at _dung_

variable _item1_
variable _item2_
variable _item3_
variable _item4_
variable _item5_

_found_ task:
  clicked item _sack_

_vict1_ task:
  when _found_ and _item1_
  give pc _reward1_
  end quest

_vict2_ task:
  when _found_ and _item2_
  give pc _reward2_
  end quest

_vict3_ task:
  when _found_ and _item3_
  give pc _reward3_
  end quest

_vict4_ task:
  when _found_ and _item4_
  give pc _reward4_
  end quest

_vict5_ task:
  when _found_ and _item5_
  give pc _reward5_
  end quest

_timer_ task:
  say 1003
  end quest

_fail_ task:
  say 1021