Quest: JHIM119
DisplayName: Walk My Bear
--Concept by BadLuckBurt
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>     If you have a few hours to kill, maybe you can help me out.
<ce>         I have a pet bear that needs to go for a walk, but I
<ce>        sprained my ankle. Could you do it for =reward_ gold?

RefuseQuest:  [1001]
<ce>                My poor bear. Gonna get fat, you know?
                                     <--->
<ce>                            But he's tame!

AcceptQuest:  [1002]
<ce>                     "Thanks! I'll call for him."
<ce>
<ce>           The bear trundles in soon after and rubs against
<ce>               his owner's leg. _questgiver_ gives you
<ce>          a leash and shows you how to pull the bear around.
<ce>
<ce>                 "Ya wanna take him over to _tavern_?
<ce>            It's a good walk, not too far. That's what we
<ce>              usually do. And if my bear dies, by %god,
<ce>              I will make you pay. Don't take too long,
<ce>              you hear? Bears are lovable but you should
<ce>               bring him back in four hours, no later."

QuestFail:  [1003]
<ce>               The bear rumbles. With a burst of speed
<ce>             he takes off and breaks free of your grasp.

QuestComplete:  [1004]
<ce>          How was it? You both have a good time? People can
<ce>         be mean to a pet bear, but a few arrows through the
<ce>       lungs is how I solve it. You did good. Take this money.

RumorsDuringQuest:  [1005]
Sorry, is that a bear following you?
<--->
Keep that bear away.

QuestorPostSuccess:   [1008]
My bear says hello. He's at home.

QuestorPostFailure:  [1009]
Bear killer.

Message:  1010
<ce>                  This is _tavern_, as _questgiver_
<ce>       asked you. The bear sniffs the various familiar objects
<ce>                 nearby and lumbers back over to you.

Message:  1011
<ce>           "Hey! A bear! A bear's here! I'm gonna kill it!"
<ce>
<ce>               One patron, feeling particularly heroic,
<ce>                     tries to slay the pet bear.

Message:  1012
<ce>      The bear roars at something nearby. You see a pair of rats
<ce>        scurrying about, and the bear's rumbling has startled
<ce>                          them into action.

Message:  1013
<ce>           The bear starts moving about rather haphazardly.
<ce>          You see a small gash on one of its paws -- 
<ce>          perhaps it was diseased by the rats.
<ce>             There's a small note tucked into its collar.
<ce>
<ce>                 "If sick, please deliver to _priest_
<ce>                     at _temple_, ___questgiver_"

Message:  1014
<ce>                      I see _questgiver_'s bear.
<ce>                 Hello bear. Yes, I'll charge %g2 for
<ce>             the service. Let me cast the spell... Done.
<ce>               %god watch over you and your new master,
<ce>                            furry friend.

Message:  1015
<ce>               The bear slumps over and ceases moving.
<ce>                 It appears the disease acted faster
<ce>                          than you expected.

Message:  1016
<ce>               "Hey you! Idiot! You idiot! Stupid %ra!"
<ce>
<ce>                     Someone is calling for you.
<ce>
<ce>                "That bear was our pet! You killed it!
<ce>                            You die now!"

Message:  1017
<ce>            The bear suddenly gets startled by some noise
<ce>             in the distance. You struggle to hold on to
<ce>           the leash, but he breaks free.
<ce>                       In his confusion, he tries to attack you.

Message:  1018
<ce>           Slowly coming to his senses, the bear calms down
<ce>           and lies on the floor. You cautiously attach the
<ce>               leash to him once more.
<ce>                        He pretends that nothing ever happened.

Message:  1019
<ce>          The ranger manages to get in a shot while the bear
<ce>          is separated from you. He only manages to get out
<ce>             an agonized roar before he ceases breathing.
<ce>
<ce>                           "Yes! Got 'im!"

Message:  1020
<ce>              My bear looks sick. There's a note in his
<ce>                   collar that says to take him to
<ce>                        _temple_. Be a pal and
<ce>               do that, will you? My ankle's sprained.

Message:  1030
%qdt:
 I am walking a bear for
 _questgiver_, who asked
 me to go to _tavern_
 and return. I'll be paid
 =reward_ gold.

Message:  1031
%qdt:
 The bear appears sick. A note in
 his collar told me to take him to
 _priest_ at _temple_.

QBN:
Item _reward_ gold range 200 to 299

Person _questgiver_ group Questor male
Person _priest_ faction Generic_Temple

Place _tavern_ local tavern
Place _temple_ local temple

Clock _timeup_ 4:00
Clock _disease_ 0:20
Clock _bearattack_ 0:05
Clock _tavernfight_ 0:04
Clock _confront_ 0:03

Foe _angry_ is Ranger
Foe _rat1_ is Giant_rat
Foe _rat2_ is Giant_rat
Foe _owner_ is Acrobat
Foe _bear_ is Grizzley_bear

--	Quest start-up:
    log 1030 step 0
    pc at _tavern_ set _inside_
    start timer _timeup_

_inside_ task:
    start timer _tavernfight_
    say 1010
    start task _spawnhero_

_notinside_ task:
    when not _inside_
    stop timer _tavernfight_

variable _tavernfight_
_spawnhero_ task:
    say 1011
    create foe _angry_ every 0 minutes 1 times with 100% success
    change foe _angry_ infighting true

_killangry_ task:
    killed _angry_

_taverndead_ task:
    when _tavernfight_ and _inside_ and not _killangry_
    say 1019
    start task _bearisdead_

_bearisdead_ task:
    remove log step 0
    start timer _confront_
    hide npc _questgiver_

_act2_ task:
    when _spawnhero_ and not _inside_ and not _bearisdead_
    pick one of _a1_ _a2_

_a1_ task:
    say 1012
    create foe _rat1_ every 0 minutes 1 times with 100% success
    create foe _rat2_ every 0 minutes 1 times with 100% success
    change foe _rat1_ infighting true
    change foe _rat2_ infighting true

_kill1rat_ task:
    killed _rat1_

_kill2rat_ task:
    killed _rat2_

_killedrats_ task:
    when _kill1rat_ and _kill2rat_
    say 1013
    start timer _disease_
    log 1031 step 1
    place npc _priest_ at _temple_
    start task _diseasedbear_

variable _diseasedbear_
_disease_ task:
    say 1015
    remove log step 1
    start task _bearisdead_

_priestclick_ task:
    clicked npc _priest_

_healbear_ task:
    when _priestclick_ and _killedrats_
    say 1014
    stop timer _disease_
    clear _diseasedbear_

_a2_ task:
    say 1017
    create foe _bear_ every 0 minutes 1 times with 100% success
    start timer _bearattack_

variable _bearattack_
_killbear_ task:
    killed _bear_
    start task _bearisdead_

_pacifybear_ task:
    when _bearattack_ and not _killbear_
    say 1018
    remove foe _bear_

_confront_ task:
    say 1016
    create foe _owner_ every 0 minutes 1 times with 100% success
    change foe _owner_ infighting true

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _spawnhero_
    clear _qgclicked_ _clearclick_

_clearclick2_ task:
    when _qgclicked_ and _a2_ and not _pacifybear_
    clear _qgclicked_ _clearclick2_

_clearclick3_ task:
    when _qgclicked_ and _a1_ and _diseasedbear_
    say 1020
    clear _clearclick3_ _qgclicked_

variable _timeup_

_timeout_ task:
    when _timeup_ and not _bearisdead_
    say 1003
    end quest

_timeout2_ task:
    when _timeup_ and _bearisdead_
    end quest

_pcgetsgold1_ task:
    when _qgclicked_ and _a2_
    give pc _reward_
    end quest

_pcgetsgold2_ task:
    when _qgclicked_ and _a1_ and not _diseasedbear_
    give pc _reward_
    end quest