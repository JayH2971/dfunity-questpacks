Quest: JHDB014
DisplayName: Dark Works of Art
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          One curious request asks that we slay a particular
<ce>              person and deliver the body to our client.
<ce>            Smells like a necromancer to me. Is this a job
<ce>                        we can entrust to you?

RefuseQuest:  [1001]
<ce>               Ah, because you have standards, surely.

AcceptQuest:  [1002]
<ce>        Perhaps I should interject that I do not know whether
<ce>          the client is actually a servant of the dark arts.
<ce>           That is merely the sense this petition gives me.
<ce>          The corpse must be delivered with all vital organs
<ce>           intact to ___nhome_. The body must not decompose
<ce>          for more than =2nhome_ days. The request calls for
<ce>                    _mark_, resident of ___mhome_.
<ce>    I will warn you, however, that %crn's guard will not tolerate
<ce>     the sight of a %ra carrying a festering corpse around. Avoid
<ce>          all civilization until you have made the delivery.
<ce>                The Night Mother watch over you, %pct.

QuestFail:  [1003]
<ce>        The body has clearly decomposed beyond what the client
<ce>      could have asked for. You discard it and cut your losses.

QuestComplete:  [1004]
<ce>          A robed figure spots you from within the cottage.
<ce>                   Soon %g1 comes out to greet you.
<ce>
<ce>     "Yes? Oh, splendid. Well done, %ra. Surely a bit of payment
<ce>      is in order, I say. Take this as motivation to keep quiet,
<ce>       will you? I deal with people who simply cannot tolerate
<ce>          this work being noised abroad. Here now, take it."

Message:  1010
<ce>                            "Hello, yes?"
<ce>
<ce>               You catch _mark_ completely by surprise.
<ce>               The killing blow follows swiftly after.

Message:  1011
<ce>              You begin to carry the corpse. You should
<ce>               avoid civilized areas while it's on you.

Message:  1012
<ce>               You are spotted carrying _mark_'s corpse
<ce>             and the alarm is raised. A few peacekeepers
<ce>          come forward to carry out justice for your crime.

Message:  1030
%qdt:
 The Dark Brotherhood of ___qgiver_
 has contracted with me to slay
 _mark_ of ___mhome_ and
 deliver %g3 body to a place
 called ___nhome_. I must
  carry it for no more than =2nhome_ days.

QBN:

Person _qgiver_ group Questor male
Person _mark_ faction Generic_Temple
Person _dummynec_ faction The_Necromancers

Place _mhome_ remote house
Place _nhome_ remote poorhome

Item _reward_ gold

Clock _count_ 1:00
Clock _2nhome_ 6.00:00 8.00:00
Clock _failsafe_ 60.00:00

--	Quest start-up:
    start timer _failsafe_
    log 1030 step 0
    place npc _mark_ at _mhome_
    pc at _nhome_ do _arrive_

_enter_ task:
    when pc enters city
    clear _exit_

_exit_ task:
    when pc enters city
    clear _enter_

_slain_ task:
    clicked npc _mark_
    say 1010
    hide npc _mark_
    say 1011
    start timer _count_
    start timer _2nhome_

variable _count_
_alert_ task:
    when _count_ and _enter_
    say 1012
    setplayercrime Murder
    spawncityguards immediate

variable _arrive_
_reward_ task:
    when _slain_ and _arrive_
    give pc _reward_
    change repute with _dummynec_ by +5
    end quest

_2nhome_ task:
    say 1003
    end quest

_failsafe_ task:
    end quest