Quest: JHFG118
DisplayName: Rattling Bones
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                  Not your typical cemetery problem:
<ce>          the bones don't stay underground. Could be because
<ce>       of necromancer mischief, residual magic, ancient curses,
<ce>       what have you. We just need someone to head on over and
<ce>           kill skeletons until the ground stops vibrating.
<ce>                       Wanna take this one on?

RefuseQuest:  [1001]
<ce>               Skeletons can be pretty tough if you're
<ce>                     not equipped right, I know.

AcceptQuest:  [1002]
<ce>           It's called ___cemetery_, quite a way out there.
<ce>              People can't properly visit their dead for
<ce>              the skeletons roaming around, so make sure
<ce>           they're gone. The contract needs you to be back by =timer_ days,
<ce>          and they're paying out some dwarven greaves.
<ce>                            Safe travels.

QuestFail:  [1003]
<ce>           Your time to report success on the skeletons to
<ce>                   the Fighters Guild has run out.

QuestComplete:  [1004]
<ce>                 Well done! What weapon did you use?
<ce>        I tell people, you've got to be packing a blunt weapon
<ce>         of some kind. Swords and daggers are useless against
<ce>            whole bone. I'm just glad you're back, anyway.
<ce>       Take your reward, and see what else we've got available.

Message:  1010
%qdt:
 The Fighters Guild will pay me
 _reward_ gold to fulfill a contract.
 I need to destroy the roaming skeletons
 around ___cemetery_ until they
 stop appearing. I have =timer_ days
 to return to ___qgiver_ for my pay.

Message:  1011
<ce>          The ground indeed vibrates here, albeit slightly.
<ce>         The magic animating these skeletons is hard at work.
<ce>
<ce>       You could wait around for the skeletons to come together
<ce>  and bash them apart, or you could try to stop the revival process
<ce>   with magic of your own. Do you try to stop the enchantment (yes)
<ce>             or fight the skeletons after they form (no)?
<ce>
<ce>                 (SKILL CHECK: YES: Mysticism, difficulty high)

Message:  1012
<ce>        The dark magic giving power to these bodies runs out.
<ce>             The earth ceases moving. Your reward awaits.

Message:  1013
<ce>        You know better than to mess with this kind of power.
<ce>      You'll just have to wait around until the skeletons rise.

Message:  1014
<ce>                         (SKILL CHECK PASSED)
<ce>
<ce>              You concentrate and try to read the forces
<ce>            at work. You see the source of necromantic magic
<ce>           a few meters away, and cast a spell to disrupt it.
<ce>       Once the cycle fails to renew itself, the magick stops.
<ce>                  You can now return for your reward.

Message:  1015
<ce>                         (SKILL CHECK FAILED)
<ce>
<ce>              You concentrate and try to read the forces
<ce>              at work. You reach a conclusion and attempt to
<ce>        disrupt the spell. Too late, you realize that this is
<ce>       far beyond you. Some bones begin to rise from the earth,
<ce>        but far more smoothly than with the ordinary skeleton.
<ce>        This one glares at you with supernatural intelligence.
<ce>                    Then it utters a single word.
<ce>
<ce>                               "IDIOT."
<ce>
<ce>                   Your task to slay the skeletons
<ce>                      has gained a complication.

QBN:
Item _reward_ item class 2 subclass 2

Place _cemetery_ remote graveyard

Clock _timer_ 00:00 0 flag 17 range 0 2

Person _qgiver_ group Questor

Foe _skel_ is Skeletal_warrior
Foe _lich_ is Lich

--	Quest start-up:
    start timer _timer_
    pc at _cemetery_ set _inside_
    log 1010 step 0
    reveal _cemetery_
    update-quest-item _reward_ set-material Dwarven

variable _inside_
_spawn_ task:
    when _inside_ and not _victory_
    prompt 1011 yes _attempt_ no _flee_
    create foe _skel_ every 30 minutes indefinitely with 10% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _skel_ infighting true
    clear _infight_

_skilled_ task:
    when skill Mysticism is at least 70

variable _attempt_

_fastwin_ task:
    when _skilled_ and _attempt_
    say 1014
    start task _victory_

_bigfail_ task:
    when not _skilled_ and _attempt_
    say 1015
    create foe _lich_ every 0 minutes 1 times with 100% success
    change foe _lich_ infighting true

_flee_ task:
    say 1013

_slain_ task:
    killed 4 _skel_
    start task _victory_

_victory_ task:
    say 1012

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    start quest JHFGC04
    end quest

_timer_ task:
    end quest