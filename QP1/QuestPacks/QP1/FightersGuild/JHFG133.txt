Quest: JHFG133
DisplayName: Payment on Delivery
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>           Really basic one here, I say. It won't pay much,
<ce>         but it's still a proper job. Someone dropped
<ce>            off a bag of money here with a note, saying it
<ce>     needs to get across town. You'll be paid a bit for doing it.
<ce>                Wanna have a nice walk across ___res_?

RefuseQuest:  [1001]
<ce>            Yeah, life's gotta be more exciting than that.
<ce>                    I wonder how much is in there.

AcceptQuest:  [1002]
<ce>                  All right, take it. They just said
<ce>            it needs to get there by tomorrow, so I guess
<ce>          that's 24 hours from now. I dunno, they didn't say
<ce>          when. You'll be paid by _pers_, the person you're
<ce>               delivering it to. Look for %g2 at _res_.
<ce>             Enjoy the fresh air while you're out there.

QuestFail:  [1003]
<ce>           You needed to deliver the gold to _pers_ by now.

QuestComplete:  [1004]
<ce>               Ah, got the money, have you? Reliable as
<ce>         ever, the Fighters Guild is. Take a bit of our recent haul.
<ce>
<ce>                 By the way, the Thieves Guild prizes
<ce>              people who show discretion. Remember that.

RumorsDuringQuest:  [1005]
There's a knight camping out in a cemetery, killing anyone who tries to loot it.

QuestorPostFailure:  [1009]
I just needed you to deliver a bag of gold. How hard is it?

Message:  1011
<ce>        As you leave the guild hall, you see an elderly woman
<ce>                  waiting outside. She sees the bag
<ce>                    in your possession and speaks.
<ce>
<ce>             "%hnr, I can't prove it to you, but that bag
<ce>           is mine. It was stolen from my house last night,
<ce>          and it contains all the money my nephews saved up
<ce>          for the rest of the month. Food, fuel, everything,
<ce>              it all comes from that gold. Please, would
<ce>                       you give it back to me?
<ce>              My family won't visit until next week and
<ce>              I don't know what I would do without it."
<ce>
<ce>                Tears form in her eyes as she speaks.
<ce>            The choice is yours. Do you give her the sack?

Message:  1012
<ce>              You decline, and she cries out mournfully,
<ce>                 hobbling off in another direction.
<ce>
<ce>               You need to deliver this money to _res_.

Message:  1013
<ce>            She doesn't have proof, but you don't need any.
<ce>               You place the coin sack into her hands.
<ce>           She bows before you as much as her ancient frame
<ce>                   can handle and closes her eyes.
<ce>
<ce>            "Oh thank you, thank you, thank you kind %ra!
<ce>            I have nothing to pay you with, but my prayers
<ce>                to %god will be long and loud for you.
<ce>            I didn't know what I would do if you said no."
<ce>
<ce>            She departs toward her home. At least now you
<ce>           don't have to carry someone else's stolen goods.

Message:  1030
%qdt:
 __qgiver_ gave me the simple task
 of delivering a bag of coins to
 a person named _pers_ at
 _res_ in ___res_.
 I have 24 hours to do it.

QBN:
Item _reward_ item class 2 subclass 1
Item _sack_ small_sack

Person _qgiver_ group Questor male
Person _pers_ faction The_Thieves_Guild

Place _res_ local house

Clock _timer_ 1.00:00

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at any fighters do _inside_
    get item _sack_
    place npc _pers_ at _res_
    update-quest-item _reward_ set-material Dwarven

variable _inside_

_confront_ task:
    when not _inside_
    prompt 1011 yes _give_ no _refuse_

_give_ task:
    say 1013
    take _sack_ from pc
    end quest

_refuse_ task:
    say 1012

_pcgetsgold_ task:
    toting _sack_ and _pers_ clicked
    give pc _reward_
    change repute with _pers_ by +5
    end quest

_timer_ task:
    say 1003
    end quest