Quest: JHFG204
DisplayName: Til Death Do Them Part
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>    A vampire lord took over a private cemetery
<ce>  with his two wives. Got some kind of harem thing
<ce>  going on, I dunno. I'm not a vampire, don't ask me.
<ce>  The property owner is paying an ebony broadsword
<ce>  to whoever gets rid of them.

RefuseQuest:  [1001]
<ce>   I get you. Vampires are real nasty folk.

AcceptQuest:  [1002]
<ce>  Head off to ___cem_, which is %di.
<ce>  They're not going to come out into the
<ce>  daylight, obviously, so wait outside until
<ce>  they show up. Make sure they're absolutely
<ce>  dead, or whatever the equivalent of dead
<ce>  is for vampires, and I'll have your
<ce>  money waiting here. It's paying =reward_
<ce>  gold if you're back within =timer_ days.

QuestFail:  [1003]
<ce>                 The Fighters Guild sent you to clean
<ce>                        out ___cem_ by now.

QuestComplete:  [1004]
<ce>  Hey, you're getting good at this.
<ce>  When even vampires fall, there's not
<ce>  much else that can scare you.
<ce>    Here you go, with my compliments.

Message:  1010
<ce>  This is the right place, but it's too close to daytime.
<ce>  You'll need to stay around until it's later at night.

Message:  1011
<ce>  You are spotted immediately. Guttural hissing
<ce>  rises into the air as the lord and his
<ce>  consorts move in for the kill.

Message:  1012
<ce>  All three vampires are defeated.

Message:  1030
%qdt:
 __qgiver_ of ___qgiver_
 will pay me =reward_ gold if I
 slay a vampire lord and two
 female vampires that have
 taken over ___cem_ of ____cem_.
 I need to be back in =timer_ days
 to be rewarded.

QBN:
Item _reward_ item class 3 subclass 5

Place _cem_ remote graveyard

Person _qgiver_ group Questor

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _vampman_ is Ancient_vampire
Foe _vamw1_ is Vampire
Foe _vamw2_ is Vampire

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _cem_ set _outside_
    reveal _cem_
    update-quest-item _reward_ set-material Ebony

_day_ task:
    daily from 5:00 to 19:00

variable _outside_

_dayarrive_ task:
    when _outside_ and _day_
    say 1010

_nightarrive_ task:
    when _outside_ and not _day_
    start task _spawn_

_spawn_ task:
    say 1011
    create foe _vampman_ every 0 minutes 1 times with 100% success
    create foe _vamw1_ every 0 minutes 1 times with 100% success
    create foe _vamw2_ every 0 minutes 1 times with 100% success
    change foe _vampman_ infighting true
    change foe _vamw1_ infighting true
    change foe _vamw2_ infighting true
    change foe _vampman_ team 0
    change foe _vamw1_ team 0
    change foe _vamw2_ team 0

_killed1_ task:
    killed _vampman_

_killed2_ task:
    killed _vamw1_

_killed3_ task:
    killed _vamw2_

_victory_ task:
    when _killed1_ and _killed2_ and _killed3_
    say 1012

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _victory_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _victory_
	give pc _reward_
	end quest

_timer_ task:
	say 1003
	end quest