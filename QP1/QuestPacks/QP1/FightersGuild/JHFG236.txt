Quest: JHFG236
DisplayName: The Wayward Wizard
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                     Some conjurer took to a cave
<ce>            and sends out his golems to attack passers by,
<ce>        delivering him their loot when they die or surrender.
<ce>          The %t's backing this contract to kill the wizard,
<ce>           and is offering an enchanted item for doing it.

RefuseQuest:  [1001]
<ce>          Magical items are really good though, you'll see.

AcceptQuest:  [1002]
<ce>             Well then, let me mark ___dung_ on your map.
<ce>         I don't know how hard a wizard like that is to kill,
<ce>             but those atronachs are gonna pack a punch.
<ce>             Try not to spend too much time in the cave.
<ce>           Sleep outside if you have to, so they don't come
<ce>          hunting you. Be back here in =timer_ days with the
<ce>        news of the wizard's death, and I'll have your _item_
<ce>                          ready and waiting.

QuestFail:  [1003]
<ce>           The Fighters Guild needed you to report killing
<ce>                   the conjurer of ___dung_ by now.

QuestComplete:  [1004]
<ce>               Ha, I knew you'd do it! Yeah, good work.
<ce>                The Fighters Guild won't be beaten by
<ce>            some schmuck of a mage. And now, your reward.

QuestorPostfailure:  [1009]

Message:  1010
%qdt:
 _qgiver_ in ___qgiver_
 assigned me a contract
 from ____qgiver_'s nobility to
 kill a conjurer in a hole
 called ___dung_. If I
 return within =timer_ days,
 I'll get an enchanted _item_.

Message:  1011
<ce>                You can feel strong magic in the air.
<ce>                  The conjurer is somewhere in here.

Message:  1012
<ce>       With the mage dead, the atronachs will stop hunting you.

QBN:
Person _qgiver_ group Questor male

Place _dung_ remote dungeon9

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _anc_ is Sorcerer
Foe _e1_ is Flesh_atronach
Foe _e2_ is Ice_atronach
Foe _e3_ is Fire_atronach
Foe _e4_ is Iron_atronach

Item _item_ magic_item

--	Quest start-up:
    start timer _timer_
    log 1010 step 0
    place foe _anc_ at _dung_
    reveal _dung_
    pc at _dung_ set _inside_

_inside_ task:
    say 1011

_infight_ task:
    daily from 0:00 to 23:59
    change foe _e1_ infighting true
    change foe _e2_ infighting true
    change foe _e3_ infighting true
    change foe _e4_ infighting true
    clear _infight_

_spawn_ task:
    when _inside_ and not _slain_
    create foe _e1_ every 5 minutes indefinitely with 2% success
    create foe _e2_ every 8 minutes indefinitely with 3% success
    create foe _e3_ every 11 minutes indefinitely with 4% success
    create foe _e4_ every 13 minutes indefinitely with 5% success

_slain_ task:
    killed _anc_
    say 1012

_pcgetsgold_ task:
    when _qgclicked_ and _slain_
    give pc _item_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest