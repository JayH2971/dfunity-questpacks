Quest: JHFG238
DisplayName: The Unwanted Thief
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>  We've got an order from the %rt telling us to get rid
<ce>  of some member of the Thieves Guild here in town.
<ce>  Hangs out in a shop, I guess as a cover story.
<ce>  It doesn't pay much, but it's pretty easy.
<ce>  Want it?

RefuseQuest:  [1001]
<ce>     You're not afraid of the Thieves Guild, are you %pct?

AcceptQuest:  [1002]
<ce>     Great! I was worried about this one.
<ce>  Glad to get someone on it.
<ce>  I'm signing the order now, which means
<ce>  you've got 24 hours to get to _res_
<ce>  and kill this thief, whoever he or she is.
<ce>  Probably gonna be pretty easy to spot.
<ce>  Just get back in one piece, I'll have _gold_
<ce>  gold pieces here for you, straight from the %rt.

QuestFail:  [1003]
<ce>                    Your contract to slay the thief
<ce>                        in _res_ has gone delinquent.

QuestComplete:  [1004]
<ce>        Yeah, the Thieves Guild aren't much
<ce>  of fighters. If they'd just come on over we could
<ce>  teach 'em how to brawl, but you know how it is.
<ce>  Underworld temptations and all that.
<ce>  Well, enough rambling. Here's your gold.

Message:  1010
<ce>   You hear rustling and some nervous talk.
<ce>  Your presence here is definitely noticed.

Message:  1011
<ce>     The thief is slain, and you are yet alive.

Message:  1030
%qdt:
 _questgiver_ of the Fighters Guild
 will pay me _gold_ gold if I get to
 _res_ in ___res_
 and kill a hostile thief.
 I need to return to the guild hall in 24 hours.

QBN:
Person _questgiver_ group Questor male
Person _dummy_ faction The_Thieves_Guild

Place _res_ local shop

Clock _timer_ 1.00:00

Item _gold_ gold range 150 to 200

Foe _mage_ is Thief

--	Quest start-up:
	place foe _mage_ at _res_
	log 1030 step 0
    pc at _res_ set _inside_
    start timer _timer_

_inside_ task:
    say 1010
	change foe _mage_ infighting true

_slain_ task:
    killed _mage_
    say 1011
    change repute with _dummy_ by -10

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc _gold_
    start quest JHFGC01
	end quest

_qgclicked_ task:
    clicked npc _questgiver_

_timer_ task:
	say 1003
	end quest