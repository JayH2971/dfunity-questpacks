Quest: JHKN106
DisplayName: Momento Retrieval
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>               %pct, it falls upon the knights of this
<ce>           order to restore honor when it has been stolen.
<ce>             A complaint has come to our door concerning
<ce>              a knave who murdered an honest citizen and
<ce>              then stole his weapon, a family heirloom.
<ce>          We can hardly resist and still consider ourselves
<ce>                     defenders of %crn. Will you
<ce>               answer the call and retrieve the weapon?

RefuseQuest:  [1001]
<ce>             Then occupy yourself in something you value
<ce>                 more, %pcf. We have work to do here.

AcceptQuest:  [1002]
<ce>               Then you will descend into the depths of
<ce>                       ___dungeon_ and find the
<ce>               cruel beast who has done this crime. We
<ce>                     merely hope the _weapon_ is
<ce>             still with the fool, such that recovering it
<ce>             will be no great chore. Have done with this
<ce>            and bring it to me in fewer than =timer_ days.
<ce>                   Honor, virtue, and glory, %pcf.

QuestFail:  [1003]
<ce>

QuestComplete:  [1004]
<ce>                Excellent work, %pct. I shall have it
<ce>                       delivered to _res_ this
<ce>               very day, so soon as one of our squires
<ce>               returns from an errand. The family will
<ce>                 remember your name for generations.
<ce>                I assure you of it, honorable knight.

Message:  1012
<ce>                 (ATTRIBUTE CHECK FAILED: ENDURANCE)
<ce>
<ce>           As soon as you enter, you step on a false tile.
<ce>            A disgusting gas begins to shoot into the air.
<ce>       You don't seem to suffer any immediate effects from it,
<ce>            but you wonder what it might have done to you.

Message:  1013
<ce>                 (ATTRIBUTE CHECK PASSED: ENDURANCE)
<ce>
<ce>           As soon as you enter, you step on a false tile.
<ce>            A disgusting gas begins to shoot into the air.
<ce>             Your system is strong enough to fight it off
<ce>        until you reach cleaner air, and you emerge unscathed.

Message:  1020
<ce>                  The thief has been slain. You see
<ce>                    the _weapon_ among the goods.

Message:  1021
<ce>                  Wait! Stop! Why are you after me?!
<ce>                   Is it because of this _weapon_?
<ce>               They told me to hold on to it! I didn't
<ce>                  do anything, by %god! Please, I'll
<ce>               give you the weapon, but just let me go!
                                     <--->
<ce>                  Someone help! This %ra's going to
<ce>                  kill me! Please, stop! I promise,
<ce>                I didn't do anything! I'm just holding
<ce>                 onto this _weapon_ for someone else!
<ce>             I'll give it to you, but let me go, please!

Message:  1022
<ce>                   Your foe begins to sob bitterly
<ce>                       as you press the attack.

Message:  1023
<ce>               Your foe throws the weapon at your feet
<ce>                 and races away from you. By the time
<ce>                  you pick it up, you have lost all
<ce>                    trace of its previous holder.

Message:  1030
%qdt:
 I have been sent from
 ___qgiver_ to a
 foul hole, ___dungeon_,
 to track down a murdering thief
 who has stolen a family heirloom.
 I must bring it to _qgiver_
 in =timer_ days' time.

QBN:
Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon10

Clock _timer_ 00:00 0 flag 17 range 0 2

Place _res_ remote house

Foe _enemy_ is Nightblade

Item _weapon_ weapon

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _enemy_ at _dungeon_
	reveal _dungeon_
	pick one of _e1_ _e2_
	pc at _dungeon_ set _inside_

variable _inside_
_will_ task:
	when attribute Endurance is at least 45

_badwill_ task:
	when _inside_ and not _will_
	say 1012
	make pc ill with Red_Death

_goodwill_ task:
	when _inside_ and _will_
	say 1013

variable _e1_
variable _e2_
_inj_ task:
	injured _enemy_
	clicked foe _enemy_
	give item _weapon_ to _enemy_
	change foe _enemy_ infighting true

_ask_ task:
	when _inj_ and _e2_
	prompt 1021 yes _mercy_ no _kill_

_mercy_ task:
	get item _weapon_
	say 1023
	remove foe _enemy_

_kill_ task:
	say 1022

_slain_ task:
	killed 1 _enemy_ saying 1020

_victory_ task:
	toting _weapon_ and _qgiver_ clicked
	give pc nothing
	legal repute +2
	end quest

_timer_ task:
	end quest
