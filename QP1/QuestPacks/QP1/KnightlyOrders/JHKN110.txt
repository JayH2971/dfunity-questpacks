Quest: JHKN110
DisplayName: The Beguiler
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>  A demon from Oblivion has taken up residence in a ruined castle
<ce>  and caused great havoc of late to local citizenry. If you have
<ce>  means of harming daedrakind, we request that you take up this matter.

RefuseQuest:  [1001]
<ce>        Not a task to be taken lightly, I agree.

AcceptQuest:  [1002]
<ce>  ___dung_ is the name of the place. Find it that way, %di.
<ce>  This daedraling will defend itself, but the task falls to you
<ce>  to destroy it. We don't care how, so long as it is dead.
<ce>  Have done with the task within =timer_ days, presenting yourself
<ce>  here as proof.

QuestFail:  [1003]
<ce>  You were to destroy the daedra and return to __questgiver_ by now.

QuestComplete:  [1004]
<ce>  A trap was laid, you say? I'm glad we sent someone capable
<ce>  enough to survive it. A good knight is hard to come by.
<ce>  The people of %crn salute you, %pct.

Message:  1010
<ce>    The daedra emits a roar as it presses upon you.

Message:  1011
<ce>  The demon falls, but it points at you. A vision enters your mind,
<ce>  in which you see yourself falling dead within these ruins.
<ce>  
<ce>  "In two hours... you will join me..." the daedra laughs as it falls inert.
<ce>  
<ce>  Its enchantment bursts forth, and you feel foul power bind to you.
<ce>  If you can escape ___dung_ before the time is up, perhaps
<ce>  you can be saved yet.

Message:  1012
<ce>  You notice some kind of energy force in these ruins.
<ce>  The daedra must have been at work while you traveled here.

Message:  1013
<ce>  ATTRIBUTE CHECK FAILED: ENDURANCE
<ce>  
<ce>  Alas, two hours have passed and you are yet inside.
<ce>  Your body is twisted with excruciating pain,
<ce>  which you cannot survive. __questgiver_ will
<ce>  plan an extravagant funeral for you.

Message:  1014
<ce>  ATTRIBUTE CHECK SUCCEEDED: ENDURANCE
<ce>  
<ce>  Alas, two hours have passed and you are yet inside.
<ce>  Your body is twisted with excruciating pain,
<ce>  which you barely survive. A weaker soul would
<ce>  have died instantly.

Message:  1015
<ce>  You are free of the castle ruins, and no foul curse has
<ce>  befallen you. It looks like the daedra's prophecy failed.

Message:  1030
%qdt:
 __questgiver_ has assigned
 me to destroy a daedra somewhere inside ___dung_.
 I am to return to ___questgiver_ and report my success
 within =timer_ days. 

QBN:
Person _questgiver_ group Questor male

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _deathtimer_ 2:00

Place _dung_ remote dungeon11

Foe _enemy_ is Fire_daedra

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	reveal _dung_
    place foe _enemy_ at _dung_
    pc at _dung_ set _inside_

_inside_ task:
    say 1012

_inj_ task:
    clicked foe _enemy_
    injured _enemy_
    say 1010
    change foe _enemy_ infighting true

_slain_ task:
    killed _enemy_
    say 1011
    start timer _deathtimer_

_cleartimer_ task:
    when _slain_ and not _inside_ and not _deathtimer_
    stop timer _deathtimer_
    say 1015

variable _deathtimer_

_skilled_ task:
    when attribute Endurance is at least 90

_instantdeath_ task:
    when _deathtimer_ and not _skilled_
    say 1013
    reduce player health by 100 every 1 seconds can kill

_neardeath_ task:
    when _deathtimer_ and _skilled_
    say 1014
    reduce player health by 75 every 1 seconds 1 times

_victory_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +4
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
	end quest