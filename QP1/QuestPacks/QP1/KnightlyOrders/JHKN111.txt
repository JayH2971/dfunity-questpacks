Quest: JHKN111
DisplayName: A Knight's Tale
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                 We've been missing _kni_, one of our
<ce>         more veteran members, for several weeks now. No one
<ce>         makes of this order a nanny service, but we know he
<ce>           usually stops by a few times a month to see what
<ce>         problems he can resolve. Now, only silence. We seek
<ce>       some capable attendant to find him and put our minds at
<ce>                    ease. Will you do this, %pcf?

RefuseQuest:  [1001]
<ce>                 More interested in fame and fortune?

AcceptQuest:  [1002]
<ce>         Then I discharge the matter into your capable hands.
<ce>         Look for him under every rock and in every cave you
<ce>       see. Find him in two months time, at the most. He and I
<ce>        have much to discuss, to debate, and to remember. %god
<ce>                            go with thee.

QuestFail:  [1003]
<ce>                Two months are up, and _qgiver_ knows
<ce>                          nothing of _kni_.

QuestComplete:  [1004]
<ce>                  _qgiver_ pats you on the shoulder.
<ce>                     You consider your job done.

RumorsDuringQuest:  [1005]
_kni_? Awfully nice fellow. Rounded up my cattle when they broke their fance.
<--->
_kni_'s missing? %god have mercy. He's a wonderful man.
<--->
You're looking for _kni_? Try with _family_ in ___famtown_.
<--->
_kni_ stayed with some family in ___famtown_, I remember.
<--->
_kni_ is the model of what all knights should be.
<--->
_kni_ went hunting in dungeons, I heard. He'll be back soon.
<--->
_kni_ slew some orc warlord a few months ago. We owe him a lot.
<--->
_kni_ exorcised some ghost over in ___qgiver_ last year.
<--->
_kni_ spends his holidays with _family_ in ___famtown_.

Message:  1012
<ce>              Hello? Yes, _kni_ is my cousin. I've been
<ce>    worried sick for the last several weeks. It's not like him to
<ce>    be gone so long, especially with all the help I need here. If
<ce>      something happened to him I wouldn't survive the shock. My
<ce>            thought is that he found something in ___dung_
<ce>      that interested him enough to make him stay. He's cleansed
<ce>         it several times over the last few years, so I know
<ce>                 he's not in any danger. If _qgiver_
<ce>             wants to look for him, I say go there first.

Message:  1013
<ce>                 Who are you? _qgiver_ sent you? %god
<ce>           be thanked. Yes, I need to leave but my injuries
<ce>                    are very serious. ___dung_ has
<ce>                changed so much. Please, lead the way.
                                     <--->
<ce>                   Yes, I am _kni_. Thank %god that
<ce>                _qgiver_ sent someone to look for me.
<ce>             I feared I would never come free. Let us go.
<ce>                       I will follow your lead.

Message:  1014
<ce>               _kni_ appears to not be found at all in
<ce>              ___dung_. The only thing you find is this
<ce>                bag. You take it to show to _qgiver_.

Message:  1030
%qdt:
 _qgiver_ has sent me
 to look for _kni_,
 a veteran member who is lost
 somewhere in ____qgiver_.
 I have two months to return
 to ___qgiver_.

Message:  1031
%qdt:
 _family_ told me
 _kni_ recently went
 to ___dung_.

Message:  1050
<ce>                _kni_, do not ever scare me like that
<ce>           again. You're not so young as when we first met!
<ce>           My personal recommendation is to take a partner,
<ce>                 even when it's to ___dung_. But you
<ce>            did not come here for a lecture. Welcome home.
<ce>          And %pcf, I have no words with which to thank you
<ce>                 sufficiently. Welcome home as well.

Message:  1051
<ce>                 %pcf, what news do you bring? %oth.
<ce>             %oth, this is not what I had ever expected.
<ce>               I will inform _family_. This is truly a
<ce>          tragedy. I thank you for making the effort. I will
<ce>            need some to get over this. %god bless us all.

QBN:
Person _qgiver_ group Questor male
Person _family_ group Resident1
Person _kni_ faction Generic_Knightly_Order male anyInfo 1005

Place _famtown_ remote random
Place _dung_ remote dungeon0

Clock _timer_ 60.00:00
Clock _death_ 25.00:00 40.00:00

Item _bag_ large_sack

--	Quest start-up:
	start timer _timer_
	start timer _death_
	log 1030 step 0
	place npc _family_ at _famtown_
	place npc _kni_ at _dung_
	add dialog for person _kni_
	pc at _dung_ do _inside_

variable _inside_
_talkfam_ task:
	clicked npc _family_
	say 1012
	log 1031 step 1
	reveal _dung_

_talkkni_ task:
	clicked npc _kni_
	say 1013
	stop timer _death_
	hide npc _kni_
	add _kni_ face
	start quest JHKNC02

_victory1_ task:
	when _qgclicked_ and _talkkni_
	say 1050
	give pc nothing
	legal repute +3
	end quest

_death_ task:
	hide npc _kni_
	place item _bag_ at _dung_

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _talkkni_ and not _grabbag_
	clear _qgclicked_ _clearclick_

_grabbag_ task:
	clicked item _bag_
	say 1014

_victory2_ task:
	when _grabbag_ and _qgclicked_
	say 1051
	change repute with _qgiver_ by -2
	give pc nothing
	end quest

_timer_ task:
	say 1003
	end quest