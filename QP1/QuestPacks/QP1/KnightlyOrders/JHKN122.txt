Quest: JHKN122
DisplayName: The Trapped Halls
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>               Witches are beings of awesome power who
<ce>            should never be trifled with. We agree on that
<ce>                  matter, don't we %pcf? %crn needs
<ce>            our order's aid to free a miserable hole from
<ce>              a witch's curse, one that will trap within
<ce>             any who enter. Will you be the needed hero?
                                     <--->
[QP1]
 
<ce>           A forlorn labyrinth in the wilderness has become
<ce>             the home of a witch that will trap within
<ce>               those who enter its halls. We need a champion
<ce>             capable of slaying her to release the curse.
<ce>               Will you volunteer for this heroic act?

RefuseQuest:  [1001]
<ce>           What manner of knight do you intend to be, %pct?

AcceptQuest:  [1002]
<ce>                Go to ___dung_ as marked on your map.
<ce>           I will warn you that, once inside, you will not
<ce>           be permitted to leave. I expect a great deal of
<ce>             confused adventurers inside who cannot find
<ce>             their way out. Defend yourself as necessary,
<ce>             but remember that your only freedom will be
<ce>             in slaying the witch. Please get to the hole
<ce>              within =2dung_ days' time, lest the curse
<ce>                     capture more witless souls.

QuestFail:  [1003]
<ce>                     _qgiver_ tasked you to go to
<ce>                 ___dung_ to find the witch by today.

QuestComplete:  [1004]
<ce>            So our witch was in fact a daedra in disguise?
<ce>          Perhaps it was an illusion spell instead. I do not
<ce>         know which of the two would have more magical power,
<ce>             for both are foes to be dreaded. My fullest
<ce>                 congratulations to you, %pct. I will
<ce>                 inform the %t concerning your deed.

Message:  1010
<ce>                        (DAEDRIC CHECK FAILED)
<ce>
<ce>         This witch shouts several discordant phrases at you
<ce>          with a hostile tone. Whatever language she speaks,
<ce>                it is clear you do not understand it.

Message:  1011
<ce>                        (DAEDRIC CHECK PASSED)
<ce>
<ce>         This witch shouts several discordant phrases at you,
<ce>        which you immediately identify as Daedric. You respond
<ce>      with sufficient speed and wit as to catch her by surprise,
<ce>                     allowing you the first blow.

Message:  1012
<ce>            The power trapping these halls seems to lift.
<ce>                  The witch has likely fled by now.
<ce>                        Your task has failed.

Message:  1013
<ce>     The air is very heavy here, laden with magical enchantments.
<ce>        If _qgiver_ spoke truly, you will not be able to leave
<ce>            until the witch is slain or has fled the cave.

Message:  1014
<ce>                A loud and angry voice erupts nearby.
<ce>
<ce>            "If I can't get out of here I'll kill you all!
<ce>                  Every one of you! Come over here!"
                                     <--->
<ce>                A loud and scared voice erupts nearby.
<ce>
<ce>               "There's no hope for us! We're all dead!
<ce>                      We're never getting out!"

Message:  1015
<ce>            Powerful magicks whisk you back into ___dung_.
<ce>                   This is no ordinary foe at work.

Message:  1016
<ce>           You have slain the witch. Immediately the arcane
<ce>             powers at work in ___dung_ begin to unravel.
<ce>              You will presumably be able to leave now.

Message:  1030
%qdt:
 _qgiver_ has given me
 =2dung_ days to enter a hole called
 ___dung_ to find a
 witch maintaining a curse on it.
 How long I take within is my own judgment,
 and I should report to ___qgiver_
 when I have slain the enchantress.

QBN:

Person _qgiver_ group Questor male
Person _witch_ faction The_Glenmoril_Witches

Place _dung_ remote dungeon7

Clock _2dung_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 14.00:00

Foe _w1_ is Daedra_seducer
Foe _e1_ is Knight
Foe _e2_ is Rogue
Foe _e3_ is Burglar

--	Quest start-up:
  start timer _2dung_
  log 1030 step 0
  place npc _witch_ at _dung_
  pc at _dung_ set _inside_
  reveal _dung_

_day_ task:
  daily from 0:00 to 23:59

_infight_ task:
  when _day_
  change foe _e1_ infighting true
  change foe _e2_ infighting true
  change foe _e3_ infighting true
  clear _day_

_inside_ task:
  start task _trap_
  create foe _e3_ every 30 minutes 1 times with 33% success msg 1014
  start timer _failsafe_
  say 1013

variable _trap_
_tele_ task:
  when _trap_ and not _slain_ and not _inside_
  teleport pc to _dung_
  say 1015
  start task _clearing_

_clearing_ task:
  clear _tele_ _clearing_

_quest_ task:
  when _inside_ and not _slain_
  create foe _e1_ every 15 minutes indefinitely with 4% success
  create foe _e2_ every 20 minutes indefinitely with 5% success
  create foe _e3_ every 25 minutes indefinitely with 6% success

_failsafe_ task:
  hide npc _witch_
  say 1012
  end quest

_talk_ task:
  clicked npc _witch_

_daed_ task:
  when skill Daedric is at least 40

_ww_ task:
  when _talk_ and _daed_
  say 1011
  hide npc _witch_
  place foe _w1_ at _dung_
  restrain foe _w1_

_wf_ task:
  when _talk_ and not _daed_
  say 1010
  hide npc _witch_
  place foe _w1_ at _dung_

_slain_ task:
  killed _w1_
  say 1016

_pcgetsgold_ task:
  when _qgclicked_ and _slain_
  give pc nothing
  legal repute +8
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_2dung_ task:
  say 1003
  end quest