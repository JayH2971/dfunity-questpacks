Quest: JHKN127
DisplayName: The Caravan
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>               Merchants around %crn have been waylaid
<ce>           by a band of thieves in their travels. They have
<ce>          petitioned our order for help in rooting them out.
<ce>             Are you willing to go along with their plot?

RefuseQuest:  [1001]
<ce>                 These people need a guardian, %pct.

AcceptQuest:  [1002]
<ce>            "Excellent. Now, it is difficult to tell where
<ce>           the band will strike next. Your task will be to
<ce>                 remain with _guide_ and %g3 caravan
<ce>           until the thieves come out of hiding. Travel to
<ce>           various small settlements, for the reports tell
<ce>             us they tend to shy away from large cities.
<ce>            Level them to the earth and report to me your
<ce>             success. I grant you one month to do this."
<ce>
<ce>                    _qgiver_ calls out to _guide_
<ce>               and introduces you, explaining the task.
<ce>                  The merchant thanks you profusely.
<ce>
<ce>           "This is wonderful! I have the fullest assurance
<ce>         that this problem will be dealt with in record time.
<ce>           I suppose that we should just go traveling about
<ce>                         until they find us!"
                                     <--->
<ce>            "Excellent. Now, it is difficult to tell where
<ce>           the band will strike next. Your task will be to
<ce>                 remain with _guide_ and %g3 caravan
<ce>           until the thieves come out of hiding. Travel to
<ce>           various small settlements, for the reports tell
<ce>             us they tend to shy away from large cities.
<ce>            Level them to the earth and report to me your
<ce>             success. I grant you one month to do this."
<ce>
<ce>                    _qgiver_ calls out to _guide_
<ce>               and introduces you, explaining the task.
<ce>                  The merchant thanks you profusely.
<ce>
<ce>             "A knight to protect me! The %t has heard us
<ce>        at last! Thank you! Lead the way. Where shall we go?"

QuestFail:  [1003]
<ce>             During this fracas _guide_ has fallen dead.
<ce>            This is a terrible price to pay for uncovering
<ce>                        this group of thieves.

QuestComplete:  [1004]
<ce>          _qgiver_ shakes your hand warmly for your service.

Message:  1020
<ce>                 Your small caravan is surrounded by
<ce>              a pack of thieves. Presumably this is what
<ce>                      _qgiver_ sent you to find.

Message:  1021
<ce>                _guide_, unscathed during the battle,
<ce>         is overjoyed to know that the group has been slain.
<ce>
<ce>            "You are truly a knight of the highest order!
<ce>              What a tale I will tell throughout %crn!"
<ce>
<ce>                After offering you profuse thanks, %g1
<ce>             prepares his things for the next leg of the
<ce>                  journey and departs. Now to report
<ce>                      to _qgiver_ in ___qgiver_.
                                     <--->
<ce>                _guide_, unscathed during the battle,
<ce>         is overjoyed to know that the group has been slain.
<ce>
<ce>          "Wow, what did I just watch? That was incredible!
<ce>                  Thank you, thank you, thank you!"
<ce>
<ce>                After offering you profuse thanks, %g1
<ce>             prepares his things for the next leg of the
<ce>                  journey and departs. Now to report
<ce>                      to _qgiver_ in ___qgiver_.

Message:  1022
<ce>                  Welcome, %pct. So _guide_ did not
<ce>           survive the battle, it would seem. We are elated
<ce>              at the destruction of this thieving band,
<ce>          but we wish it did not have to come at this cost.
<ce>              I nevertheless thank you for your service.
<ce>            %crn will be greatly benefitted by your work.

Message:  1023
<ce>          Welcome, %pct. You have slain the band of thieves
<ce>            and preserved the life of the merchant. Truly
<ce>         you have passed this test with flying colors. I will
<ce>           tell new recruits to this order concerning your
<ce>         deeds and the example they provide. Excellent work.

Message:  1024
<ce>                Though _guide_ fell during the battle,
<ce>              this band is still vanquished. It is time
<ce>                        to report to _qgiver_.

Message:  1030
%qdt:
 _qgiver_ of ___qgiver_ has
 sent me to accompany a merchant
 named _guide_ in a plot
 to root out a violent band of
 thieves in the countryside.
 I have one month to travel
 about until I find them.
 I was told to travel
 to a variety of settlements
 instead of cities.

QBN:

Person _qgiver_ group Questor male
Person _guide_ faction The_Merchants

Clock _goal_ 30.00:00
Clock _battle_ 00:04 00:08
Clock _appear_ 2.00:00 4.00:00

Foe _s1_ is Assassin
Foe _s2_ is Burglar

---- Quest startup
    log 1030 step 0
    add _guide_ face
    start timer _goal_
    pick one of _e1_ _e2_ _e3_ _e4_
    start timer _appear_

variable _e1_
variable _e2_
variable _e3_
variable _e4_
variable _appear_

_f1_ task:
    when pc enters tavernhome

_f2_ task:
    when pc enters village

_f3_ task:
    when pc enters hamlet

_f4_ task:
    when pc enters templehome

_f5_ task:
    when pc exits anywhere
    clear _f5a_

_f5a_ task:
    when pc enters anywhere
    clear _f5_

_t1_ task:
    when _e1_ and _f1_
    start task _spawn_

_t2_ task:
    when _e2_ and _f2_
    start task _spawn_

_t3_ task:
    when _e3_ and _f3_
    start task _spawn_

_t4_ task:
    when _e4_ and _f4_
    start task _spawn_

_t5 task:
    when _appear_ and _f5_
    start task _spawn_

_spawn_ task:
    say 1020
    start timer _battle_
    create foe _s1_ every 0 minutes 1 times with 100% success
    create foe _s2_ every 0 minutes 3 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _s1_ infighting true
    change foe _s2_ infighting true
    clear _infight_

_battle_ task:
    say 1003
    drop _guide_ face

_w1_ task:
    killed _s1_

_w2_ task:
    killed 3 _s2_

_slain_ task:
    when _w1_ and _w2_
    stop timer _battle_

_survive_ task:
    when _slain_ and not _battle_
    say 1021
    drop _guide_ face

_notsurvive_ task:
    when _slain_ and _battle_
    say 1024

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcgetsgold1_ task:
    when _qgclicked_ and _slain_ and _battle_
    say 1022
    change repute with _qgiver_ by -2
    give pc nothing
    legal repute +3
    end quest

_pcgetsgold2_ task:
    when _qgclicked_ and _slain_ and not _battle_
    say 1023
    give pc nothing
    legal repute +3
    end quest

_goal_ task:
    change repute with _qgiver_ by +2
    end quest