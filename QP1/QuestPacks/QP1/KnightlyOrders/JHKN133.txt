Quest: JHKN133
DisplayName: Stalking the Prey
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>                  A streak of tigers has migrated to
<ce>             a forlorn home in %crn. A hunter wishes for
<ce>           an honorable knight to help slay the beasts, and
<ce>          when we speak of honor, your name quickly follows.
<ce>                   Will you accompany this stalker?

RefuseQuest:  [1001]
<ce>          Disappointing, but perhaps it is not so difficult.

AcceptQuest:  [1002]
<ce>                      "Well there we are then!"
<ce>
<ce>            A boisterous ranger pats you on the shoulder,
<ce>                    and _questgiver_ nods at you.
<ce>
<ce>            "Glad to hear I've got a taker! Let's head on
<ce>                  out then %pcn! My name's _hunter_.
<ce>            We're headed to ___poorhome_,  to clear us out
<ce>            some tigers! Let's get there in =timer_ days.
<ce>                       I'm ready when you are!"

QuestFail:  [1003]
<ce>

QuestComplete:  [1004]
<ce>                         You are left alone.

Message:  1010
<ce>          Sure enough, you see the pack of tigers around the
<ce>         property. Whoever lives inside must be terrified to
<ce>                  leave. _hunter_ squeezes your arm.
<ce>
<ce>            "Okay, I'm gonna pull a bunch of 'em away and
<ce>           shoot 'em down with my bow. You'll kill whatever
<ce>                    doesn't follow me. Ready? Go!"
<ce>
<ce>          Before you can answer, the stalker begins hooting
<ce>              and running away. Three tigers follow %g2,
<ce>                     and the rest settle for you.

Message:  1011
<ce>               The tigers now slain, you get your gear
<ce>            together to leave. _hunter_ shouts furiously.
<ce>
<ce>          "Hey! What're you doin'? There's three more right
<ce>          there, and we can't leave any stragglers! They'll
<ce>        come right back and eat someone else up if we leave!"
<ce>
<ce>       The hunter points at three tiny tiger cubs nuzzling the
<ce>       bodies of the fallen adults. They couldn't be more than
<ce>             a month old; they pose no threat to anyone.
<ce>                  _hunter_ pulls out a short blade.
<ce>
<ce>          "I'm gonna go tell the family the problem's over.
<ce>             You stab these three right through, and I'll
<ce>                           be right back."
<ce>
<ce>                     Do you kill the baby tigers?

Message:  1012
<ce>            You make the cubs' death as quick as you can.
<ce>                Their pathetic mewling rends the air.
<ce>           Even if they knew how to defend themselves, the
<ce>         sorrow of losing their parents clouds their senses,
<ce>                      and they put up no fight.
<ce>
<ce>                  _hunter_ returns with a big smile.
<ce>
<ce>           "How's it feel bein' a hero? Aw, you're probably
<ce>             used to it. Maybe one day I'll join y'all in
<ce>                   __questgiver_ and we can do this
<ce>                             some more!"

Message:  1013
<ce>                        _hunter_ goes berserk.
<ce>
<ce>         "If you're not here to kill tigers, what're you good
<ce>             for?! Get lost and let me handle this then!"
<ce>
<ce>          With the short blade out, %g1 advances toward the
<ce>                   cubs. Do you let %g2 kill them?

Message:  1014
<ce>           You can't take part in this, but also can't stop
<ce>             _hunter_. After %g1 stabs the cubs through,
<ce>          %g1 lets out a frustrated sigh and addresses you.
<ce>
<ce>         "Well, glad that's over with. Didn't expect a knight
<ce>           to be such a wuss, though. I'm gonna go tell the
<ce>              folks here that the problem's gone. Bye."

Message:  1015
<ce>              "Pathetic %ra! Tiger lover! I'm not gonna
<ce>                    let you stab me in the back!"
<ce>
<ce>               _hunter_ steps back and readies %g3 bow.

Message:  1016
<ce>        You knock %g2 to the ground, enough to instil the fear
<ce>       of death in %g2. Then %g1 begins crying. You make clear
<ce>               this fight is over, and _hunter_ starts
<ce>      crawling away. The tiger cubs, for their part, make their
<ce>          various ways into the wilderness. You stand as the
<ce>                             sole victor.

Message:  1017
<ce>       You were to clear out the tigers in ___poorhome_ by now.
<ce>                   _hunter_ is disgusted with you.

Message:  1030
%qdt:
 A stalker named _hunter_ has
 asked __questgiver_ for help in
 slaying a band of tigers at
 ___poorhome_. We need to be there
 within =timer_ days.

QBN:

Person _questgiver_ group Questor male
Person _hunter_ group Resident1

Place _poorhome_ remote poorhome

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _tig_ is 3 Sabretooth_tiger
Foe _hnt_ is Ranger

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _poorhome_ set _inside_
    add _hunter_ face

_inside_ task:
    say 1010
    drop _hunter_ face
    create foe _tig_ every 0 minutes 1 times with 100% success

_killed_ task:
    killed 3 _tig_
    prompt 1011 yes _obey_ no _defy_

_obey_ task:
    say 1012
    give pc nothing
    end quest

_defy_ task:
    prompt 1013 yes _weak_ no _fight_

_weak_ task:
    say 1014
    give pc nothing
    end quest

_fight_ task:
    say 1015
    create foe _hnt_ every 0 minutes 1 times with 100% success

_inj_ task:
    injured _hnt_
    clicked foe _hnt_
    say 1016
    remove foe _hnt_
    give pc nothing
    end quest

_timer_ task:
    say 1017
    end quest