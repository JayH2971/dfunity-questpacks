Quest: JHKN135
DisplayName: In Search of the Lost
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                A local family here in ___questgiver_
<ce>          is worried sick over their son, who descended into
<ce>             some ruins on some sort of heroic escapade.
<ce>        That was weeks ago. They wish to hear something of his
<ce>       fate, and if he is alive, for someone to bring him back.
<ce>                Will you lend yourself to this cause.

RefuseQuest:  [1001]
<ce>                                Fine.

AcceptQuest:  [1002]
<ce>              Seek for the youth, _dummy_, in ___dung_.
<ce>           You are allotted =timer_ days to go and return,
<ce>             before they hire from a more reliable source
<ce>          and we are shamed. If you find the boy, bring him
<ce>          back with all haste. If you find evidence of him,
<ce>     bring it here nonetheless. And if things go worse than that,
<ce>      we must know his fate nonetheless. %god speed thy flight.

QuestFail:  [1003]
<ce>               You were to return with news of _dummy_
<ce>              to __questgiver_ of ___questgiver_ by now.

QuestComplete:  [1004]
<ce>        How grievous indeed. I thank you for giving the child
<ce>       a proper burial. I will take upon me the onerous task of
<ce>      informing the family. We wish all stories could have happy
<ce>         endings, but we must face reality. Take rest, %pcf.
<ce>      One more family in %crn has been served by our knighthood.

Message:  1011
<ce>           You find a mostly-devoured body here, with signs
<ce>          of death suggesting several days, perhaps a week,
<ce>               of time from death to your arrival.
<ce>            You take time to memorize the youth's features
<ce>                 and consider the cause of his death.
<ce>             Then you dispose of the body appropriately.
<ce>            You will have sad news to offer __questgiver_.

Message:  1030
%qdt:
 _questgiver_ has sent me to
 ___dung_ to look for 
 _dummy_, whose family hopes
 he is still alive. I must find him
 or any sign of him and return to
 ___questgiver_ within =timer_ days.

Message:  1031
%qdt:
 I have found a body much resembling
 what _dummy_ would look like.
 I must report my finding to
 __questgiver_ in ___questgiver_.

QBN:

Person _questgiver_ group Questor male
Person _dummy_ faction Children male

Item _body_ dead_body

Place _dung_ remote dungeon6

Clock _timer_ 00:00 0 flag 17 range 0 2

-- Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _body_ at _dung_
    have _body_ set _remove_
    reveal _dung_

_remove_ task:
    start task _victory_
    take _body_ from pc
    say 1011
    log 1031 step 1

variable _victory_

_qgclicked_ task:
    clicked npc _questgiver_

_pcgetsgold_ task:
    when _victory_ and _qgclicked_
    give pc nothing
    legal repute +5
    start quest JHKNC05
    end quest

_S.04_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _S.04_

_timer_ task:
    say 1003
    end quest