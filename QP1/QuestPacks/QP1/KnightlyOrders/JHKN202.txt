Quest: JHKN202
DisplayName: The Summoner
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>      We have been advised of some fool
<ce>  who has holed up to practice summoning daedra.
<ce>  Regardless of the mage's capability, we must
<ce>  treat this as a real threat. We need a knight
<ce>  to take the battle to the summoner and prevent a crisis,
<ce>  before any demons are summoned.

RefuseQuest:  [1001]
<ce>    Let us know if you see any heroes out there.

AcceptQuest:  [1002]
<ce>   I will mark ___dung_ on your map. Somewhere inside,
<ce>  the rogue mage works. You'll likely encounter
<ce>  imps while inside, as they were summoned
<ce>  as part of an earlier experiment.
<ce>  Bring me word of the demonologist's death
<ce>  within =timer_ days, that we may be at peace.

QuestFail:  [1003]
<ce>                    _qgiver_ expected your report
<ce>                  regarding the daedra summoner by now.

QuestComplete:  [1004]
<ce>      Fine work, %pct. It's much better for us to
<ce>  pre-empt these cases -- no one enjoys
<ce>  fighting the daedra.

QuestorPostSuccess:   [1008]
Keep practicing your skills, and even the toughest daedra will fall to you.

Message:  1010
<ce>  Faint magical energy tinges the air.
<ce>  You hope you're not too late to stop
<ce>  this daedra summoner.

Message:  1011
<ce>  "No! You'll not stop me, not when I'm so close!"

Message:  1012
<ce>  The summoner is defeated, the ritual interrupted.

Message:  1030
%qdt:
 __qgiver_ of
 ___qgiver_ depend on me to
destroy a pretended daedra summoner
somewhere in ___dung_. I must do this
and return to report within =timer_ days.

QBN:

Person _qgiver_ group Questor male

Place _dung_ remote dungeon3

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _mage_ is Mage
Foe _enemy1_ is Imp

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _mage_ at _dung_
	pc at _dung_ set _inside_
	reveal _dung_

_inside_ task:
    say 1010

_quest_ task:
	when _inside_ and not _slain_
	create foe _enemy1_ every 13 minutes indefinitely with 15% success

_daily_ task:
	daily from 0:00 to 23:59
	change foe _enemy1_ infighting true
	clear _daily_

_inj_ task:
    injured _mage_
	clicked foe _mage_
    say 1011
    change foe _mage_ infighting true
    change foe _mage_ team 16

_slain_ task:
	killed 1 _mage_ saying 1012

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +3
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
    end quest