Quest: JHMG101
DisplayName: An Illusory Hunt
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                My dear %pcf, we often tell our newest
<ce>            inductees to the guild, "Too much power in too
<ce>               little time has destroyed many." Sadly,
<ce>                not all our students take heed of that
<ce>               warning, and we currently have a bit of
<ce>               a situation on our hands because of it.
<ce>                   For a copy of a book, we request
<ce>                  your help in culling a student too
<ce>                  well-versed in Illusion magic for
<ce>                           one's own good.
                                     <--->
<ce>               I regret to speak of this matter, but it
<ce>                is a necessary task. The Mages Guild,
<ce>                 wielding all the power that it does,
<ce>              assumes responsibility for any misuse that
<ce>               arises out of its teachings. On occasion
<ce>                there is no solution to a problematic
<ce>                 pupil except a mercy killing. The %t
<ce>                  insists that we do away with this
<ce>               shapeshifting villain. Will you help us?

RefuseQuest:  [1001]
<ce>                        Very well, but what if
<ce>                       you are the next target?
                                     <--->
<ce>                  Loyalty, %pct. Prove your loyalty.

AcceptQuest:  [1002]
<ce>                   An admirable response, my child.
<ce>                     It seems this student became
<ce>                    hopped up on power after using
<ce>                 a chameleon spell for the first time
<ce>                  and found it just addictive. Then
<ce>                    came experimentation with the
<ce>                  shapeshifting spells, and suddenly
<ce>                   we have reports pouring in of a
<ce>                tiger wearing robes that kills people.
<ce>                  One of our student's friends knew
<ce>              that there was this hiding spot of resort,
<ce>                   ___dung_ in this same province.
<ce>               Go there and find our pupil, in whatever
<ce>                state as may be the case, and end this
<ce>                menace for us. You have =timer_ days.

QuestFail:  [1003]
<ce>     The Mages Guild expected you to slay the illusionist by now.

QuestComplete:  [1004]
<ce>                   A thousand thanks for saving our
<ce>                 poor pupil from a worse fate. Those
<ce>                   who fail to control power become
<ce>                slaves to it in time. Have your book.

Message:  1010
<ce>                    (ATTRIBUTE CHECK PASSED: LUCK)
<ce>
<ce>          Luck is on your side. The renegade student is both
<ce>        nearby and in normal form. You both arm yourselves for
<ce>          the battle as you hear, "You won't take me alive!"

Message:  1020
<ce>              As the creature's corpse falls, you hear a
<ce>             groan. This must have been the shapeshifter.

Message:  1021
<ce>                    This rogue student is no more.

Message:  1030
%qdt:
 The Mages Guild of
 ___qgiver_ has asked
 me to find a rogue student
 skilled with illusion magic in
 ___dung_. I have
 =timer_ days to end this threat
 and return.

QBN:
Item _reward_ book

Person _qgiver_ group Questor male

Place _dung_ remote dungeon5

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _pause_ 0:01

Foe _enemy1_ is Sabretooth_tiger
Foe _enemy2_ is Spider
Foe _enemy3_ is Giant_scorpion
Foe _enemy4_ is Grizzley_bear
Foe _enemy5_ is Mage

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	reveal _dung_
	pc at _dung_ do _inside_

_luck_ task:
	when attribute Luck is at least 85
	pick one of _lucky_ _notlucky_ _notlucky_

variable _notlucky_
variable _lucky_
variable _inside_

_freewin_ task:
	when _lucky_ and _inside_
	start timer _pause_

_pause_ task:
	say 1010
	create foe _enemy5_ every 0 minutes 1 times with 100% success

_noluck_ task:
	when not _lucky_
	pick one of _D.01_ _D.02_ _D.03_ _D.04_ _D.05_

_D.01_ task:
	place foe _enemy1_ at _dung_

_D.02_ task:
	place foe _enemy2_ at _dung_

_D.03_ task:
	place foe _enemy3_ at _dung_

_D.04_ task:
	place foe _enemy4_ at _dung_

_D.05_ task:
	place foe _enemy5_ at _dung_

_slain_ task:
	killed 1 _enemy1_ saying 1020
	killed 1 _enemy2_ saying 1020
	killed 1 _enemy3_ saying 1020
	killed 1 _enemy4_ saying 1020
	killed 1 _enemy5_ saying 1021

_inj_ task:
	injured _enemy1_
	injured _enemy2_
	injured _enemy3_
	injured _enemy4_
	injured _enemy5_
	clicked foe _enemy1_
	clicked foe _enemy2_
	clicked foe _enemy3_
	clicked foe _enemy4_
	clicked foe _enemy5_
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	change foe _enemy3_ infighting true
	change foe _enemy4_ infighting true
	change foe _enemy5_ infighting true

_qgclicked_ task:
	clicked npc _qgiver_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest