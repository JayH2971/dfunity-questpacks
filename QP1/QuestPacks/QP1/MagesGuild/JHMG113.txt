Quest: JHMG113
DisplayName: Wandering Atronach
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>            One of our experiments is off wandering around
<ce>             another city, apparently not doing any harm,
<ce>             but still in need of someone to lead it back
<ce>            here. If it's not too much of a bother, would
<ce>                    you be willing to go find it?
                                     <--->
<ce>              A dolt of a student led an atronach off on
<ce>              a sight-seeing trip and forgot to cast the
<ce>               proper binding spell. This is made worse
<ce>                 by the fact that he forgot about its
<ce>              existence until returning here. If you'll
<ce>               be heading out, perhaps you can go fetch
<ce>              it for us and save us some embarrassment.

RefuseQuest:  [1001]
<ce>                  This was a simple task, you know.

AcceptQuest:  [1002]
<ce>                 Lovely. The atronach is in ___city_.
<ce>           It actually flees from most living beings, so it
<ce>          will need some degree of attraction to come close.
<ce>             Take this bit of _elem_. That should provide
<ce>          a familiar sensation to lead it towards you. From
<ce>        there just give it a good, hard blow across the body,
<ce>        and it should be stunned back into submission. It may
<ce>       freeze up in place, but trust me, it is created to obey.
<ce>       It will follow after some time. Bring it here in =timer_
<ce>            days so we can recycle its material. You will
<ce>                   be paid =reward_ gold. Good day.

QuestFail:  [1003]
<ce>                Your task to take the atronach back to
<ce>                   ___qgiver_ has run out of time.

QuestComplete:  [1004]
<ce>                 The atronach, still a little addled,
<ce>                        trundles in after you.
<ce>
<ce>         "Well, the proof is right here with you. Thank you,
<ce>                %pcf. It looks like it isn't seriously
<ce>              damaged. We know precisely what to do from
<ce>                    here. _stud_, if you please?"
<ce>
<ce>           A student uses a magic tool of some kind to lead
<ce>              the atronach into a separate room, slight
<ce>                agonized wailing notwithstanding. Then
<ce>                follows a flash of light and the sound
<ce>                   of something sizzling. _qgiver_
<ce>              hands you your reward with a kindly smile
<ce>                     and turns to other business.

RumorsDuringQuest:  [1005]
Some atronach's been wandering the streets of ___city_.
<--->
You hear about that atronach the Mages Guild lost?

Message:  1010
<ce>           Somewhere in these streets, an atronach wanders.
<ce>                   You'll have to find it somehow.
<ce>
<ce>        A very subtle enchantment lingers in the environment,
<ce>               likely due to the atronach's wandering.
<ce>               Perhaps you could use that magic to your
<ce>           benefit, to attract it to you. Do you try (yes)
<ce>                 or wait for it to come to you (no)?
<ce>
<ce>                  (SKILL CHECK: YES: Mysticism, difficulty low)

Message:  1011
<ce>                         (SKILL CHECK PASSED)
<ce>
<ce>                You start to work, and manage to pull
<ce>                 the flow of magicka toward yourself.

Message:  1012
<ce>              Inhuman wailing hails the presence of the
<ce>              missing atronach. It stumbles toward you,
<ce>                   recognizing the familiar _elem_.
<ce>
<ce>                        "Leave me," it moans.
                                     <--->
<ce>              Inhuman wailing hails the presence of the
<ce>              missing atronach. It stumbles toward you,
<ce>                   recognizing the familiar _elem_.
<ce>
<ce>                   "Don't take me back," it moans.

Message:  1013
<ce>                  With a single strike the atronach
<ce>                    seems to have its mind wiped.
<ce>                        If what _qgiver_ told
<ce>                  you is true, it should follow the
<ce>               _elem_ you were given once it recovers.
<ce>               Now is the time to return to ___qgiver_.

Message:  1014
<ce>                         (SKILL CHECK FAILED)
<ce>
<ce>        You attempt to manipulate the magic for your benefit.
<ce>       Whatever you end up doing gets the atronach's attention,
<ce>                           but it is angry.

Message:  1015
<ce>         This is a simple task. So long as you remain in the
<ce>     streets, you'll find it. No need to muck things up further.

Message:  1020
<ce>              The atronach is dead, and you have failed.

Message:  1030
%qdt:
 _qgiver_ of The Mages Guild
 has assigned me to lead an
 atronach wandering around
 ___city_ back to
 ___qgiver_. I will be
 paid =reward_ gold if I
 do so within =timer_ days.
 It requires a strike of some kind
 to reprogram, but I must not destroy it.

QBN:
Item _elem_ element
Item _reward_ gold

Person _qgiver_ group Questor male
Person _stud_ faction The_Mages_Guild

Place _city_ remote tavernhome

Foe _atr_ is Ice_atronach

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _find_ 00:06 00:22

--	Quest start-up:
    start timer _timer_
    pc at _city_ set _arrival_
    log 1030 step 0
    get item _elem_

_arrival_ task:
    prompt 1010 yes _attempt_ no _flee_

variable _attempt_

_skilled_ task:
    when skill Mysticism is at least 25

_easywin_ task:
    when _attempt_ and _skilled_
    say 1011
    start task _find_

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1014
    start task _badfind_

_flee_ task:
    say 1015
    start timer _find_

_stop_ task:
    when not _arrival_
    stop timer _find_

_find_ task:
    say 1012
    create foe _atr_ every 0 minutes 1 times with 100% success
    restrain foe _atr_

_badfind_ task:
    say 1012
    create foe _atr_ every 0 minutes 1 times with 100% success
    cast Feet_of_Notorgo spell on _atr_
    cast Orc_Strength spell on _atr_

_fail_ task:
    killed _atr_
    say 1020
    end quest

_stun_ task:
    injured _atr_
    clicked foe _atr_
    say 1013
    remove foe _atr_

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _stun_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _stun_
    take _elem_ from pc
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest