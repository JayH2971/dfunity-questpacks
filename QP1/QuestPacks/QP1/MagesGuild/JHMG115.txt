Quest: JHMG115
DisplayName: A Victim of Shapeshifting
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>           Actually, we have a matter of immediate import.
<ce>          I do hope you'll accept this. It's a bit of a long
<ce>           story, but the short version is that one of our
<ce>                   local members, _dummy_, has been
<ce>            cursed by some band of shapeshifters to assume
<ce>              the form of a beast. Mercenaries from the
<ce>              Fighters Guild are now poised to kill %g2,
<ce>                unaware that %g1 is innocent. Will you
<ce>                       defend our poor friend?
                                     <--->
[QP1]
 
<ce>           We have something of a rescue mission that needs
<ce>             an immediate counterpart. A good friend and
<ce>               member of the Mages Guild, someone named
<ce>                   _dummy_, has been cursed to take
<ce>              the form of an animal by shapeshifters and
<ce>             no one has been able to cast off the spell.
<ce>             Long story short, the Fighters Guild is now
<ce>            on its way to slay our poor friend, believing
<ce>              %g2 to be a legitimate animal in a house.
<ce>                Someone must go defend %g2. Is it you?

RefuseQuest:  [1001]
<ce>               Then %g3 life will be on your conscience
<ce>                 should %g1 not survive the attempt!

AcceptQuest:  [1002]
<ce>         Well, I gave you the short version, and now the long
<ce>        one. The shapeshifting spell is advanced, to the point
<ce>            that _dummy_ actually thinks %g1 is an animal.
<ce>        I feel certain %g1 will attack you on sight. No matter
<ce>               what else happens in _res_, you must not
<ce>         slay %g2. That is an inviolable directive. Fight off
<ce>         the mercenaries who arrive and report to me when you
<ce>             are finished. I have here a book I think you
<ce>         will relish as a reward. And did I forget to mention
<ce>          you must get there immediately? The Fighters Guild
<ce>                 will already be on the move by now.

QuestFail:  [1003]
<ce>             There is no doubt by now the Fighters Guild
<ce>                       will have slain _dummy_.

QuestComplete:  [1004]
<ce>                  Oh, what a hero! _dummy_ will know
<ce>                about your work and praise your name,
<ce>                  once %g1 is able to think straight.
<ce>                You have truly saved a life this day.
<ce>                Take this magnificent book, =reward_,
<ce>         as my heartfelt thanks for protecting the innocent.

RumorsDuringQuest:  [1005]
Them Shapeshifters are awful people, I says.
<--->
You heard about that fellow cursed by the Shapeshifters? Wonder if he deserved it.

Message:  1010
<ce>             You made it here before the Fighters Guild.
<ce>        The shapeshifted mage, in bestial form, cowers nearby.
<ce>             With some skill in Illusion magic, you could
<ce>                try to dispel the enchantment yourself
<ce>                   and save everyone some trouble.
<ce>                 Do you attempt it (yes) or fight off
<ce>                    the mercenaries instead (no)?
<ce>
<ce>                  (SKILL CHECK: YES: Illusion, difficulty low)

Message:  1011
<ce>       You hear no more shouting. Hopefully these are the last
<ce>                  mercenaries sent to slay _dummy_.

Message:  1012
<ce>                         (SKILL CHECK PASSED)
<ce>
<ce>            Just as you hear footsteps approach the door,
<ce>               you grasp the spell's concept and try to
<ce>                  undo it. _dummy_ takes the form of
<ce>              a person once more and falls to the floor.
<ce>
<ce>                  "Thank you, thank you, thank you!
<ce>               What a horrifying experience that was!"
<ce>
<ce>             Mercenaries from the Fighters Guild burst in
<ce>                seconds after, demanding to know where
<ce>               the creature is. You both tell them that
<ce>          the house is clean, and they depart, disappointed.
<ce>
<ce>                 _dummy_ retreats to collect %g2self.
<ce>                     Your role here is finished.

Message:  1013
<ce>                         (SKILL CHECK FAILED)
<ce>
<ce>            Just as you hear footsteps approach the door,
<ce>               you grasp the spell's concept and try to
<ce>                    undo it. Your skills fail you,
<ce>            and the enchanted creature misinterprets your
<ce>      attempt as an offensive spell. It prepares for the attack
<ce>    just as the Fighters Guild mercenaries burst through the door.

Message:  1014
<ce>              You hear two people approaching the door.
<ce>                   You hope you're ready for this.

Message:  1020
<ce>                 _dummy_ has been slain by your hand,
<ce>               and the Mages Guild will not forget it.

Message:  1030
%qdt:
 _qgiver_ has sent me
 to _res_ immediately
 to defend a local member named
 _dummy_ from a band of
 mercenaries. I should go as quickly
 as humanly possible.

QBN:
Item _reward_ book

Person _qgiver_ group Questor male
Person _dummy_ faction The_Mages_Guild
Person _fg_ faction The_Fighters_Guild

Place _res_ local random

Clock _timer_ 00:40
Clock _failsafe_ 60.00:00

Foe _dk1_ is Spider
Foe _dk2_ is Giant_scorpion
Foe _dk3_ is Grizzley_bear
Foe _dk4_ is Harpy
Foe _bk1_ is 2 Barbarian
Foe _bk2_ is 2 Knight

--	Quest start-up:
    start timer _failsafe_
    log 1030 step 0
    pick one of _d1_ _d2_ _d3_ _d4_
    pick one of _b1_ _b2_
    pc at _res_ set _inside_

variable _d1_
variable _d2_
variable _d3_
variable _d4_
variable _b1_
variable _b2_

_inside_ task:
    prompt 1010 yes _attempt_ no _flee_
    stop timer _timer_

variable _attempt_
_skilled_ task:
    when skill Illusion is at least 25

_flee_ task:
    say 1014
    restrain foe _dk1_
    restrain foe _dk2_
    restrain foe _dk3_
    restrain foe _dk4_

_bigwin_ task:
    when _attempt_ and _skilled_
    say 1012
    start task _vict_

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1013

_notinside_ task:
    when not _inside_ and not _slain_
    start timer _timer_

_spawn1_ task:
    when _inside_ and _d1_ and not _vict_
    create foe _dk1_ every 0 minutes 1 times with 100% success

_spawn2_ task:
    when _inside_ and _d2_ and not _vict_
    create foe _dk2_ every 0 minutes 1 times with 100% success

_spawn3_ task:
    when _inside_ and _d3_ and not _vict_
    create foe _dk3_ every 0 minutes 1 times with 100% success

_spawn4_ task:
    when _inside_ and _d4_ and not _vict_
    create foe _dk4_ every 0 minutes 1 times with 100% success

_bk1_ task:
    when _inside_ and _b1_ and not _vict_
    create foe _bk1_ every 1 minutes 1 times with 75% success

_bk2_ task:
    when _inside_ and _b2_ and not _vict_
    create foe _bk2_ every 1 minutes 1 times with 75% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _bk1_ infighting true
    change foe _bk2_ infighting true
    clear _infight_

_slain_ task:
    killed 2 _bk1_
    killed 2 _bk2_
    say 1011
    stop timer _failsafe_
    change repute with _fg_ by -10
    start task _vict_

variable _vict_

_fail_ task:
    killed _dk1_
    killed _dk2_
    killed _dk3_
    killed _dk4_
    say 1020
    change repute with _dummy_ by -18
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _vict_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _vict_
    give pc _reward_
    end quest

_failsafe_ task:
    end quest

_timer_ task:
    say 1003
    end quest