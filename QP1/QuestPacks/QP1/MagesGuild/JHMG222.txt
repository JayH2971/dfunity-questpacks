Quest: JHMG222
DisplayName: Scholarly Pursuits
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>           Ah! This one, I think, is just perfect for you.
<ce>             The Archmage offers an enchanted item to the
<ce>        person who returns to him a certain stolen belonging.
<ce>          We have scried its location, and now only lack the
<ce>          brave soul who will retrieve it. Are you willing?

RefuseQuest:  [1001]
<ce>                         Hmm. Disappointing.

AcceptQuest:  [1002]
<ce>              Splendid! You'll find the stolen _tri_ in
<ce>                     a labyrinth called ___dung_.
<ce>                  Return it here, and you'll receive
<ce>             an enchanted _magic_ capable of undoing any
<ce>       effects of paralysis. Does that sound worth your while,
<ce>          %pct? The offer is good for the next =timer_ days,
<ce>                        so hasten your steps.

QuestFail:  [1003]
<ce>                  You were to deliver the Archmage's
<ce>                  stolen _tri_ to ___qgiver_ by now.

QuestComplete:  [1004]
<ce>         A thousand thanks, %pcf. The Archmage, reclusive as
<ce>              he might be, will hear your name for this.
<ce>        While we work out the details of your future promotion
<ce>        and so forth, perhaps this _magic_ will tide you over?
<ce>                  It is a fine reward for fine work.

Message:  1010
<ce>                 The _tri_ is close by, but the thief
<ce>                was clever enough to put a trap on it.
<ce>              You can feel the magical energy from here.
<ce>              Someone skilled in the thieving arts would
<ce>                 be able to disarm the trap manually.
<ce>        On the other hand, if you were confident in Mysticism,
<ce>             you could counter the effect to nullify it.
<ce>         Attempting and failing to disarm the trap, however,
<ce>           would be significantly more dangerous than just
<ce>                   suffering its effects outright.
<ce>                          What will you do?
<ce>
<ce>    (ANCHOR) Leverage your Pickpocketing skill to disable the trap
<ce>                   (skill check Pickpocketing, low)
<ce>
<ce>      (COUNTER) Counter the trap with an enchantment of your own
<ce>                   (skill check Mysticism, medium)
<ce>
<ce> (O.K.) Walk into the trap, knowing that it will cause nonlethal damage.

Message:  1011
<ce>                 (SKILL CHECK FAILED: PICKPOCKETING)
<ce>
<ce>          Your attempt to make a small anchor to attract the
<ce>    trap's power goes terribly wrong. The offensive spell rebounds
<ce>       between you and the anchor multiple times, damaging you
<ce>              significantly. However, you remain alive.

Message:  1012
<ce>                   (SKILL CHECK PASSED: PICKPOCKET)
<ce>
<ce>         You make a small anchor from scrap metal and toss it
<ce>          toward the trap. The offensive enchantment fires,
<ce>       and then goes inert. The _tri_ is yours for the taking.

Message:  1013
<ce>                   (SKILL CHECK FAILED: MYSTICISM)
<ce>
<ce>         Standing on the edge of the trap's range, you begin
<ce>   a spell to counter its effects. You misjudge the power required,
<ce>  and both your spell and the trap's effect shoot through your body.
<ce>   It hurts terribly, and your innards have a strange feel to them.

Message:  1014
<ce>                   (SKILL CHECK PASSED: MYSTICISM)
<ce>
<ce>        Setting a counter-enchantment to the trap is trivial.
<ce>        Whoever this thief was, their magical power could not
<ce>             have been so impressive. With a few seconds'
<ce>           concentration, you manage to soak up the trap's
<ce>        power entirely, leaving the _tri_ ripe for the taking.

Message:  1015
<ce>    You're mature enough to recognize that a labor poorly executed
<ce>              can be worse than making no effort at all.
<ce>      You cross into the trap's range and suffer some harm, but
<ce>        not enough to kill you. The _tri_ is within reach now.

Message:  1016
<ce>              Now beyond the reach of any trap, you grab
<ce>                        the Archmage's _tri_.

Message:  1030
%qdt:
_qgiver_ of __qgiver_
in ___qgiver_ will give me
an enchanted _magic_, capable
of dispelling paralysis, if
I can retrieve a stolen
_tri_ from ___dung_
within =timer_ days.

QBN:
Item _magic_ item class 4 subclass 26
Item _tri_ trinket

Place _dung_ remote dungeon11

Person _qgiver_ group Questor

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _tri_ at _dung_
    reveal _dung_

_timer_ task:
    say 1003
    make _tri_ permanent
    end quest

_approach_ task:
    player within 3 units of item _tri_
    promptmulti 1010 19:pickpocket _pick_ 11:myst _myst_ 5:accept _suffer_

variable _pick_
variable _myst_

_skillpick_ task:
    when skill Pickpocket is at least 30

_picksuccess_ task:
    when _pick_ and _skillpick_
    say 1012

_pickfail_ task:
    when _pick_ and not _skillpick_
    say 1011
    reduce player health by 45

_skillmyst_ task:
    when skill Mysticism is at least 50

_mystsuccess_ task:
    when _myst_ and _skillmyst_
    say 1014

_mystfail_ task:
    when _myst_ and not _skillmyst_
    say 1013
    reduce player health by 60
    make pc ill with Chrondiasis

_suffer_ task:
    say 1015
    reduce player health by 30

_clicky_ task:
    clicked item _tri_
    say 1016

_victory_ task:
    toting _tri_ and _qgiver_ clicked
    give pc _magic_
    end quest