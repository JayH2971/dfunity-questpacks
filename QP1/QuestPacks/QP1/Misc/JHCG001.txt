-- Cartographers Quest
Quest: JHCG001
DisplayName: No Cemetery Unturned
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHCG001

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHCG001

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHCG001

QuestFail:  [1003]
<ce>   You were given =timer_ days to map
<ce>   out ___grav_ for the Cartographers,
<ce>   which have now run out.

QuestComplete:  [1004]
<ce>        Ah, %pcf. Let me take a look at your map. 
<ce>        Well, we weren't so far off after all! Thank you
<ce>        for this information. We've allocated something
<ce>        of a modest reward for you. Continue on, recruit.
<ce>        We'll have more to offer you in the future.
<ce>        Just be patient with us for now while
<ce>        we build our trust in one another.

Message:  1010
<ce>        A page calls out to you. "Hey! %pcf, right?
<ce>        The Cartographers have a message for you!"
<ce>        He hands you a letter and departs.
<--->
<ce>        A page calls out to you. "Hey! %pcf, right?
<ce>        The Cartographers have a message for you!"
<ce>        She hands you a letter and departs.

Message:  1011
  %pcn,
 
  We have need of your services. We must chart the
  distance from __grav_ to ___place_.
  I can mark its approximate location on your map
  if you meet me here. We need this done in =timer_ days.
 
                         _qg_
                         Explorer, Field Officer
                         The Cartographers
                         ___place_, ____place_

Message:  1012
<ce>        %pcf! How good it is to see you. We have
<ce>        another simple task on our hands, it seems.
<ce>        We will pay you, as we always do. Be patient
<ce>        with us and we'll get you moving into more
<ce>        exotic locales.
 
<ce>        As I mentioned, __grav_ is a place
<ce>        we've neglected for more time than we should
<ce>        have. I'll show its approximate location on your
<ce>        map. We need you to provide us precise
<ce>        coordinates and traveling distance.
<ce>        Meet me here once you've charted it.

Message:  1013
<ce>        It looks like this is __grav_. It's incredible
<ce>        that a graveyard could be forgotten, but after
<ce>        all, the Cartographers exist for a reason.
<--->
<ce>        From what you can tell, this is the cemetery
<ce>        _qg_ told you to find. Now
<ce>        to report its location back to %g2.

Message:  1030
%qdt:
 The Cartographers have given me
 an assignment to chart __grav_
 in ____place_. I must deliver my
 report to _qg_ in =timer_ days or less.
 I can also speak to %g2 in ___place_
 for directions.

Message:  1099
<ce>  __grav_ is a lost location
<ce>  here in %crn. We trust you
<ce>  to visit it and bring us details
<ce>  concerning its location.

QBN:
Item _reward_ gold range 150 to 200
Item _letter_ letter used 1011

Place _grav_ remote graveyard
Place _place_ remote tavern

Clock _timer_ 24.00:00 38.00:00

Person _qg_ group Librarian

--	Quest start-up:
          start timer _timer_
          give pc _letter_ notify 1010
          place npc _qg_ at _place_
          pc at _grav_ set _inside_
          have _letter_ set _log_

_log_ task:
    log 1030 step 0

_qgclicked_ task:
	clicked npc _qg_ 

_begin_ task:
     when _qgclicked_ and not _talked_ and not _found_
     say 1012
     reveal _grav_
     clear _qgclicked_
     start task _talked_

variable _talked_
_inside_ task:
     start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_

_victory_ task:
    when _qgclicked_ and _found_
   start quest JHCGENT1
    give pc _reward_
    end quest

_timer_ task:
         say 1003
         start quest JHCGFAIL
         end quest