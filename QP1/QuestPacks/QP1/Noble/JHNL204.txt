Quest: JHNL204
DisplayName: Due Reward
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>  A vassal of mine has made egregious
<ce>  and public mistakes, requiring that
<ce>  an example be made. I need someone
<ce>  to go and fetch this person, that
<ce>  we may carry out an apt sentence.

RefuseQuest:  [1001]
<ce>  Then tell no one of it.

AcceptQuest:  [1002]
<ce>  The imbecile's name is _pers_.
<ce>  ___place_ is %g3 place of
<ce>  residence, though where you'll find %g2
<ce>  is up in the air. Return here with
<ce>  %g2 in =timer_ days for your gold.

QuestFail:  [1003]
<ce>  I don't see _pers_. Do you?

QuestComplete:  [1004]
<ce>  _qgiver_ is reading some papers
<ce>  as you approach. _pers_
<ce>  makes obeisance, but is soon hoisted
<ce>  up by armed guards. A herald calls out.
<ce>  
<ce>  "_pers_ is this day
<ce>  found guilty of treason, embezzlement,
<ce>  public fraud, and whatever other crime
<ce>  adequate to the sentence prescribed
<ce>  by the noble _qgiver_.
<ce>  Your lands are forfeit and your
<ce>  family, destitute. So shall it be
<ce>  unto all who betray %crn!"
<ce>  
<ce>  The vassal is carried outside in a
<ce>  confused, screaming rage, and the
<ce>  blackguard follows. _qgiver_
<ce>  looks up from the papers long enough to
<ce>  hand you a bag of money, and then
<ce>  resumes reading.

RumorsDuringQuest:   [1005]
_qgiver_ has it out for someone, they says.

Message:  1010
<ce>  Oh? The great _qgiver_ requests
<ce>  my presence? Let us go immediately.
<ce>  My tasks here can wait.
<ce>  I will follow you, %ra.
<--->
<ce>  What, _qgiver_ needs me?
<ce>  Yes, let us go right away.
<--->
<ce>  What is this? _qgiver_?
<ce>  I shouldn't delay. I would
<ce>  hate to tarnish my reputation
<ce>  with %g2. Let's go now.
<--->
<ce>  A message from _qgiver_?
<ce>  Yes, I agree, we should hurry.
<ce>  I wouldn't want to alter %g3
<ce>  opinion of me.

Message:  1030
%qdt:
 _qgiver_ has sent me to
 ___place_ to collect a
 failing vassal named _pers_
 and bring %g2 to ___qgiver_
 to settle matters. We must
 return within =timer_ days.

QBN:
Item _reward_ gold

Person _qgiver_ group Questor
Person _pers_ faction Dancers

Place _place_ remote random

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
	start timer _timer_
	log 1030 step 0 
    place npc _pers_ at _place_

_begin_ task:
  clicked npc _pers_
  say 1010
  hide npc _pers_
  add _pers_ face

_pcgetsgold_ task:
  when _qgclicked_ and _begin_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _begin_
    say 1003
	clear _qgclicked_ _clearclick_ 

_timer_ task:
	end quest