Quest: JHTP108
DisplayName: Battery Charge
-- Message panels
QRC:

QuestorOffer:  [1000]
[QP1]
 
<ce>     Not all diseases are made equal. Most require a few earnest
<ce>         castings of Cure Disease before they are purged, but
<ce>          we must be prepared for certain special cases that
<ce>        enter our door. We need someone to recharge one of the
<ce>        relics we use for such rarer diseases. Will you do so?

RefuseQuest:  [1001]
<ce>               Hmm. This isn't particularly difficult.

AcceptQuest:  [1002]
<ce>         Here it is then. You'll find it useless to cure the
<ce>         regular illnesses of the Iliac Bay, so don't expect
<ce>       any miracles if you're in need. Your task is to take it
<ce>                down into ___dung_ for about two days.
<ce>           You must stay underground as long as it takes to
<ce>           recharge its power. You'll know when it's ready.
<ce>            It gives off a distinct glow for a few minutes
<ce>        before subsiding, which is all the signal you'll need.
<ce>       If I can see you here within one month's time, you will
<ce>       have mended our gap and ensured the people of ___qgiver_
<ce>           remain free of the exotic illnesses of Tamriel.
<ce>                        Safe spelunking, %pct.

QuestFail:  [1003]
<ce>              One month has passed since you were given
<ce>             the curing _tri_ and you have not returned.

QuestComplete:  [1004]
<ce>               Ah, there it is. You may not be able to
<ce>         tell anymore, but I can. Thank you for preparing it.
<ce>        Perhaps this clothing will suit your liking in return,
<ce>                        for you and your kin.

Message:  1010
<ce>          It appears you are free to roam ___dung_ until the
<ce>        _tri_ regains its magical energy from the environment.

Message:  1011
<ce>        You become aware of a rapidly accumulating light among
<ce>      your belongings. The _tri_ starts to glow in a crescendo,
<ce>    holding a distinct shine for about a minute. It then subsides
<ce>       and returns to its normal appearance. It must be ready.

Message:  1012
<ce>           A fellow traveler greets you from afar and then
<ce>          starts to continue on. However, the _tri_ in your
<ce>          possession is unusual enough to garner attention.
<ce>
<ce>      "Hey! That's one of those things they use in the temples,
<ce>       right? For the tough diseases? That's worth a mint! I've
<ce>       got =pay_ gold for you if you give it to me, right now!"

Message:  1013
<ce>                             "Your loss."
<ce>
<ce>                         You are left alone.

Message:  1014
<ce>   "Oh, good! Thank you, %ra. I don't know how you got one of them,
<ce>     but it'll make a big splash back home. What a clever gift!"
<ce>
<ce>       Your new friend hands you the money and takes the _tri_.
<ce>                 You see a good opportunity so that,
<ce>           if you wanted, you could take it back by force.

Message:  1015
<ce>                       (STRENGTH CHECK FAILED)
<ce>
<ce>      A hooded figure emerges from some nearby shadows and takes
<ce>       the _tri_ from among your possessions. You see the thief
<ce>                hurry further down the dark passages.

Message:  1016
<ce>      "Hey, I'm sorry! Those things are really valuable, is all!
<ce>                Here, take it back. Just let me live."

Message:  1017
<ce>                       (STRENGTH CHECK PASSED)
<ce>
<ce>      A hooded figure emerges from some nearby shadows to steal
<ce>           the _tri_ from you. Your foe underestimates your
<ce>        strength and slips awkwardly while trying to wrench it
<ce>      from you. Rather than try again, the would-be thief flees
<ce>                from any retribution you might extend.

Message:  1018
<ce>                  Power shoots forth from the _tri_.
<ce>    Whatever illness this is intended to cure, you don't have it.

Message:  1019
<ce>              "Hey, what's with that look in your eyes?"

Message:  1020
<ce>     You obtain the _tri_ from your unexpected customer's corpse.

Message:  1021
<ce>                Your customer escapes with the _tri_.

Message:  1030
%qdt:
 _qgiver_ of __qgiver_
 has sent me to ___dung_
 to recharge a magical _tri_ capable
 of alleviating rare illnesses.
 I should return it to ___qgiver_
 within one month.

QBN:
Item _tri_ trinket
Item _pay_ gold range 4100 to 5700
Item _mclo_ mens_clothing
Item _fclo_ womens_robe
Item _ring_ ring

Person _qgiver_ group Questor

Foe _thief_ is Thief
Foe _cust_ is Knight

Place _dung_ remote dungeon2

Clock _timer_ 30.00:00
Clock _recharge_ 1.16:00 1.23:00
Clock _event_ 8:00 1.00:00
Clock _escape_ 0:05

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    get item _tri_
    pc at _dung_ set _inside_
    have _tri_ set _holding_
    reveal _dung_

_inside_ task:
    say 1010
    start timer _recharge_
    start timer _event_

_stop_ task:
    when not _inside_
    stop timer _recharge_
    stop timer _event_

variable _recharge_
variable _holding_
_strength_ task:
    when attribute Strength is at least 80

_event_ task:
    pick one of _e1_ _e2_ _e3_

_e1_ task:
    prompt 1012 yes _sell_ no _refuse_

_sell_ task:
    prompt 1014 yes _betray_ no _escape_
    get item _pay_
    take _tri_ from pc

_betray_ task:
    say 1019
    create foe _cust_ every 0 minutes 1 times with 100% success
    restrain foe _cust_
    start timer _escape_

_escape_ task:
    say 1021
    remove foe _cust_
    end quest

_murder_ task:
    killed _cust_
    say 1020
    get item _tri_

_refuse_ task:
    say 1013

variable _e2_
_lose_ task:
    when _e2_ and not _strength_
    say 1015
    take _tri_ from pc
    place foe _thief_ at _dung_
    clear _holding_

_inj_ task:
    injured _thief_
     clicked foe _thief_
    say 1016
    get item _tri_
    restrain foe _thief_
    change foe _thief_ infighting true

_defend_ task:
    when _e2_ and _strength_
    say 1017

variable _e3_
_finish_ task:
    when _recharge_ and _holding_
    say 1011
    _tri_ used do _use_

_use_ task:
    say 1018

_victory_ task:
    when _qgclicked_ and _finish_
    give pc _mclo_
    give pc _fclo_
    give pc _ring_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _finish_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest