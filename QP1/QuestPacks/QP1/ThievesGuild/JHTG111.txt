Quest: JHTG111
DisplayName: A Bit of Bribery
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>         One of the family's got a rather vindictive enemy in
<ce>               %crn's courts. This noble's been hauling
<ce>        the fellow to jail every few months for petty crimes.
<ce>           We need someone to help bribe the problem away.

RefuseQuest:  [1001]
<ce>               Someone's gonna come around and do this.

AcceptQuest:  [1002]
<ce>           Word has it the noble's right hand mutt, _kni_,
<ce>       has a soft spot for precious stones. We've been watching
<ce>          a particular shipment of a flawless kind of _gem_
<ce>         that should catch %g3 attention. Grab a sample from
<ce>                    _jewel_ in ___jewel_ and then
<ce>                get it to _kni_. Lives in ___knitown_.
<ce>        Do it at night when %g1's not working and there won't
<ce>               be as many witnesses. I'll have =reward_
<ce>             gold for you if you're here in =timer_ days.

QuestFail:  [1003]
<ce>                 You were to deliver the stolen _gem_
<ce>                  and report to _questgiver_ by now.

QuestComplete:  [1004]
<ce>                     Tell me, how'd _kni_ react?
<ce>      I figured %g1'd put up a show. Nice work. Take the money.

Message:  1011
<ce>                     (PICKPOCKETING CHECK FAILED)
<ce>
<ce>         "Thief! I'm sick of you lot! I'll kill you myself!"

Message:  1012
<ce>                     (PICKPOCKETING CHECK PASSED)
<ce>
<ce>          You deftly put the gem away. No one is the wiser.

Message:  1013
<ce> "Good evening. I fail to recognize your face. State your business."

Message:  1014
<ce>        "My word, what an atrocious proposal you make. You say
<ce>        I ought to let your friend go and present me with this
<ce>          _gem_ in exchange? Tell me, have you a good reason
<ce>             behind this blatant perversion of justice?"

Message:  1015
<ce>               "Preposterous. And what might that be?"
<ce>
<ce>        You explain that the guild member is suffering from a
<ce>    vindictive noble's wrath rather than the weight of any crime.
<ce>
<ce>    "That is quite difficult for me to believe. Our justice system
<ce>       is impeccable, and every accusation by our fair monarchy
<ce>            must be well-founded. Don't you believe that?"

Message:  1016
<ce>      "Ha! I should call the town guard on you for trespassing.
<ce>        I never once gave you permission to enter. But lest I
<ce>          be called uncivilized, I will permit you to leave
<ce>                  without further issue. Good day."
<ce>
<ce>                _kni_ extends %g3 hand to shake yours.
<ce>                           Will you do so?

Message:  1017
<ce>  Still holding the _gem_, you take %g3 hand and make the transfer.
<ce>                   _kni_ nods knowingly and winks.
<ce>             Your fellow guild member will soon be free.

Message:  1018
<ce>                   "As I thought. Begone, chattel!"

Message:  1019
<ce> "Indeed, and I would be a fool to consort any further with you. Begone."

Message:  1020
<ce>               _kni_ frowns and reaches %g3 hand back.
<ce>      It appears your fellow guild member will not be set free.

Message:  1030
%qdt:
 I'm being sent to ___jewel_ to get a
 particular _gem_ from _jewel_,
 which I need to give to some servant
 named _kni_ who lives in ___knitown_.
 I'll get paid =reward_ gold if I'm back
 to the Thieves Guild in ___questgiver_
 in =timer_ days. If I don't find
 the servant at home, I should wait
 'til night.

QBN:

Person _questgiver_ group Questor male
Person _kni_ faction Random_Knight

Item _gem_ gem
Item _reward_ gold range 3600 to 4100

Place _knitown_ remote house
Place _jewel_ remote jewelryshop

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Healer

--   Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _gem_ at _jewel_
    place npc _kni_ at _knitown_

_got_ task:
    clicked item _gem_

_skill_ task:
    when skill Pickpocket is at least 60

_notfree_ task:
    when _got_ and not _skill_
    say 1011
    create foe _enemy_ every 0 minutes 1 times with 100% success

_free_ task:
    when _got_ and _skill_
    say 1012

_day_ task:
    daily from 6:00 to 18:00

_hide_ task:
    when _day_
    hide npc _kni_

_restore_ task:
    when not _day_
    restore npc _kni_

_kclearclick_ task:
    when _kniclicked_ and not _got_
    clear _kniclicked_ _kclearclick_

_kniclicked_ task:
    clicked npc _kni_

_kcontinue_ task:
    when _kniclicked_ and _got_
    say 1013
    _gem_ used do _talk1_

_talk1_ task:
    prompt 1014 yes _talk2_ no _fail1_

_fail1_ task:
    say 1018
    make _gem_ permanent
    end quest

_talk2_ task:
    prompt 1015 yes _fail2_ no _talk3_

_fail2_ task:
    say 1019
    make _gem_ permanent
    end quest

_talk3_ task:
    prompt 1016 yes _victory_ no _fail3_

_fail3_ task:
    say 1020
    make _gem_ permanent
    end quest

_victory_ task:
    say 1017
    take _gem_ from pc

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _clearclick_

_pcgetsreward_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest