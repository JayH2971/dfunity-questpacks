Quest: JHTG121
DisplayName: Petty Theft
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  You know how to pick pockets, %pct?
<ce>  We got someone over at a tavern in town
<ce>  holding something they shouldn't.
<ce>  We need it in our hands by the end of the day.

RefuseQuest:  [1001]
<ce>   Not a popular art. Shame on us as thieves.

AcceptQuest:  [1002]
<ce>  All right, the mark's in _tav_,
<ce>  off %di here in ___tav_.
<ce>  Look for a bloke with a scar on his chin,
<ce>  red hair. In one of his pockets he's got
<ce>  a piece of paper with some important information on it.
<ce>  What that is, you don't need to know.
<ce>  Get it here before one day's up.
<ce>  There's =gold_ gold riding on this.

QuestFail:  [1003]
<ce>         The Thieves Guild needed you to steal and
<ce>  deliver the paper from the red-haired man by now.

QuestComplete:  [1004]
<ce>  Here it is. I don't hear anyone shouting halt,
<ce>  so I'll assume the job was clean. Nice work.
<ce>  We could use more pickpockets like you.

QuestorPostSuccess:   [1008]
How're them fingers doing? Steal anything nice lately?

QuestorPostFailure:   [1009]
Yeah, our so-called pickpocket. Guild's gonna be a laughingstock at this rate.

Message:  1010
<ce>  As luck would have it, your mark is about to leave the tavern.
<ce>  He matches the description. If you're going to steal from him,
<ce>  this is your chance. Will you take it?
<ce>
<ce>              (SKILL CHECK: YES: Pickpocket, difficulty low)

Message:  1011
<ce>  The words on this paper shift around.
<ce>  You'd need some spell to read them.

Message:  1012
<ce>                         (SKILL CHECK FAILED)
<ce>
<ce>  You wait until he passes before reaching into the back pocket
<ce>  of his coat. Not only is it empty, he notices your attempt.
<ce>  The man delivers you a swift backhand before calling for the guards
<ce>  and fleeing into the town.

Message:  1013
<ce>  You could get into real trouble doing something like this.
<ce>  You step aside and let the man leave the tavern.
<ce>  _qgiver_ may not be happy, but you feel better this way.

Message:  1014
<ce>                (SKILL CHECK PASSED)
<ce>  
<ce>  You know how to deal with this.
<ce>  You eye the man's pockets and casually bump into him.
<ce>  He's angry with the confrontation, but doesn't notice
<ce>  your theft. You apologize and he leaves in a huff.

Message:  1030
%qdt:
 Some man with red hair and a chin scar
is in _tav_, holding a paper of worth
to the Thieves Guild. I need to liberate
it and deliver it to _qgiver_ here
in ___tav_ before one day passes.
My reward will be =gold_ gold pieces.

QBN:
Item _gold_ gold range 450 to 600
Item _letter_ letter used 1011

Place _tav_ local tavern

Person _qgiver_ group Questor

Clock _timer_ 1.00:00
Clock _delay_ 0:02

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _tav_ do _inside_

_skilled_ task:
    when skill Pickpocket is at least 25

_inside_ task:
    prompt 1010 yes _attempt_ no _flee_

_flee_ task:
    say 1013
    end quest

variable _attempt_

_theft_ task:
    when _attempt_ and _skilled_
    get item _letter_
    say 1014

_victory_ task:
    toting _letter_ and _qgiver_ clicked
    give pc _gold_
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    remove log step 0
    stop timer _timer_
    reduce player health by 10

_outside_ task:
    when _bigfail_ and not _inside_
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest
