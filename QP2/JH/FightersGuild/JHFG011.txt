Quest: JHFG011
DisplayName: Ancient Artifact
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>	Hey %pcf, I've got some news you might be
<ce>	 interested in hearing. Turns out in some cave around
<ce>	 here there's an artifact, like the real, ancient, powerful
<ce>	kind. Heard of Auriel's Bow, Chrysamere, that kind
<ce>	of thing? This is one of those artifacts. It's being held
<ce>	by a vampire ancient way down in there, guarded by
<ce>	a whole cult of his. I've talked with some of the other
<ce>	guild members, and we've all agreed we're not touching
<ce>	it with a ten-foot pole. Then you showed up and I figured
<ce>	 I'd tell you. No shame if you fail to get it, all right? I'll
<ce>	 tell you where it is if you're interested.

RefuseQuest:  [1001]
<ce>	Good. I really don't want to lose you down there.

AcceptQuest:  [1002]
<ce>	Well, all right then. It's in ___dung_.
<ce>	Take everything you've got. I said there's a cult
<ce>	down there, and I'm not kidding. Those folks are nuts,
<ce>	but like loony. It's gonna be a fight for your life 'til you
<ce>	find the head honcho. If it gets bad, promise me you're
<ce>	gonna run, all right? Good luck down there buddy.
<ce>	Come back in one piece.

QuestFail:  [1003]

QuestComplete:  [1004]
<ce>	With the artifact in your hands, this cult has
<ce>	 lost its attraction. Your quest is complete.

RumorsDuringQuest:  [1005]
There's a cult down in ___dung_. Don't bother no one, though.

RumorsPostfailure:  [1006]

RumorsPostsuccess:  [1007]

QuestorPostsuccess:  [1008]
So you actually beat the cult. You're a legend.

QuestorPostfailure:  [1009]

Message:  1019
<ce>	You immediately notice a powerful aura of death in
<ce>	 ___dung_. The cult takes notice of you.

Message:  1021
<ce>	 The cult's numbers appear endless.
<ce>	  You may wish to reconsider.

Message:  1020
<ce>	The vampire ancient emits a shrill hiss.
<ce>	Waves of power seem to pulse through the air.

Message:  1030
%qdt:
 _qgiver_ in ___qgiver_
 told me about a powerful
 artifact being held by a vampire
 ancient in ___dung_.
 If I'm feeling brave, I can fight it
 and its cult to win the prize.

QBN:
Person _qgiver_ group Questor male

Place _dung_ remote dungeon

Clock _timer_ 60.00:00

Foe _anc_ is Ancient_vampire
Foe _e1_ is Mage
Foe _e2_ is Battle_mage
Foe _e3_ is Vampire
Foe _e4_ is Spellsword
Foe _e5_ is Healer
Foe _e6_ is Barbarian

Item _art1_ artifact Auriels_Bow
Item _art2_ artifact Auriels_Shield
Item _art3_ artifact Chrysamere
Item _art4_ artifact Lords_Mail
Item _art5_ artifact Necromancers_Amulet
Item _art6_ artifact Staff_of_Magnus
Item _art7_ artifact Warlocks_Ring

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0 
	place foe _anc_ at _dung_
	pick one of _a1_ _a2_ _a3_ _a4_ _a5_ _a6_ _a7_
	reveal _dung_
	pc at _dung_ set _inside_

_inside_ task:
	start task _spawn_
	say 1019
	create foe _e3_ every 1 minutes 1 times with 95% success
	create foe _e4_ every 1 minutes 1 times with 95% success
	create foe _e6_ every 1 minutes 1 times with 95% success

_spawn_ task:
	when _spawn_ and _inside_
	create foe _e1_ every 2 minutes indefinitely with 20% success
	create foe _e2_ every 3 minutes indefinitely with 30% success
	create foe _e3_ every 4 minutes indefinitely with 40% success
	create foe _e4_ every 5 minutes indefinitely with 50% success
	create foe _e5_ every 6 minutes indefinitely with 60% success
	create foe _e6_ every 7 minutes indefinitely with 70% success

_warning_ task:
	killed 3 _e1_
	killed 3 _e2_
	killed 3 _e3_
	killed 3 _e4_
	killed 3 _e5_
	killed 3 _e6_
	say 1021

_a1_ task:
	give item _art1_ to _anc_
	have _art1_ set _victory_

_a2_ task:
	give item _art2_ to _anc_
	have _art2_ set _victory_

_a3_ task:
	give item _art3_ to _anc_
	have _art3_ set _victory_

_a4_ task:
	give item _art4_ to _anc_
	have _art4_ set _victory_

_a5_ task:
	give item _art5_ to _anc_
	have _art5_ set _victory_

_a6_ task:
	give item _art6_ to _anc_
	have _art6_ set _victory_

_a7_ task:
	give item _art7_ to _anc_
	have _art7_ set _victory_

_inj_ task:
	injured _anc_
	say 1020

_victory_ task:
	make _art1_ permanent
	make _art2_ permanent
	make _art3_ permanent
	make _art4_ permanent
	make _art5_ permanent
	make _art6_ permanent
	make _art7_ permanent
	give pc nothing
	end quest

_timer_ task:
	change repute with _qgiver_ by +3
	end quest