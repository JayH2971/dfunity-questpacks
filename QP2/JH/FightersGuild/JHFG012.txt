Quest: JHFG012
DisplayName: Wizard Amok
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> We've got a request here from ___res_
<ce> to go off some fellow wreaking havoc
<ce> around the city. Apparently some member
<ce> of the Mages Guild who's on _drug_,
<ce> or so they say. It's paying =gold_ gold.
<ce> Wanna take it?

RefuseQuest:  [1001]
<ce> Weird, I thought this was gonna be
<ce> easy. No one wants to take it.

AcceptQuest:  [1002]
<ce> The contract says that this so-called
<ce> "_dummy_" has to be dead
<ce> within =2res_ days. After that just
<ce> come pick up your money whenever you
<ce> want. Go find %g2 in ___res_.
<ce> Ask around if %g3 house isn't
<ce> obvious. Stay safe.

QuestFail:  [1003]
<ce> Your contract to slay _dummy_
<ce> in ___res_ ends today.

QuestComplete:  [1004]
<ce> Hey, welcome back. Good to see you
<ce> in one piece. Here's your gold.

Message:  1022
<ce> Fool! How dare you attack
<ce> a member of the Mages Guild!

Message:  1032
%qdt:
 _questgiver_ of the Fighters Guild
 has contracted me to slay someone
 named _dummy_ in ___res_
 on behalf of the city. I have
 =2res_ days to do it, and can
 get my reward any time after that.

QBN:
Person _questgiver_ group Questor male
Person _dummy_ faction The_Mages_Guild

Place _res_ remote house1

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00

Item _gold_ gold
Item _drug_ drug

Foe _mage_ is Spellsword

--	Quest start-up:
	start timer _2res_ 
    start timer _failsafe_ 
	place foe _mage_ at _res_ 
    place npc _dummy_ at _res_
    hide npc _dummy_
	log 1032 step 0 

_ipr_ task:
          injured _mage_
          say 1022

_slain_ task:
	killed 1 _mage_
    change repute with _dummy_ by -10
    stop timer _2res_

_qgclicked_ task:
	clicked npc _questgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_ 
	clear _qgclicked_ _clearclick_ 

_pcreward_ task:
	when _qgclicked_ and _slain_ 
	give pc _gold_
	end quest 

    _2res_ task:
	say 1003
    end quest

_failsafe_ task:
    end quest