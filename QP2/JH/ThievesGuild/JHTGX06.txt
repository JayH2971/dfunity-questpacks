Quest: JHTGX06
DisplayName: Party Crasher
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTG06X

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTG06X

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTG06X

QuestFail:  [1003]
<ce> ERROR 1003 JHTG06X

QuestComplete:  [1004]
<ce> Talk about reliable. Did you even break a sweat?

RumorsDuringQuest:   [1005]
They say The Crow's showing up in the Bay again. I think it's an impostor.
<--->
Y'hear about The Crow showing up in some town? Probably lies.
<--->
They say The Crow's around, but I thought he died awhile ago.

Message:  1010
<ce> I've got one final irregular job before we can
<ce> start up a rhythm. If you're constant, I can get
<ce> a stream of jobs coming in. You'll be able to make
<ce> as much money as you want.
<ce> 
<ce> I tried looking around, and there's three members of the
<ce> Fighters Guild who waste their time in ___weap_.
<ce> Look for someone named _fg_. If you're
<ce> lucky at all, you'll find all three together and get
<ce> it done. Get all three tongues and a kidney.
<ce> I'll be paying =reward_ this time. Meet me
<ce> in ___crowhome_, at _crowhome_.

Message:  1030
%qdt:
 The Crow's giving me one
 last job before we start a
 formal, consistent employment.
 I should head to ___weap_
 and look for _fg_.
 There'll be three people in all,
 and I need to deliver their tongues
 and one kidney to _crow_
 at _crowhome_, in
 ___crowhome_.

Message:  1060
<ce> You only barely enter the building when
<ce> the walls shake from a deafening scream.
<ce> 
<ce> "It's %pcf! Kill %pg2! NOW!!"

Message:  1070
<ce> With the Fighters Guild down,
<ce> it's time to take the organs.

Message:  1071
<ce> You stuff the bag with three tongues and a kidney and seal it.

Message:  1080
<ce> Do you wish to leave behind the _lode_
<ce> and give up your ties to _crow_?

Message:  1081
<ce> You discard the small piece of metal.
<ce> Whatever schemes he has are his alone now.

Message:  1082
<ce> You return the _lode_ to your
<ce> belongings, at least for now.

Message:  1099
<ce> All right, you've made it. You have now completely
<ce> earned my trust. I've gotten my responses from the
<ce> people over in "Bruma" as they call it, and they're
<ce> ready to do business. Let me see what I have here.

QBN:

Person _crow_ named The_Crow faction The_Crow male
Person _fg_ faction The_Fighters_Guild

Place _crowhome_ remote house4
Place _weap_ remote weaponstore

Item _reward_ gold range 2801 to 3200
Item _lode_ lodestone
Item _bag_ small_sack
Item _bag2_ small_sack

Foe _warr_ is Barbarian

--	Quest start-up:
    say 1010
    log 1030 step 0
    get item _lode_
    get item _bag_
    place npc _fg_ at _weap_
    _lode_ used do _prompt_
    pc at _weap_ do _inside_

_inside_ task:
    hide npc _fg_
    say 1060
    create foe _warr_ every 0 minutes 3 times with 100% success

_prompt_ task:
    prompt 1080 yes _quit_ no _stay_

_quit_ task:
    say 1081
    end quest

_stay_ task:
    say 1082

_slain_ task:
    killed 3 _warr_
    say 1070
    change repute with _fg_ by -45
    _bag_ used do _take_
    place npc _crow_ at _crowhome_

variable _take_
_take2_ task:
    when _inside_ and _take_
    take _bag_ from pc
    get item _bag2_
    say 1071

_talk_ task:
    toting _bag2_ and _crow_ clicked
    give pc _reward_
    say 1099
    start quest JHTGX07
    end quest