Quest: JHIM003
DisplayName: An Ordinary Haunting
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                   Please, help me! There's a ghost
<ce>                  in my house! Will you get it out?
                                     <--->
<ce>                        _ghostres_ is haunted!
<ce>                    I need someone's help! Please!

RefuseQuest:  [1001]
<ce>                    Why do you refuse to help me?!

AcceptQuest:  [1002]
<ce>                 Please, go to _ghostres_ and destroy
<ce>             it! I can't bear this any longer! I'll give
<ce>                 you everything I have, just help me!
<ce>                 I can't spend another day like this!

QuestFail:  [1003]
<ce>            _questgiver_ asked you to remove the ghost in
<ce>             _ghostres_ in one day, which has now passed.

QuestComplete:  [1004]
<ce>                 Thank you! Thank you! All I have are
<ce>               these _reward_ gold pieces. Please take
<ce>                     them and my deepest thanks.

Message:  1020
<ce>                   The malignant spirit fades away.

Message:  1030
%qdt:
 _questgiver_, of
 ___questgiver_, has
 a ghost haunting %g3 house.
 I should destroy it in one day.

Message:  1099
<ce>                         _ghostres_ is still
<ce>                   haunted! Why won't you help me?!

QBN:
Item _reward_ gold range 200 to 299

Person _questgiver_ group Questor male

Place _ghostres_ local house2

Clock _timer_ 1.00:00

Foe _ghost1_ is Ghost
Foe _ghost2_ is Wraith

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pick one of _D.01_ _D.02_

_D.01_ task:
	place foe _ghost1_ at _ghostres_
	change foe _ghost1_ infighting true

_D.02_ task:
	place foe _ghost2_ at _ghostres_
	change foe _ghost2_ infighting true

_slain_ task:
	killed 1 _ghost1_ saying 1020
	killed 1 _ghost2_ saying 1020

_qgclicked_ task:
	clicked npc _questgiver_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_timer_ task:
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	say 1099
	clear _qgclicked_ _clearclick_