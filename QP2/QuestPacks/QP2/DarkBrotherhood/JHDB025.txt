Quest: JHDB025
DisplayName: A Fighter Astray
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                   Mephala watch your blade, %pct.
<ce>               Your loyalty to the Brotherhood exceeds
<ce>            all other concerns. Prove it by disposing of a
<ce>       member of the Fighters Guild who has taken up residence
<ce>                      in one of %crn's caverns.

RefuseQuest:  [1001]
<ce>           Then find some other way to prove your loyalty.
<ce>              But prove it, lest your time come as well.

AcceptQuest:  [1002]
<ce>                 You will travel to ___dung_ and find
<ce>       the fighter inside. Use whatever method best suits you.
<ce>     The more evident that this was the work of the Brotherhood,
<ce>         the better for us all. Let none cross our reputation
<ce>    with impunity. The fool will only stay there for =2res_ days,
<ce>      so ensure the killing is done by then. Report your success
<ce>                       at any time after that.

QuestFail:  [1003]
<ce>          The Fighters Guild mark has fled ___dung_ by now.

QuestComplete:  [1004]
<ce>        Some in this hall feared you had loyalties elsewhere.
<ce>               I am happy to see the contrary. We don't
<ce>          always compensate our contracts, but I happened to
<ce>         get my hands on this _magic_ that can paralyze foes,
<ce>                        and I thought of you.
<ce>          May Mephala's foes know true terror at your hands.

Message:  1021
<ce>                        I won't go down easy!
<ce>                 I'm a member of the Fighters Guild!
                                     <--->
<ce>             Ha! I'll show you what the guild taught me!

Message:  1022
Quest log:
_fg_ of ___fg_ has
tasked me with traveling to ___dung_
and slaying all the spiders inside it.
I have to return within =random_ days
for my pay of =gold_ gold.

Message:  1031
%qdt:
_questgiver_ assigned me
to slay a member of
the Fighters Guild in ___dung_
before =2res_ days have passed.
I can return to the Brotherhood
in ___questgiver_ at any time afterward.

Message:  1041
<ce>            The Fighters Guild now has one less mercenary
<ce>            to count on. The Brotherhood will be pleased.

QBN:
Person _questgiver_ group Questor male
Person _dummytg_ faction The_Fighters_Guild

Place _fg_ remote fighters
Place _dung_ remote dungeon6

Item _random_ gold range 12 to 22
Item _gold_ gold range 121 to 449
Item _magic_ item class 4 subclass 35
Item _letter_ letter

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00

Foe _thief_ is Warrior

--	Quest start-up:
    start timer _2res_
    start timer _failsafe_
    place foe _thief_ at _dung_
    log 1031 step 0
    reveal _dung_

_ith_ task:
    injured _thief_
    say 1021
    change foe _thief_ infighting true
    give item _letter_ to _thief_
    _letter_ used do _readletter_

_readletter_ task:
    say 1022
    take _letter_ from pc

_slain_ task:
    killed 1 _thief_ saying 1041
    change repute with _dummytg_ by -20
    stop timer _2res_

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _magic_
    end quest

_2res_ task:
    say 1003
    end quest

_failsafe_ task:
    end quest