Quest: JHDB127
DisplayName: No Thieves Allowed
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  Idiocy in Tamriel seems to know no bounds.
<ce>  Imagine the stupidity of a person who
<ce>  enters a Dark Brotherhood sanctuary
<ce>  unannounced and invited, and begins
<ce>  taking valuable items from off the ground.
<ce>  Such is the quality of people the Thieves Guild
<ce>  is turning out these days. If you will slay this
<ce>  thief, you'll be rewarded with an item
<ce>  that will empower you to swim like
<ce>  the swiftest of fish.

RefuseQuest:  [1001]
<ce>  The Thieves Guild's members are pitifully weak.
<ce>  This job will be a Sundas stroll for someone else.

AcceptQuest:  [1002]
<ce>  I pray that the Thieves Guild will learn to keep
<ce>  their members to stay out of our halls, and soon.
<ce>  The fool recently left town, and now stays at
<ce>  _tav_ of ___tav_. So says the Spymaster.
<ce>  Return with news of the thief's death
<ce>  within =timer_ days.

QuestFail:  [1003]
<ce>     The Spymaster is not wrong, %pcf.
<ce>  Find and kill the thief at _tav_ of ___tav_.

QuestComplete:  [1004]
<ce>  To be honest, I hoped you would accept this contract.
<ce>  Our members are expected to put the Night Mother's wishes
<ce>  above all other considerations. The Thieves Guild's
<ce>  loss is your gain -- and your new _reward_ proves it.

Message:  1010
<ce>  Somewhere in this tavern is the thief
<ce>  you have to slay.

Message:  1011
<ce>  "Oh yeah? I'm tougher than you!"

Message:  1012
<ce>  The thief is dead.

Message:  1030
%qdt:
 The Dark Brotherhood of ___questgiver_
 will give me an object empowered with
 swift swimming if I kill a member of
 the Thieves Guild who stole from us.
 The thief stays at _tav_ of ___tav_.
 I must report my success within =timer_ days.

QBN:
Person _questgiver_ group Questor male
Person _fgperson_ faction The_Thieves_Guild remote

Place _tav_ remote tavern

Clock _timer_ 00:00 0 flag 1 range 0 2

Item _reward_ item class 4 subclass 10

Foe _enemy_ is Thief

--	Quest start-up:
    start timer _timer_
    place foe _enemy_ at _tav_
    change foe _enemy_ infighting true
    log 1030 step 0
    pc at _tav_ do _inside_
    hide npc _fgperson_

_inside_ task:
    say 1010

_inj_ task:
    injured _enemy_

_talk_ task:
    when _inj_ and not _victory_
    say 1011
    cast Feet_of_Notorgo spell on _enemy_
    cast Nimbleness spell on _enemy_
    cast Shield spell on _enemy_

_victory_ task:
    killed _enemy_
    say 1012

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    say 1003
    clear _qgclicked_ _clearclick_
    
_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    start quest JHDBC02
    end quest

_timer_ task:
    end quest