Quest: JHFG037
DisplayName: Repaying the Dark Brotherhood
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>    Okay, so, this one. It's for killing a person,
<ce>  but let me give you the background first.
<ce>  The old lady who's paying us is a widow
<ce>  with only one son... well, HAD one son.
<ce>  The Dark Brotherhood got him, and now that
<ce>  she's destitute, she's using
<ce>  her savings for vengeance. That motivate you?

RefuseQuest:  [1001]
<ce>           I guess it takes the right kind of person.

AcceptQuest:  [1002]
<ce>       Okay, well, watch your back. The Dark Brotherhood
<ce> ain't known for selling flowers or doing chores.
<ce> You're gonna find the perp in ___res_.
<ce> Ask around until you find _res_, and get it done.
<ce> When you're finished, come back to ___tav_
<ce> and find _woman_ at _tav_.
<ce> That's where she's living 'til her savings run out.
<ce> Go pick up your =gold_ gold pieces from her before =timer_ days,
<ce> that's the limit on this contract. Stay safe.

QuestFail:  [1003]
<ce>      Your contract for the widow at _tav_,
<ce>  _woman_, has expired.

QuestComplete:  [1004]
<ce>       _woman_ is overjoyed that justice has been done.

Message:  1010
%qdt:
 The Fighters Guild of
 ___tav_ has sent me to
 ___res_ to slay a Dark Brotherhood member
 in _res_. Afterwards, I am to claim my
 =reward_ gold pieces from _woman_, who
 stays at _tav_ here in ___tav_.
 The contract is void after =timer_ days.

Message:  1011
<ce>      (ATTRIBUTE CHECK FAILED: AGILITY)
<ce>  
<ce>  Paranoia and precaution go hand in hand with the Brotherhood.
<ce>  There's a trap guarding the entrance to this house,
<ce>  and you're too clumsy to avoid it.
<ce>  Painful barbs dig into your skin,
<ce>  coated with a dark liquid.

Message:  1012
<ce>     The widow has been avenged.

Message:  1013
<ce>      (ATTRIBUTE CHECK PASSED: AGILITY)
<ce>  
<ce>  Paranoia and precaution go hand in hand with the Brotherhood.
<ce>  There's a trap guarding the entrance to this house,
<ce>  but you artfully dodge it.
<ce>  It's a good thing, too; those spiked barbs would have
<ce>  done real damage to your neck.

Message:  1014
<ce>  You introduce yourself to _woman_
<ce>  and detail how you disposed of her son's assassin.
<ce>  Relief washes over her.
<ce>  
<ce>  "Oh, thank you, thank you, %ra.
<ce>  To think I'll be able to pass on without burdens to carry!
<ce>  The Fighters Guild are champions of us
<ce>  common folk. Here, let me get that gold for you."
<ce>  
<ce>  She begins rummaging for your =reward_ gold pieces.
<ce>  Knowing that she's destitute and likely on her last
<ce>  few coins, you could tell her to keep the money instead.
<ce>  Will you accept her payment?

Message:  1015
<ce>  You take the money she offers you.
<ce>  The guild isn't a charity, and lowering your price
<ce>  could impair other members' possibilities to make a living.
<ce>  Besides, you worked for this money.

Message:  1016
<ce>  You tell _woman_ that the money won't be necessary.
<ce>  She blinks in confusion, and then tries to hide her tears
<ce>  of gratitude. Without further explanation, she merely smiles
<ce>  at you and puts the coins back in her purse, saying,
<ce>  
<ce>  "I still have some connections to the merchants here in %crn.
<ce>  They'll be hearing about you and your kindness."

QBN:
Item _reward_ gold range 300 to 400

Person _woman_ faction The_Sisters_of_Kykos female
Person _merch_ faction The_Merchants
Person _db_ faction The_Dark_Brotherhood

Place _res_ remote house1
Place _tav_ local tavern

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Nightblade

--	Quest start-up:
	start timer _timer_
	log 1010 step 0
	place foe _enemy_ at _res_
	change foe _enemy_ infighting true
    pc at _res_ do _inside_

_will_ task:
	when attribute Agility is at least 35

variable _inside_

_passwill_ task:
	when _inside_ and _will_
    say 1013

_failwill_ task:
	when _inside_ and not _will_
	say 1011
    reduce player health by 25
	make pc ill with Witches'_Pox

_slain_ task:
	killed _enemy_
	say 1012
    place npc _woman_ at _tav_
    change repute with _db_ by -20

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
    prompt 1014 yes _accept_ no _refuse_
    start quest JHFGC06

_qgclicked_ task:
	clicked npc _woman_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_accept_ task:
    say 1015
    give pc _reward_
    end quest

_refuse_ task:
    say 1016
    change repute with _merch_ by +10
    give pc nothing
    end quest

_timer_ task:
	say 1003
	end quest