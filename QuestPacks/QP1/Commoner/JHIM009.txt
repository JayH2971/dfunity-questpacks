Quest: JHIM009
DisplayName: Surprise!
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  Hey, some friends and I are doing a sort of
<ce>  birthday celebration for a buddy of ours,
<ce>  and we need someone on the outside.
<ce>  We'll pay you =reward_ gold for helping us.
<ce>  No fighting or danger, we promise.

RefuseQuest:  [1001]
<ce>  %oth. Well, good luck to you.

AcceptQuest:  [1002]
<ce>  All right, so here's the plan. We're doing this
<ce>  sort of mock abduction thing. You're gonna
<ce>  pretend to kidnap him and take him all the
<ce>  way to ___dung_, where we'll
<ce>  meet you and give him the birthday surprise.
<ce>  We've got someone over at his house to tell us
<ce>  when you arrive and leave, so we won't be too
<ce>  far behind. We also let the town guard know,
<ce>  and they're okay with it for now. Our buddy's
<ce>  name is _friend_. He'll be
<ce>  at _res_ tonight after 8.
<ce>  Make sure you get him tonight.
<ce>  Cover his head with this bag and get
<ce>  him moving. See you on the other side.

QuestFail:  [1003]
<ce>  You've taken too long. _questgiver_,
<ce>  who was following from afar off with friends,
<ce>  take _friend_ and spoil the
<ce>  surprise out of fear that you've taken it
<ce>  too far. You receive no payment.

QuestComplete:  [1004]
<ce>  _friend_ moans fearfully.
<ce>  You wonder how long this charade will go on.
<ce>  
<ce>  Then you hear many voices shout, "SURPRISE!"
<ce>  
<ce>  A crowd of his friends surround him, undo his
<ce>  bands, and laugh about the great time they
<ce>  had planning it. You receive opulent praise for
<ce>  your performance in the act. As everyone heads
<ce>  outside, _questgiver_ hands you
<ce>  the promised reward and thanks you.

Message:  1010
<ce>  Yes, I'm _friend_.
<ce>  How can I help you?

Message:  1011
<ce>  You cover _friend_'s head with
<ce>  the cloth sack and begin to drag him around.
<ce>  He yells for help but no one intervenes.
<ce>  Whether they're all in on it or are actually
<ce>  terrified of you is yet to be seen.

Message:  1030
%qdt:
 _questgiver_ of
 ___res_ has asked me to
 help fake an abduction of
 a friend as a sort of birthday
 prank. I can find _friend_
 in _res_ after 8 PM.
 I've been given a bag to cover
 his head with. I'll take him to
 ___dung_ for =reward_ gold pieces.

QBN:
Person _questgiver_ group Questor
Person _friend_ group Resident1 male

Place _dung_ remote dungeon11
Place _res_ local house

Clock _oneday_ 1.00:00
Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _find_ 0:00 0:03

Item _reward_ gold
Item _sack_ small_sack

--	Quest start-up:
	start timer _oneday_
	log 1030 step 0
    hide npc _questgiver_
	place npc _friend_ at _res_
	pc at _dung_ set _idun_
    pc at _res_ set _ires_
	reveal _dung_
    get item _sack_

_oneday_ task:
    end quest

_day_ task:
    daily from 5:00 to 20:00
    hide npc _friend_

_night_ task:
    when not _day_
    restore _friend_

_ires_ task:
    _sack_ used do _taken_

_talk_ task:
    clicked npc _friend_
    say 1010

variable _taken_
_moveit_ task:
    when _taken_ and _ires_ and _night_
    start task _moveit2_

_moveit2_ task:
    say 1011
    hide npc _friend_
    add _friend_ face
    stop timer _oneday_
    start timer _timer_
    take _sack_ from pc
    start task _moving_

variable _moving_
variable _idun_
_winner_ task:
    when _idun_ and _moving_
    start timer _find_

_nowinner_ task:
    when not _winner_
    stop timer _find_

_find_ task:
    give pc _reward_
    end quest

_rescue_ task:
    when _timer_ and _moveit2_
    say 1003

_timer_ task:
	end quest