Quest: JHIM011
DisplayName: How to Save a Life
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          My dear %ra, I have angered the wrong sort of foe
<ce>      and find my life in danger. I need a bodyguard, right now.
<ce>                        Will you be my savior?

RefuseQuest:  [1001]
<ce>              And I will die, and no one will mourn me.

AcceptQuest:  [1002]
<ce>     Then I employ you from this very second. My things are with
<ce>        me, and this establishment means nothing so long as my
<ce>   pursuer seeks my life. I do know that it is one furious person,
<ce>    a spellcaster of some sort. I am lucky so as to not merit more
<ce>  attention than that. I will stay with you until I know for certain
<ce>       that things have settled themselves, one way or another.
<ce>       Don't mind me. I'll watch for myself while I'm with you.
<ce>                   Be about your ordinary business.

QuestFail:  [1003]
<ce>                          ERROR 1003 JHIM011

QuestComplete:  [1004]
<ce>    "Well, this matter is finished, isn't it %pcf? My everlasting
<ce>      gratitude to you for keeping me safe. Have these =reward_
<ce>      gold pieces. I would feel guilty holding on to them now."

Message:  1017
<ce>                     You sense something moving.

Message:  1018
<ce>         "I can't believe someone would try to kill me. What
<ce>       is this world coming to? Yes, I shouldn't have done it,
<ce>         but how does it come to this? Oh, this is terrible.
<ce>          I am grateful I did not have to draw blood, but my
<ce>            reputation will be ruined nonetheless. Perhaps
<ce>              ___qgiver_ is no place for me after all."

Message:  1019
<ce>       A courier impatiently stomps over to you, puts a letter
<ce>        into your bag, and moves on to more important matters.
<ce>
<ce>                         You open it at once.

Message:  1020
_qgiver_ of ___qgiver_
 
This is my last warning. Either pay the sum
of =fakegold_ gold pieces by this date next month
or I will repossess your animals. Please make whatever
sacrifice necessary to prevent this. I do not want to see
you lose your livelihood merely because of some
deeply foolish debts you have incurred.
 
                      Cordially,
                      _human_
                      ___dummy_

Message:  1021
<ce>           "Oh, my. I believe I was wrong about this whole
<ce>          matter. Then, I think we can both agree that there
<ce>           won't be any assassin heading my way, is there?
<ce>          At least not for this matter. Oh, my. Let me catch
<ce>               my breath. This is an overwhelming joy."

Message:  1030
%qdt:
 _qgiver_, a resident of
 ___qgiver_, has asked to
 accompany me in my travels while
 an assassin of some sort looks for
 %g2. I must merely be on the lookout
 until the time comes.

QBN:
Item _reward_ gold
Item _fakegold_ gold range 3001 to 6999
Item _letter_ letter

Person _qgiver_ group Questor male
Person _human_ group Local_3.0

Place _dummy_ remote house1

Clock _timer_ 12:00 6.12:00
Clock _failsafe_ 180.00:00

Foe _enemy_ is Nightblade

--	Quest start-up:
    start timer _timer_
    start timer _failsafe_
    log 1030 step 0
    pick one of _E.01_
    hide npc _qgiver_
    add _qgiver_ face

variable _timer_
variable _E.01_
variable _E.02_

_danger_ task:
    when _E.01_ and _timer_
    say 1017
    create foe _enemy_ every 1200 minutes indefinitely with 100% success

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ infighting true
    clear _daily_

_victory1_ task:
    killed _enemy_
    say 1018
    give pc _reward_
    end quest

_safe_ task:
    when _E.02_ and _timer_
    give pc _letter_ notify 1019
    have _letter_ set _victory2_

_victory2_ task:
    say 1020
    say 1021
    give pc _reward_
    end quest

_failsafe_ task:
    end quest