Quest: JHDB001
DisplayName: Returning to the Natural Order
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>             Understand that we have no personal vendetta
<ce>               against this mark, %pct. For us, all are
<ce>        free to obey the sky, the trees, the Night Mother, or
<ce>         nothing at all, however they may find satisfaction.
<ce>          Yet when one causes wild beasts to attack passers
<ce>           by, a request for an accounting quickly follows.
<ce>         Will you return a nature-crazed hermit to the earth?
                                     <--->
<ce>          On rare occasions, we receive petitions to execute
<ce>            the law when the town guard refuses to do so.
<ce>           My reading of the law is rather negligent, but I
<ce>             am certain some provision must exist against
<ce>             enraging wild animals and sending them into
<ce>          nearby towns. Are you willing to play executioner
<ce>                  against a nature-wielding hermit?

RefuseQuest:  [1001]
<ce>               This contract will be taken up quickly.
<ce>                  It will be a true joy to someone.

AcceptQuest:  [1002]
<ce>                 The recluse has made of ___mondung_
<ce>            a home, completing rituals of kinds that drive
<ce>             the wildlife into a frenzy. Apparently these
<ce>             attacks wane during the night hours, as even
<ce>            the clerics of the dirt require rest. Restore
<ce>             the natural order and return before =timer_
<ce>               nights have passed. Mephala be with you.

QuestFail:  [1003]
<ce>                  You were to report to _questgiver_
<ce>              concerning the wild hermit's death by now.

QuestComplete:  [1004]
<ce>            Now you see the dangers of subsisting entirely
<ce>                on _alch_. The Brotherhood is pleased
<ce>            to provide a return to the natural order when
<ce>            all other responsible parties refuse to do so.

Message:  1020
<ce>              The priest of nature is now dead. The wild
<ce>        animals will likely return to their natural patterns.

Message:  1024
<ce>                     Odd. _questgiver_ warned you
<ce>                your mark would cause mayhem with the
<ce>            local wildlife, but this place shows no signs
<ce>              of it. Perhaps the rumors were overblown.

Message:  1030
%qdt:
 In ___mondung_ there's a
 religious hermit who has caused
 animal attacks on surrounding cities.
 I must execute the Dark Brotherhood's
 contract to cull the priest and return
 to ___questgiver_ in =timer_ days' time.

QBN:
Item _alch_ small_plant

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon6

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Healer
Foe _wild1_ is Grizzley_bear
Foe _wild2_ is Giant_bat
Foe _wild3_ is Giant_rat
Foe _wild4_ is Spriggan

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pick one of _dud_ _dud_ _dud_ _dud_ _alt_
	pc at _mondung_ set _inside_
	place foe _enemy_ at _mondung_
	reveal _mondung_

_daytime_ task:
	daily from 8:00 to 18:00

_daily_ task:
	daily from 0:00 to 23:59
	change foe _wild1_ infighting true
	change foe _wild2_ infighting true
	change foe _wild3_ infighting true
	change foe _wild4_ infighting true
	change foe _wild1_ team 0
	change foe _wild2_ team 0
	change foe _wild3_ team 0
	change foe _wild4_ team 0
	clear _daily_

variable _inside_
variable _dud_
variable _alt_
_altspawn_ task:
	when _inside_ and _alt_
	start task _altinform_

_altinform_ task:
	say 1024

_spawn_ task:
	when _inside_ and _daytime_ and not _slain_ and not _alt_
	create foe _wild1_ every 80 minutes indefinitely with 45% success
	create foe _wild2_ every 65 minutes indefinitely with 36% success
	create foe _wild3_ every 59 minutes indefinitely with 34% success
	create foe _wild4_ every 40 minutes indefinitely with 22% success

_injured_ task:
	injured _enemy_
	change foe _enemy_ infighting true
	change foe _enemy_ team 0

_slain_ task:
	killed 1 _enemy_ saying 1020

_pcgetsgold_ task:
	when _slain_ and _qgclicked_
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_timer_ task:
	say 1003
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_