Quest: JHFG008
DisplayName: To Save a Vampire
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>             Got a job here to help keep a vampire alive,
<ce>               err, less dead than could otherwise be.
<ce>            Man, I don't know how to say it. Can you keep
<ce>          a vampire and his enemies apart? And not tell the
<ce>              whole world who it is while you're at it?

RefuseQuest:  [1001]
<ce>               Okay. Better to not tell you more then.

AcceptQuest:  [1002]
<ce>                  Okay, here's the secret. Vampire's
<ce>              a member of the nobility here in ___pal_.
<ce>                     Name's _noble_. Very quiet.
<ce>           Stays in the Palace. Actually not a bad fellow.
<ce>         You might see him in some of the pubs, buying drinks
<ce>       for the folks. Turns out some rascals want to raise some
<ce>        instability, and he's worried for his safety tonight.
<ce>        Says the attack's probably gonna come after midnight.
<ce>         You be there and help him fight off whatever things
<ce>       are gonna come for him, and you'll get a nice reputation
<ce>                as a hero around here, I'm sure of it.
<ce>                     Just be ready for anything.

QuestFail:  [1003]
<ce>                       _noble_ has been slain.

QuestComplete:  [1004]
<ce>           _noble_ sighs. Without sweat or heaving breath,
<ce>         it's hard to tell whether the battle truly cost him
<ce>                     any effort. Soon he speaks.
<ce>
<ce>             "You can keep a secret, can't you, good %ra?
<ce>         I'm afraid I don't have much money to pay you with,
<ce>           but in exchange for your discretion, I will make
<ce>          your fame in %crn soar. I believe your reputation
<ce>             among the people will fare far better now."
<ce>
<ce>          Calling for a servant, he retires to another room.
<ce>                      Your reward soon arrives.

RumorsPostFailure:  [1006]
Such a shame, what happened to _noble_.
<--->
Not even the Palace in ___pal_ is safe anymore, after what happened to _noble_.
<--->
_noble_ was one of a kind. We'll miss him.
<--->
They say _noble_ was a vampire. Scary thought!

RumorsPostSuccess:  [1007]
I always love hearing about _noble_. Charming fellow.
<--->
_noble_ is one of a kind.
<--->
_noble_'s off buying some mead for the townspeople, I hear.

Message:  1010
<ce>          "Fighters Guild? Good. Glad you're here. I just--"
<ce>
<ce>                  _noble_ is cut short by a magical
<ce>      distortion nearby. A portal opens long enough for several
<ce>            beasts to come through, gnashing with hunger.
<ce>              It then closes after exhausting its power.
<ce>
<ce>                       "No time! Help me now!"

Message:  1012
<ce>                     You were to speak to _noble_
<ce>                    after midnight to protect him.

Message:  1030
%qdt:
 The Fighters Guild needs me to
 protect a local noble today, a secretive
 vampire named _noble_,
 in the Palace at ___pal_.
 I should find him inside after midnight
 to see how I can help.

QBN:

Place _pal_ local palace

Clock _timer_ 1.06:00 0

Foe _nym_ is Nymph
Foe _garg_ is Gargoyle
Foe _ally_ is Ancient_vampire

Person _noble_ factiontype Vampire_Clan male

Item _reward_ gold range 400 to 600

--	Quest start-up:
    start timer _timer_
    log 1030 step 0

_appear_ task:
    daily from 0:01 to 3:59
    place npc _noble_ at _pal_

_failtime_ task:
    daily from 4:00 to 4:30

_talk_ task:
    clicked npc _noble_
    create foe _nym_ every 0 minutes 3 times with 100% success
    create foe _garg_ every 0 minutes 3 times with 100% success
    say 1010
    stop timer _timer_
    hide npc _noble_
    place foe _ally_ at _pal_

_day_ task:
    daily from 00:00 to 23:59
    change foe _nym_ infighting true
    change foe _garg_ infighting true
    change foe _ally_ infighting true
    change foe _ally_ team 1
    change foe _nym_ team 0
    change foe _garg_ team 0
    clear _day_

_kill1_ task:
    killed 3 _nym_

_kill2_ task:
    killed 3 _garg_

_victory_ task:
    when _kill1_ and _kill2_
    give pc _reward_
    remove foe _ally_
    legal repute +20
    end quest

_casualty_ task:
    killed 1 _ally_ saying 1003
    end quest

_bigfail_ task:
    when _failtime_ and not _talk_
    say 1012
    end quest

_timer_ task:
    say 1012
    end quest