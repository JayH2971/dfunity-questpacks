Quest: JHFG009
DisplayName: Wild Hunt
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>               Hey there %pcf, I've got something of a
<ce>           challenge for you, no risk at all. Something of
<ce>          a vendetta to satisfy. We've got this client who's
<ce>      sick of a werewolf that's been eating up his farm animals.
<ce>                He knows it's staying in ___dungeon_,
<ce>         but he can't be bothered to go in and do it himself.
<ce>          You're gonna have to be a good hunter to do this,
<ce>              a real good one. Wanna try it? It's paying
<ce>                        an enchanted _reward_.

RefuseQuest:  [1001]
<ce>      Yeah, sooner or later it's gonna show up at the right time
<ce>             and find a silver pitchfork waiting for it.
<ce>                     Let me see what else I have.

AcceptQuest:  [1002]
<ce>          That's the spirit! Like I said, I'm not gonna hold
<ce>          this against you if you don't get it done. There's
<ce>           no money on the line, no human lives in danger.
<ce>       Yet, at least. The trick is that this critter knows its
<ce>           lair too well. Knows its strangers and fellows.
<ce>         Once you've set foot, it's only gonna give you a few
<ce>       hours to find it before it's gone, like six, he told me.
<ce>        So don't go wandering around for no reason down there.
<ce>                Get to ___dungeon_ and kill the thing
<ce>           before it disappears on ya. I'll hold on to the
<ce>                 _reward_ 'til you show me the proof.

QuestFail:  [1003]
<ce>              You realize enough time has passed by now.
<ce>                  The werewolf has to be long gone.

QuestComplete:  [1004]
<ce>            You did it? Amazing. The old farmer's gonna be
<ce>           ecstatic, I tell you. I guess I'll tell everyone
<ce>                ___qgiver_'s got a lycanthrope expert.
<ce>                      Anyway, take your reward.

Message:  1015
<ce>      Now that you've set foot inside, there's no turning back.
<ce>          It's time to find the werewolf before it escapes.

Message:  1020
<ce>             Your hunt was successful. You draw a sample
<ce>              of the werewolf's blood and pack it away.

Message:  1030
%qdt:
 The Fighters Guild of ___qgiver_
 has challenged me to find and slay
 a werewolf inside ___dungeon_,
 which is prone to fleeing once it's
 hunted. Once I enter I'll only have
 about six hours to find and slay it.
 My reward will be a magical _reward_.

QBN:
Item _reward_ magic_item
Item _blood_ werewolf_blood

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon11

Clock _failsafe_ 90.00:00
Clock _hunt_ 6:00

Foe _enemy1_ is Werewolf

--	Quest start-up:
	start timer _failsafe_
	log 1030 step 0
	reveal _dungeon_
	pc at _dungeon_ set _starthunt_
	place foe _enemy1_ at _dungeon_

_starthunt_ task:
	say 1015
	start timer _hunt_

_inj_ task:
	injured _enemy1_
	change foe _enemy1_ infighting true

_slain_ task:
	killed 1 _enemy1_ saying 1020
	stop timer _hunt_
	get item _blood_

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_hunt_ task:
	say 1003
	change repute with _qgiver_ by +3
	end quest

_failsafe_ task:
	end quest