Quest: JHKN030
DisplayName: Ransom Payment
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                     A family native to this city
<ce>                    has informed us of a relative
<ce>                     who was kidnapped by knaves
<ce>                  and taken for ransom. They cannot
<ce>                   pay, but someone must. Will you
<ce>                  go and offer up the price to save
<ce>                         the captive's life?

RefuseQuest:  [1001]
<ce>            Someone here has to have deep pockets, surely.

AcceptQuest:  [1002]
<ce>                    You are to look for _ransom_,
<ce>           who was abducted a few days previous. The family
<ce>               was informed that the handler will be in
<ce>                       a store called _place_,
<ce>                  in the city of ___place_. I do not
<ce>                  need to emphasize, my friend, that
<ce>              this entirely depends on you. If you fail,
<ce>               no one else will be able to clean up the
<ce>                  mess. They will expect you within
<ce>                       =2place_ days. Godspeed.

QuestFail:  [1003]
<ce>             The ransom date has passed. You have failed.

QuestComplete:  [1004]
<ce>                    _ransom_, we will notify your
<ce>              family immediately. What a joy we all feel
<ce>             for your safe return. And what a noble act,
<ce>            %pcf! I will voice it abroad as far as I can.
<ce>             This is the work of true knights. A hundred
<ce>                   congratulations on your success.
                                     <--->
<ce>              %oth, I have spent sleepless nights since
<ce>            I sent you out, %pct. My fears were unfounded.
<ce>             Please, _ransom_ wait here. I will send out
<ce>            a squire to find your family. And as for you,
<ce>                %pcf, I will voice your act all abroad
<ce>                %crn. Let no one forget your selfless
<ce>               sacrifice for one who cannot repay you.

QuestLogEntry:  [1010]
%qdt:
 I have accepted a task
 from ==qgiver_ to retrieve a
 person at ransom. As the family
 is too poor to pay, I will offer up
 the sum myself. The contact is to be
 found in ___place_, at a
 shop called _place_.
 I must do so within =2place_ days.

Message:  1011
%qdt:
 I have learned _ransom_,
 the person I have ransomed,
 is in ___dung_.

Message:  1014
<ce>              Where're the coins? I don't see no coins.
<ce>                  Get me real money and we'll talk.

Message:  1020
<ce>                You have it? Lemme see. Well... yeah.
<ce>         Good job. We might be shrewd but we do honest work.
<ce>                 Go find your prize down in ___dung_.
<ce>          I'll mark it on your map. Pleasure doing business.
                                     <--->
<ce>          Got the money? I'll count it out. Nice. Hope they
<ce>             send you more often. I'll mark the place on
<ce>                     your map, ___dung_. Go there
<ce>          and look. I'll tell my people to clear out of it.
<ce>          Should be a nice Sundas stroll for ya. Later kid.

Message:  1021
<ce>                    You find _ransom_ here, whose
<ce>           countenance fills with joy at the sight of you.
<ce>
<ce>             "By %god, I had given up all hope. I thought
<ce>           the ransom failed and they left me to die here.
<ce>                     Please, take me from here!"
<ce>
<ce>                     Now to return to ==qgiver_.
                                     <--->
<ce>                    You find _ransom_ here, whose
<ce>           countenance fills with joy at the sight of you.
<ce>
<ce>              "%oth! Can it be true? I owe you my life,
<ce>             good knight! Please, let us away from here!"
<ce>
<ce>                     Now to return to ==qgiver_.

Message:  1023
<ce>              Well aren't you loaded? Let's see just how
<ce>            much more you got on you. Hey! Playtime's on!
                                     <--->
<ce>               Nice work, %ra! That all you got on you?
<ce>                  Let's find out together, shall we?

Message:  1024
Job number =rand_
<ce>
Name: _ransom_
Location: ___dung_
<ce>
No more funny business. Take the gold
and get going. This is your last warning.

Message:  1025
<ce>                   "You got the gold? Give it here.
<ce>                      Let's get this over with."
<ce>
<ce>                  As you hand the money over, a loud
<ce>                  banging sound comes from the front
<ce>                 door. A small band of rabblerousers
<ce>                          proceeds to enter.
<ce>
<ce>                   "So we heard there was something
<ce>                happening here today! How's about you
<ce>                  share some of that money with us?"
<ce>
<ce>              The handler hides behind you and whispers,
<ce>
<ce>                "If I die, you'll never find out where
<ce>                        _ransom_ is. Help me!"
                                     <--->
<ce>                     "You here to pay? Hurry up.
<ce>                           Let me see it."
<ce>
<ce>                  As you hand the money over, a loud
<ce>                  banging sound comes from the front
<ce>                 door. A small band of rabblerousers
<ce>                          proceeds to enter.
<ce>
<ce>                 "Overheard about your little ransom
<ce>                  deal! Weird how you didn't tell us
<ce>                  about it. How's about you give us
<ce>                           a piece of it?"
<ce>
<ce>              The handler hides behind you and whispers,
<ce>
<ce>               "I can't fight! Please, help me and I'll
<ce>                     tell you where _ransom_ is!"

Message:  1026
<ce>               Good work, %ra. I'm getting outta here.
<ce>                  Head to ___dung_. I'll mark it on
<ce>                     your map and then I'm gone.
                                     <--->
<ce>                    Nice save. The place is called
<ce>                       ___dung_. I'm just gonna
<ce>               mark it on your map and I'm outta here.

Message:  1027
<ce>              Ah, and lest I forget, they have demanded
<ce>               the sum of 1341 gold pieces. Be sure to
<ce>               have it on your person in physical form.
                                     <--->
<ce>             Oh, I should tell you the sum. They require
<ce>               1341 gold pieces, all in one, no letters
<ce>                     of credit. %god go with you.

Message:  1028
<ce>              Ah, and lest I forget, they have demanded
<ce>               the sum of 1798 gold pieces. Be sure to
<ce>               have it on your person in physical form.
                                     <--->
<ce>             Oh, I should tell you the sum. They require
<ce>               1798 gold pieces, all in one, no letters
<ce>                     of credit. %god go with you.

Message:  1029
<ce>              Ah, and lest I forget, they have demanded
<ce>               the sum of 2152 gold pieces. Be sure to
<ce>               have it on your person in physical form.
                                     <--->
<ce>             Oh, I should tell you the sum. They require
<ce>               2152 gold pieces, all in one, no letters
<ce>                     of credit. %god go with you.

QBN:

Person _crook1_ faction The_Thieves_Guild male
Person _crook2_ faction The_Thieves_Guild male
Person _crook3_ faction The_Thieves_Guild male
Person _ransom_ faction The_Merchants
Person _qgiver_ group Questor

Place _place_ remote bookstore
Place _dung_ remote dungeon12

Item _return_ gold range 2152 to 2200
Item _letter_ letter used 1024
Item _rand_ gold range 44 to 89

Foe _enemy1a_ is Rogue
Foe _enemy1b_ is Rogue
Foe _enemy2_ is 3 Thief

Clock _2place_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 60.00:00

--	Quest start-up:
    log 1010 step 0
    start timer _failsafe_
    start timer _2place_
    pick one of _E.01_ _E.02_ _E.03_
    pc at _place_ do _inside_
    place npc _ransom_ at _dung_

_E.01_ task:
    say 1027
    place npc _crook1_ at _place_

_E.02_ task:
    say 1028
    place npc _crook2_ at _place_

_E.03_ task:
    say 1029
    place npc _crook3_ at _place_

_talk1_ task:
    clicked _crook1_ and at least 1341 gold otherwise do _nogo_
    pick one of _simple_ _trap_ _ambush_
    stop timer _2place_

_talk2_ task:
    clicked _crook2_ and at least 1798 gold otherwise do _nogo_
    pick one of _simple_ _trap_ _ambush_
    stop timer _2place_

_talk3_ task:
    clicked _crook3_ and at least 2152 gold otherwise do _nogo_
    pick one of _simple_ _trap_ _ambush_
    stop timer _2place_

_2place_ task:
    say 1003
    end quest

_nogo_ task:
    say 1014

_simple_ task:
    say 1020
    reveal _dung_
    hide npc _crook1_
    hide npc _crook2_
    hide npc _crook3_
    log 1011 step 1

_trap_ task:
    say 1023
    hide npc _crook1_
    hide npc _crook2_
    hide npc _crook3_
    place foe _enemy1a_ at _place_
    create foe _enemy1b_ every 0 minutes 1 times with 100% success
    give item _letter_ to _enemy1a_
    give item _return_ to _enemy1a_
    make _return_ permanent
    change foe _enemy1a_ infighting true

_k1_ task:
    killed _enemy1a_
    reveal _dung_
    log 1011 step 1

_ambush_ task:
    say 1025
    hide npc _crook1_
    hide npc _crook2_
    hide npc _crook3_
    add _crook1_ face
    create foe _enemy2_ every 0 minutes 1 times with 100% success

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy2_ infighting true
    clear _daily_

variable _inside_
_kill_ task:
    killed 3 _enemy2_
    start task _save_

_escape_ task:
    when _ambush_ and not _inside_
    start task _save_

_save_ task:
    say 1026
    reveal _dung_
    drop _crook1_ face
    log 1011 step 1

_find_ task:
    clicked npc _ransom_
    say 1021
    hide npc _ransom_
    add _ransom_ face

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _find_
    clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
    when _qgclicked_ and _find_
    legal repute +8
    give pc nothing
    end quest

_failsafe_ task:
    end quest