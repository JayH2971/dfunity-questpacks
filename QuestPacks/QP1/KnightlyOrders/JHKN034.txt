Quest: JHKN034
DisplayName: Honeyed Words
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>           Few troubles concern a knightly order more than
<ce>                a noble gone berserk. We mostly avoid
<ce>      such a problem, but today we come close. One of our knight
<ce>        brothers is betrothed to the daughter of an important
<ce>    aristocrat, but only recently gained information that her past
<ce>     is not as pure as promised. He wishes out of the engagement,
<ce>      but fears royal wrath. We require a knight more persuasive
<ce>   than any other. Have you etiquette to ward off a furious parent?

RefuseQuest:  [1001]
<ce>          Perhaps we'll have to employ a bard of some sort.

AcceptQuest:  [1002]
<ce>            You will not journey far this time. Meet _nob_
<ce>         in the palace of this city and make a good argument
<ce>            for our brother, _knight_, whatever you will.
<ce>       Defend his honor and save him from his pending marriage.
<ce>         If words fail, I trust you will find some other way.
<ce>            Five days will not pass before our man must be
<ce>             granted liberty, %pct. %god guide your way.

QuestFail:  [1003]
<ce>                    _knight_ will be irretrievably
<ce>                    engaged to the damsel by now.

QuestComplete:  [1004]
<ce>                 Very well, I see that this must be.
<ce>        I wish to never see that cretin again, but I will not
<ce>             hold __qgiver_ responsible. Be off with you,
<ce>           squire, and be more careful than this lout was.
<ce>                 Life only gives you so many chances.

RumorsPostSuccess:  [1007]
Them Archaeologists are good people, I says.
<--->
The Archaeologists and the Mages Guild are always fighting about stuff. %god knows what.

QuestorPostFailure:  [1009]
Someone else stepped up after your failure with _nob_, thank %god.

QuestLogEntry:  [1010]
%qdt:
 __qgiver_ in ___qgiver_
 need me to speak to a noble named
 _nob_ in the local palace,
 to end the betrothal of
 %g3 daughter and _knight_.
 I have =timer_ days.

Message:  1011
%qdt:
 _nob_ has rejected my request to
 liberate _knight_ and demands a sum of
 2,500 gold coins instead.

Message:  1014
<ce>                       (ETIQUETTE CHECK PASSED)
<ce>
<ce>              Hmph. As hard as your story is to believe,
<ce>               I must give __qgiver_ their proper due.
<ce>              This scandal will cost us all more than we
<ce>          bargained for... But the alternative is far worse.

Message:  1015
<ce>                      (PERSONALITY CHECK PASSED)
<ce>
<ce>              Hmph. As hard as your story is to believe,
<ce>               I must give __qgiver_ their proper due.
<ce>              This scandal will cost us all more than we
<ce>          bargained for... But the alternative is far worse.

Message:  1016
<ce>                       (ETIQUETTE CHECK FAILED)
<ce>
<ce>             Perhaps you overestimated your speechcraft.
<ce>                       _nob_ flies into a rage.
<ce>             Insults flow regarding you, your parentage,
<ce>                       _knight_, and __qgiver_.
<ce>
<ce>          "Perhaps I was a fool to let things get this far,
<ce>          if _knight_ is so low as to make such allegations.
<ce>           My daughter will not marry a reprobate! I demand
<ce>         2,500 gold pieces to repay what I've sunk into this
<ce>            miserable betrothal. I will then send notices
<ce>               of remittal to the interested parties."
<ce>
<ce>             _nob_ continues to glare at you, waiting for
<ce>                 the money. __qgiver_ probably won't
<ce>                        be able to help you...

Message:  1017
<ce>            You shuffle around to find 2,500 gold pieces,
<ce>              but do not have enough. _nob_ seethes with
<ce>           rage, but does not open %g3 mouth to release it.

Message:  1018
<ce>             You discreetly deliver the 2,500 gold pieces
<ce>                          into _nob_'s hand.

QBN:
Person _qgiver_ group Questor male
Person _nob_ faction Random_Noble
Person _knight_ faction Generic_Knightly_Order male

Place _palace_ local palace

Clock _timer_ 5.00:00

--	Quest start-up:
    start timer _timer_
    log 1010 step 0
    place npc _nob_ at _palace_

_talk_ task:
    clicked npc _nob_

_conv_ task:
    when _talk_ and not _fail_
    start task _begin_

_begin_ task:
    pick one of _go1_ _go2_ _go3_

_check1_ task:
    when skill Etiquette is at least 30

_check2_ task:
    when skill Etiquette is at least 35

_check3_ task:
    when skill Etiquette is at least 40

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
    when _check1_ and _go1_
    start task _victory1_

_c2_ task:
    when _check2_ and _go2_
    start task _victory1_

_c3_ task:
    when _check3_ and _go3_
    start task _victory1_

_d1_ task:
    when not _check1_ and _go1_
    start task _tryspeech_

_d2_ task:
    when not _check2_ and _go2_
    start task _tryspeech_

_d3_ task:
    when not _check3_ and _go3_
    start task _tryspeech_

_tryspeech_ task:
    pick one of _go4_ _go5_ _go6_

_check4_ task:
    when attribute Personality is at least 75

_check5_ task:
    when attribute Personality is at least 80

_check6_ task:
    when attribute Personality is at least 85

variable _go4_
variable _go5_
variable _go6_

_c4_ task:
    when _check4_ and _go4_
    start task _victory2_

_c5_ task:
    when _check5_ and _go5_
    start task _victory2_

_c6_ task:
    when _check6_ and _go6_
    start task _victory2_

_d4_ task:
    when not _check4_ and _go4_
    start task _fail_

_d5_ task:
    when not _check5_ and _go5_
    start task _fail_

_d6_ task:
    when not _check6_ and _go6_
    start task _fail_

_victory1_ task:
    say 1014
    give pc nothing
    end quest

_victory2_ task:
    say 1015
    give pc nothing
    end quest

_fail_ task:
    say 1016
    log 1011 step 1
    clear _talk_

_payup_ task:
    when _talk_ and _fail_
    pay 2500 money do _victory3_ otherwise do _nomoney_

_victory3_ task:
    say 1018
    give pc nothing
    end quest

variable _nomoney_
_nomoney1_ task:
    when _nomoney_ and not _victory3_
    say 1017
    clear _talk_ _nomoney_

_timer_ task:
    say 1003
    end quest