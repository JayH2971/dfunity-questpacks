Quest: JHTG001
DisplayName: The Test of Speed
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>         Sometimes in the guild we like to kick back for some
<ce>        sport. Got an invitation you might want to listen to.
<ce>           There's a treasure chest in a crypt around here
<ce>         that's infested with bats. If you can find the chest
<ce>           and bring it back here without killing any bats,
<ce>                you get something enchanted. Want in?

RefuseQuest:  [1001]
<ce>                  We'll see if you change your mind.

AcceptQuest:  [1002]
<ce>             "All right, let us just do one thing before
<ce>                      we tell you where it is."
<ce>
<ce>        A robed figure enters, casts two short spells on you,
<ce>              and leaves. You don't feel any different.
<ce>
<ce>          "There. Now we'll be able to see you, and the bats
<ce>        will come swarming after you. Kill none of them in the
<ce>                   crypt, place called ___mondung_.
<ce>             The spell won't last forever, so get on back
<ce>             here in =timer_ days with the chest in hand.
<ce>              You'll be getting this enchanted _reward_
<ce>                    if you do it. Take care now."

QuestFail:  [1003]
<ce>                You needed to deliver the chest to the
<ce>                        Thieves Guild by now.

QuestComplete:  [1004]
<ce>          That was a thrill to watch. A bunch of us gathered
<ce>             around and shared some mead, made some bets.
<ce>                Wish we could do it more often. Here's
<ce>               your _reward_. Hope it serves you well.

QuestLogEntry:  [1010]
%qdt:
 I've been given a challenge by the Thieves Guild
 to find a certain chest in ___mondung_
 and deliver it to them in ___questgiver_
 within =timer_ days. I can't kill any
 bats inside the place.

Message:  1011
<ce>         As your hand touches this chest, a magical insignia
<ce>         appears in front of you. This must be the right one.
<ce>        You're disappointed to see it's empty, but it's still
<ce>                          yours to deliver.

Message:  1012
<ce>       As this bat falls, a design like a skull appears before
<ce>         your eyes. The Thieves Guild knows you have failed.

Message:  1013
<ce>          You can already hear the faint chittering of bats.

QBN:
Item _reward_ magic_item
Item _chest_ item class 1 subclass 10

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon0

Clock _timer_ 00:00 0 flag 17 range 1 2

Foe _bat_ is Giant_bat

--	Quest start-up:
  start timer _timer_
  log 1010 step 0
  pc at _mondung_ set _inside_
  place item _chest_ at _mondung_
  reveal _mondung_

_inside_ task:
  say 1013
  create foe _bat_ every 5 minutes indefinitely with 33% success

_find_ task:
  clicked item _chest_
  say 1011

_slain_ task:
  killed _bat_
  say 1012
  make _chest_ permanent
  end quest

_timer_ task:
  say 1003
  make _chest_ permanent
  end quest

_victory_ task:
  toting _chest_ and _questgiver_ clicked
  give pc _reward_
  end quest