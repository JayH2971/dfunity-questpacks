Quest: JHTG009
DisplayName: Only the Best
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Yeah, I've got something for you. That is, for YOU.
<ce> It ain't gonna be pretty, I can tell you that.
<ce> But if you take it, the client's offering one of those
<ce> artifacts, the powerful ones. That's how big this
<ce> thing is. Are you gonna take it?

RefuseQuest:  [1001]
<ce> Well then, that's that.

AcceptQuest:  [1002]
<ce> Here. There's this bag here, and don't bother opening it.
<ce> You can't and you don't wanna know what's inside. Now that
<ce> it's in your hands, two things are gonna start to happen.
<ce> One, you're gonna start to feel violently ill, and that's
<ce> not gonna go away. Two, you're gonna start to see daedra
<ce> coming for you. That's why this is for you specifically.
<ce> For most people this is a suicide job, and maybe we should
<ce> call it that. But this bag has to get to ___tav_.
<ce> A contact named _gpers_ is gonna be in a tavern
<ce> called _tav_ about =days_ days from now.

QuestFail:  [1003]

QuestComplete:  [1004]
<ce> Welcome, child. Give me the bag, quickly.
<ce> No creature must know of its continued presence here.
<ce> And now, before I slay you and your witnessing eyes,
<ce> take this reward. It is a fair exchange.
<ce> Safe travels, %ra.

Message:  1010
<ce> But really, let me just ask this. One last time.
<ce> Are you sure you want to do this? You've only got one life,
<ce> and I'd hate to see you throw it away.

Message:  1011
<ce> All right then. Get marching, soldier.
<ce> Oblivion's on its way.

Message:  1012
<ce> Sure, no hard feelings. Like I said, it's suicide.
<ce> I'll tell the client it's not worth the trouble.

Message:  1015
<ce> You could throw away the bag if you choose, though
<ce> that would likely cause problems for the Thieves Guild.
<ce> Do you wish to abandon it?

Message:  1016
<ce> You discard the bag.

Message:  1020
<ce> A deafening roar sounds nearby.

Message:  1030
%qdt:
 _questgiver_ of the Thieves Guild
 gave me a bag that needs to get to
 a person named _gpers_
 at _tav_, ___tav_,
 after about =days_ days.

QBN:

Person _questgiver_ group Questor male
Person _tg_ faction The_Thieves_Guild
Person _gpers_ faction The_Anthotis

Item _bag_ small_sack
Item _art1_ artifact Auriels_Bow
Item _art2_ artifact Auriels_Shield
Item _art3_ artifact Chrysamere
Item _art4_ artifact Lords_Mail
Item _art5_ artifact Necromancers_Amulet
Item _art6_ artifact Staff_of_Magnus
Item _art7_ artifact Warlocks_Ring

Place _tav_ remote tavern

Foe _dae1_ is Daedroth
Foe _dae2_ is Frost_daedra
Foe _dae3_ is Fire_daedra
Foe _dae4_ is Daedra_seducer
Foe _dae5_ is Daedra_lord

Clock _delay_ 0:03
Clock _days_ 4.00:00 6.00:00

-- Quest start-up:
 log 1030 step 0
 prompt 1010 yes _begin_ no _quit_

_quit_ task:
  say 1012
  change repute with _tg_ by +2
  end quest

_begin_ task:
  say 1011
  pick one of _a1_ _a2_ _a3_ _a4_ _a5_ _a6_ _a7_
  get item _bag_
  _bag_ used do _endprompt_
  have _bag_ set _sick_
  start timer _delay_
  start timer _days_

_days_ task:
  place npc _gpers_ at _tav_

_delay_ task:
  say 1020
  send _dae1_ every 15 minutes with 40% success
  send _dae2_ every 15 minutes with 40% success
  send _dae3_ every 15 minutes with 40% success
  send _dae4_ every 15 minutes with 40% success
  send _dae5_ every 15 minutes with 40% success

variable _a1_
variable _a2_
variable _a3_
variable _a4_
variable _a5_
variable _a6_
variable _a7_
_sick_ task:
  daily from 00:01 to 23:59
  make pc ill with Leprosy
  clear _sick_

_endprompt_ task:
  prompt 1015 yes _fail_ no _continue_

_fail_ task:
  say 1016
  change repute with _tg_ by -13
  take _bag_ from pc
  end quest

variable _continue_
_victory_ task:
  toting _bag_ and _gpers_ clicked

_v1_ task:
  when _victory_ and _a1_
  give pc _art1_
  end quest

_v2_ task:
  when _victory_ and _a2_
  give pc _art2_
  end quest

_v3_ task:
  when _victory_ and _a3_
  give pc _art3_
  end quest

_v4_ task:
  when _victory_ and _a4_
  give pc _art4_
  end quest

_v5_ task:
  when _victory_ and _a5_
  give pc _art5_
  end quest

_v6_ task:
  when _victory_ and _a6_
  give pc _art6_
  end quest

_v7_ task:
  when _victory_ and _a7_
  give pc _art7_
  end quest