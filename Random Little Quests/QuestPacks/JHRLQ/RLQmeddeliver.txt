Quest: RLQmeddeliver
DisplayName: Random Little Quest A Medical Delivery
-- Message panels
QRC:

QuestComplete:   [1004]
<ce>  Oh, thank %god you came with this.
<ce>  We shouldn't have gone another day
<ce>  without it. Thank you.
<ce>  Take this gold.

Message:  1010
<ce>              Under the overcast sky, a woman in fairly
<ce>                    formal clothing accosts you,
<ce>               shouting far too loudly for the purpose.
<ce>  
<ce>  "EXCUSE ME! I need a delivery! _person_
<ce>  at _tavern_ is expecting this _ingred_ and
<ce>  will pay if you deliver it to %g2!
<ce>  I don't have the time to do it myself!
<ce>  Make some money today, why don't you?!"

Message:  1011
<ce>  "THAT'S RIGHT! Some gold for your trouble,
<ce>  go get it at _tavern_ over to the %di!
<ce>  Take the _ingred_, go find %g2!"
<ce>  
<ce>  A tag on the _ingred_ says it needs to be
<ce>  delivered within 24 hours.

Message:  1012
<ce>  24 hours have passed, and the _ingred_
<ce>  has lost its potency. You discard it.

Message:  1013
<ce>  The woman wastes no time in turning to accost
<ce>  some other unfortunate soul.

Message:  1014
%qdt:
 I have been given a sample of _ingred_ to deliver to
 someone named _person_ in _tavern_, ___tavern_.
 It needs to be there in twenty-four hours.

QBN:

Clock _timer_ 1.00:00

Item _reward_ gold range 31 to 84
Item _ingred_ small_plant

Person _person_ faction Healers

Place _tavern_ local tavern

--	Quest start-up:
    prompt 1010 yes _accept_ no _reject_

_reject_ task:
    say 1013
    end quest

_accept_ task:
    say 1011
    get item _ingred_
    place npc _person_ at _tavern_
    start timer _timer_
    log 1014 step 0

_finalwin_ task:
    toting _ingred_ and _person_ clicked
    give pc _reward_
    end quest

_timer_ task:
    say 1012
    end quest