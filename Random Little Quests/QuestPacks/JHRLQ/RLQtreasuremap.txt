Quest: RLQtreasuremap
DisplayName: Random Little Quests Treasure Map
-- Message panels
QRC:

QuestFail:   [1003]
<ce>  The treasure in ___dung_
<ce>  will have been claimed by pirates by now.

QuestComplete:  [1004]
<ce>               This appears to be the fabled treasure.

Message:  1010
<ce> A piece of parchment is stuck in the snow here.
<ce> It seems a bit old and worn, and no one nearby
<ce> pays it any mind. It appears to have directions
<ce> to a cavern somewhere in %crn with a treasure inside.
<ce> Do you pick it up and read the directions?

Message:  1011
<ce>  You are already busy enough without
<ce>  investigating claims of treasure.
<ce>  You leave the map in the snow.

Message:  1012
<ce>  The instructions explain how to find ___dung_,
<ce>  where the treasure is to be found. It contains
<ce>  a warning that pirates are suspected to arrive
<ce>  in about =timer_ days to move it to another location.
<ce>  If you intend to collect the treasure, it'll have
<ce>  to be before then.

Message:  1013
%qdt:
 I found a treasure map explaining how to find
 ___dung_, where a treasure lies. I would need
 to claim it within =timer_ days.

QBN:

Item _sack_ item class 1 subclass 10
Item _reward1_ magic_item
Item _reward2_ mineral
Item _reward3_ diamond
Item _reward4_ gold range 521 to 679
Item _reward5_ gold range 1835 to 2999

Place _dung_ remote dungeon

Clock _timer_ 25.00:00 40.00:00

--   Quest start-up:
    prompt 1010 yes _accept_ no _quit_

_quit_ task:
    say 1011
    end quest

_accept_ task:
  start timer _timer_
  say 1012
  pick one of _item1_ _item2_ _item3_ _item4_ _item5_ _item5_
  reveal _dung_
  place item _sack_ at _dung_
  log 1013 step 0

variable _item1_
variable _item2_
variable _item3_
variable _item4_
variable _item5_

_found_ task:
  clicked item _sack_

_vict1_ task:
  when _found_ and _item1_
  give pc _reward1_
  end quest

_vict2_ task:
  when _found_ and _item2_
  give pc _reward2_
  end quest

_vict3_ task:
  when _found_ and _item3_
  give pc _reward3_
  end quest

_vict4_ task:
  when _found_ and _item4_
  give pc _reward4_
  end quest

_vict5_ task:
  when _found_ and _item5_
  give pc _reward5_
  end quest

_timer_ task:
  say 1003
  end quest