Quest: RLQyousteal
DisplayName: Random Little Quest You Steal
-- Message panels
QRC:

QuestComplete:   [1004]
<ce>  The mark doesn't even realize it. You grab the
<ce>  gem without a single rustle, and pocket it before
<ce>  anyone can see you.

Message:  1010
<ce>  A careless peasant is going about his daily tasks,
<ce>  unaware that he has left a very shiny jewel showing
<ce>  from his pocket. If you were good at theft,
<ce>  you could attempt to gain it for yourself.
<ce>  Will you try?
<--->
<ce>  A careless peasant is going about her daily tasks,
<ce>  unaware that she has left a very shiny jewel showing
<ce>  from her pocket. If you were good at theft,
<ce>  you could attempt to gain it for yourself.
<ce>  Will you try?

Message:  1011
<ce>  You ignore the gem and continue on your way.

Message:  1012
<ce>  You calmly step forward and try to pull on the
<ce>  gem, but are only a bit too clumsy to get it free.
<ce>  The peasant turns and then screeches for help.

QBN:

Item _reward_ gem

--	Quest start-up:
    prompt 1010 yes _accept_ no _reject_

_reject_ task:
    say 1011
    end quest

_accept_ task:
    pick one of _go1_ _go2_ _go3_

_check1_ task:
	when skill Pickpocket is at least 20

_check2_ task:
	when skill Pickpocket is at least 30

_check3_ task:
	when skill Pickpocket is at least 40

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
	when _check1_ and _go1_
	start task _success_

_c2_ task:
	when _check2_ and _go2_
	start task _success_

_c3_ task:
	when _check3_ and _go3_
	start task _success_

_d1_ task:
	when not _check1_ and _go1_
	start task _fail_

_d2_ task:
	when not _check2_ and _go2_
	start task _fail_

_d3_ task:
	when not _check3_ and _go3_
	start task _fail_

variable _success_
variable _fail_

_finalwin_ task:
    when _accept_ and _success_
    give pc _reward_
    end quest

_finalfail_ task:
    when _accept_ and not _success_
    say 1012
    setplayercrime Theft
    spawncityguards immediate
    end quest