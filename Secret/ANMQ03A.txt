Quest: ANMQ03A
DisplayName: ANMQ03A
-- Message panels
QRC:

QuestComplete:  [1004]
<ce>        "What a show," the lich claps its bony hands together.
<ce>              "You should come see me in Scourg Barrow.
<ce>        You're both invited. We will form an alliance to find
<ce>         the Totem, and you will gain tremendous dark power."
<ce>
<ce>        A rough map of the Dragontail Mountains is burned into
<ce>       the ground in front of you, and an X marks the presumed
<ce>                        location of his lair.
<ce>
<ce>        "We don't need you! Leave us alone!" Karethys screams.
<ce>
<ce>      "Well, the invitation's there," the King of Worms answers.
<ce>           The lich then decomposes, leaving you two alone.
<ce>
<ce>         "Okay, so. We can't talk out in the open like this."
<ce>    The Dark Elf struggles to catch her breath. "I'm heading back
<ce>            to Wayrest. I'll leave you to think about it:
<ce>          once you get the Totem, what would you do with it?
<ce>     You need a plan. You step into the daylight with that thing,
<ce>          and you're dead..." She sighs, "As demonstrated."
<ce>
<ce>     "Thank you for hearing me out. I have no loyalty to Wayrest,
<ce>            only the people of Tamriel. I'll try to serve
<ce>        the highest purpose, whatever it is. Stay safe, %pcf."
<ce>
<ce> (NOTE: This version of ANMQ does not contain the King of Worms' sidequest.)
<ce>                      (It will be added later.)
<ce>              (Thank you for playing the beta version.)

Message:  1010
<ce>        "So the great Popudax has sent you to me, is that it?
<ce>       How wild! I didn't think he knew I existed!" She blushes
<ce>         momentarily before regaining some composure. "Well,
<ce>     yes, I'm certain we have things to discuss. I'm sure I could
<ce>         arrange a nice outing to discuss politics with you!"
<ce>           She then whispers, "Too many ears and eyes here.
<ce>               Meet me at ___shrine_, out in Wayrest."
<ce>
<ce>    She then gathers up her things and announces she's heading out
<ce>  for a while. Prince Helseth glares at you from across the gallery
<ce>                       with a hint of jealousy.

Message:  1011
<ce>          Karethys is waiting for you in this open clearing.
<ce>         She waves at the shrine attendant, who turns a blind
<ce>                       eye to your discussion.
<ce>
<ce>     "Ah, so glad to be out of that stuffy castle. We Dark Elves
<ce>       were never meant to stay buried under brick and mortar.
<ce>      But yes, we have important things to talk about, don't we?
<ce>
<ce>     "Popudax was more than right. The news spread like wildfire.
<ce>       They're trying to keep it away from the populace for the
<ce>     panic it would cause, but every royal family is fully aware
<ce>    of the coming crisis. They've already sent platoons out across
<ce>    their respective regions to dig up whatever hole or cave they
<ce>  can, to see if they can find clues about how to revive or control
<ce>         Numidium. But they won't find the Totem. Know why?"
<ce>
<ce>   She reaches into her handbag and pulls out a small ebony statue.
<ce>  "Because I have it right here! See?" She puts it in your hands and
<ce> lets you examine it. Then she bursts into laughter. "I'm just kidding.
<ce>     That's not the Totem. Because you know what would happen if
<ce>              you actually had the Totem in your hands?
<ce>
<ce> "Armies would combine just to pry it from you. Then those armies would
<ce>massacre themselves for the slight chance they could run away with it
<ce>   themselves. The dead would rise up by the Necromancers to devour
<ce>  you and take it away. You'd never sleep, eat, or find a moment of
<ce>                peace again, until it left your hands.
<ce>
<ce> "So think about it. Finding the Totem is one thing, but going around
<ce> with it? That's another. You don't just walk from one city to another, let
<ce>alone across borders, with the key to world domination in your hands.
<ce>    So you've got to find some way to transport it without moving.
<ce>                        Are you thinking yet?"

Message:  1012
<ce>   The sound of clattering bones surprises you. Something unnatural
<ce>      is moving nearby, and it's coming closer. You and Karethys
<ce>        cut your conversation short as you look around for the
<ce>                            interruption.
<ce>
<ce>        A cloaked figure is approaching. It doesn't take long
<ce>         to realize it has no skin. Its finger bones clank on
<ce>         one another, its legs rattle, and its jaw clatters.
<ce>                At a short distance, it speaks to you.
<ce>
<ce>    "Hello, young ones! Forgive me for breaking up your play date.
<ce>        I merely heard the word 'Totem' and couldn't restrain
<ce>                my curiosity. Mind if I have a word?"
<ce>
<ce>       Karethys's eyes go wide. She starts to cast a spell, but
<ce>                   the undead figure speaks first.
<ce>
<ce>        "Don't waste your time. It wouldn't be me you destroy.
<ce>         This lich? The slightest representation of my power.
<ce>       For I am the King of Worms, and wherever there are dead,
<ce>      I have ears to hear. Now then, don't let me interrupt you.
<ce>          Tell me about this Totem. I'd love to hear more."
<ce>
<ce>         Your companion shouts back, "We don't know anything!
<ce>      We don't have it or know where it is!" The voice retorts,
<ce>     "I don't expect you to have it. And I already know more than
<ce>    you do about it. So have you really said everything you know?"
<ce>
<ce>      The lich gestures, and four bodies rise up from the earth.
<ce>
<ce>       "I'm serious! We don't know anything!" Karethys screams.
<ce>
<ce>      "Good. Then I'll make sure you stay that way." The undead
<ce>         servants lurch forward to attack. Karethys begins to
<ce>            cast a spell. It may take some time to finish.

Message:  1013
<ce>               "There! It's done!" Karethys shouts, and
<ce>               some figures appear to aid in the fight.

Message:  1014
%qdt:
 Karethys, court sorceress of Wayrest,
 has asked me to leave the castle
 and go to ___shrine_ to talk privately
 as Popudax suggested.

--while visiting MQ towns, get letters from totem holders
-to wine and dine you or offer other incentives.

QBN:
Person _kareth_ named Karethys atHome

Foe _zomb1_ is Skeletal_warrior
Foe _zomb2_ is Skeletal_warrior
Foe _zomb3_ is Zombie
Foe _zomb4_ is Zombie
Foe _lich_ is Lich
Foe _garg1_ is Gargoyle
Foe _garg2_ is Gargoyle
Foe _garg3_ is Gargoyle
Foe _garg4_ is Gargoyle
Foe _imp1_ is Imp
Foe _imp2_ is Imp
Foe _imp3_ is Imp
Foe _imp4_ is Imp

Place _shrine_ remote cult
Place _scourg_ permanent ScourgBarrowEntry

Clock _delay_ 0:02 0:02
Clock _fight_ 1:00

--	Quest start-up:
    say 1010
    log 1014 step 0

_arrival_ task:
    when pc enters cult
    say 1011
    say 1012
    add _kareth_ face
    start timer _delay_
    create foe _zomb1_ every 0 minutes 1 times with 100% success
    create foe _zomb2_ every 0 minutes 1 times with 100% success
    create foe _zomb3_ every 0 minutes 1 times with 100% success
    create foe _zomb4_ every 0 minutes 1 times with 100% success
    create foe _lich_ every 0 minutes 1 times with 100% success
    cast Wizard_Rend spell on _lich_
    restrain foe _lich_
    change foe _zomb1_ infighting true
    change foe _zomb2_ infighting true
    change foe _zomb3_ infighting true
    change foe _zomb4_ infighting true
    change foe _lich_ team 1

_level6_ task:
    level 6 completed

_level4_ task:
    level 4 completed

_delay_ task:
    say 1013
    start timer _fight_

_spawn1_ task:
    when _delay_ and _level6_
    create foe _imp1_ every 0 minutes 1 times with 100% success
    create foe _imp2_ every 0 minutes 1 times with 100% success
    change foe _imp1_ team 1
    change foe _imp2_ team 1
    change foe _imp1_ infighting true
    change foe _imp2_ infighting true
    create foe _imp3_ every 0 minutes 1 times with 100% success
    create foe _imp4_ every 0 minutes 1 times with 100% success
    change foe _imp3_ team 1
    change foe _imp4_ team 1
    change foe _imp3_ infighting true
    change foe _imp4_ infighting true

_spawn2_ task:
    when _delay_ and _level4_ and not _level6_
    create foe _garg1_ every 0 minutes 1 times with 100% success
    create foe _garg2_ every 0 minutes 1 times with 100% success
    change foe _garg1_ team 1
    change foe _garg2_ team 1
    change foe _garg1_ infighting true
    change foe _garg2_ infighting true
    create foe _imp1_ every 0 minutes 1 times with 100% success
    create foe _imp2_ every 0 minutes 1 times with 100% success
    change foe _imp1_ team 1
    change foe _imp2_ team 1
    change foe _imp1_ infighting true
    change foe _imp2_ infighting true

_spawn3_ task:
    when _delay_ and not _level4_
    create foe _garg1_ every 0 minutes 1 times with 100% success
    create foe _garg2_ every 0 minutes 1 times with 100% success
    change foe _garg1_ team 1
    change foe _garg2_ team 1
    change foe _garg1_ infighting true
    change foe _garg2_ infighting true
    create foe _garg3_ every 0 minutes 1 times with 100% success
    create foe _garg4_ every 0 minutes 1 times with 100% success
    change foe _garg3_ team 1
    change foe _garg4_ team 1
    change foe _garg3_ infighting true
    change foe _garg4_ infighting true

_slain1_ task:
    killed _zomb1_

_slain2_ task:
    killed _zomb2_

_slain3_ task:
    killed _zomb3_

_slain4_ task:
    killed _zomb4_

_killall_ task:
    when _slain1_ and _slain2_ and _slain3_ and _slain4_
    start task _final_

_fight_ task:
    remove foe _zomb1_
    remove foe _zomb2_
    remove foe _zomb3_
    remove foe _zomb4_
    start task _final_

_final_ task:
    start quest ANMQ03WOOD
    reveal _scourg_
    remove foe _lich_
    restrain foe _garg1_
    restrain foe _garg2_
    restrain foe _garg3_
    restrain foe _garg4_
    restrain foe _imp1_
    restrain foe _imp2_
    restrain foe _imp3_
    restrain foe _imp4_
    drop _kareth_ face
    give pc nothing
    end quest
